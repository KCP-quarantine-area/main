
pkgname=librsvg
pkgver=2.40.12
pkgrel=1
pkgdesc="SAX-based renderer for SVG files into a GdkPixbuf"
arch=('x86_64')
url="https://live.gnome.org/LibRsvg"
license=('LGPL')
depends=('gdk-pixbuf2' 'pango' 'libcroco')
makedepends=('intltool' 'gobject-introspection')
optdepends=('python: rsvg commandline utility')
options=('!libtool')
install=librsvg.install
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.40/${pkgname}-${pkgver}.tar.xz")
md5sums=('6eb2d1b21676310db4d7569a5e791f84')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  ./configure --prefix=/usr \
              --libexecdir=/usr/lib/${pkgname} \
              --disable-static 
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make DESTDIR="${pkgdir}" install 
}
