
pkgbase="llvm"
pkgname=('llvm' 'clang' 'clang-analyzer')
pkgver=3.6.0
subver=3.6.0
_gcc_ver=4.9.2
pkgrel=1
arch=('x86_64')
url="http://llvm.org/"
license=('custom:University of Illinois/NCSA Open Source License')
makedepends=('gcc-libs' 'libffi' 'python2' 'ocaml' "gcc=${_gcc_ver}" 'groff')
source=("http://llvm.org/releases/${pkgver}/${pkgbase}-${pkgver}.src.tar.xz"
        "http://llvm.org/releases/$pkgver/cfe-$pkgver.src.tar.xz"
        "http://llvm.org/releases/$pkgver/compiler-rt-$pkgver.src.tar.xz")
md5sums=('f1e14e949f8df3047c59816c55278cec'
         'e3012065543dc6ab8a9842b09616b78d'
         'cc36dbcafe43406083e98bc9e74f8054')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}.src"

  # http://llvm.org/bugs/show_bug.cgi?id=4840
  mv "${srcdir}/cfe-${pkgver}.src" tools/clang

  mv "$srcdir/compiler-rt-$pkgver.src" projects/compiler-rt

  # doc directories
  sed -i 's:\$(PROJ_prefix)/docs/llvm:$(PROJ_prefix)/share/doc/llvm:' \
      Makefile.config.in

  _optimized_switch="enable"
  [ "$(check_option strip)" == 'n' ] && _optimized_switch="disable"

  # location libffi headers 
  export CPPFLAGS="${CPPFLAGS} $(pkg-config --cflags libffi)"

  _cxx_headers="/usr/include/c++/${_gcc_ver}"
  
  CC=gcc CXX=g++ \
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-shared \
              --enable-libffi \
              --enable-targets=all \
              --disable-expensive-checks \
              --disable-debug-runtime \
              --disable-assertions \
              --with-binutils-include=/usr/include \
              "--${_optimized_switch}-optimized"

  make REQUIRES_RTTI=1
}

package_llvm() {
pkgdesc="The LLVM Core libraries provide a modern source- and target-independent optimizer, along with code generation support for many popular CPUs."
depends=('perl' 'libffi')
conflicts=('llvm-ocaml')
replaces=('llvm-ocaml')

  cd "${srcdir}/${pkgbase}-${pkgver}.src"

  # https://github.com/mxcl/homebrew/issues/8947#issuecomment-3256464
  sed -e 's:\$(ObjDir)/META.llvm:\$(OcamlDir)/META.llvm:' \
      -i bindings/ocaml/llvm/Makefile

  # Move the clang directory 
  mv tools/clang "${srcdir}"
  # -j1, stop race conditions 
  make -j1 DESTDIR="${pkgdir}" install
  mv "${srcdir}/clang" tools

  # Move OCaml bindings 
  #rm -rf "${srcdir}"/{ocaml,ocamldoc}
  #mv "${pkgdir}"/usr/{lib/ocaml,share/doc/llvm/ocamldoc} "${srcdir}"

  # Remove duplicate files 
  #rm "${pkgdir}"/usr/{lib/libllvm*,share/doc/llvm/ocamldoc.tar.gz}

  # Fix permissions of static libs
  chmod -x "${pkgdir}"/usr/lib/*.a
  # Remove execute bit from static libraries
  #chmod -x "${pkgdir}"/usr/lib/ocaml/libllvm*.a

  rm "${pkgdir}"/usr/lib/*LLVMHello.*

  install -D -m644 LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_clang() {
pkgdesc="Clang is an \"LLVM native\" C/C++/Objective-C compiler, which aims to deliver amazingly fast compiles, extremely useful error and warning messages and to provide a platform for building great source level tools."
url="http://clang.llvm.org/"
# http://old.nabble.com/%22clang--v%22-shows-a-GCC-call-td28378453.html
depends=("llvm=${pkgver}-${pkgrel}" "gcc=${_gcc_ver}")

  # Installation path docs
  sed -e 's:$(PROJ_prefix)/share/doc/llvm:$(PROJ_prefix)/share/doc/clang:' \
      -i "${srcdir}/${pkgbase}-${pkgver}.src/Makefile.config"

  cd "${srcdir}/${pkgbase}-${pkgver}.src/tools/clang"
  make DESTDIR="${pkgdir}" install

  chmod -x "${pkgdir}"/usr/lib/*.a

  # Revert the path change in case of repackage
  sed -e 's:$(PROJ_prefix)/share/doc/clang:$(PROJ_prefix)/share/doc/llvm:' \
      -i "${srcdir}/${pkgbase}-${pkgver}.src/Makefile.config"

  install -D -m644 LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_clang-analyzer() {
pkgdesc="The Clang Static Analyzer is a tool that automatically finds bugs in your code, and is a great example of the sort of tool that can be built using the Clang frontend as a library to parse C/C++ code."
url="http://clang-analyzer.llvm.org/"
depends=("clang=${pkgver}-${pkgrel}" 'python2')

  cd "${srcdir}/${pkgbase}-${pkgver}.src/tools/clang"

  install -d "${pkgdir}"/usr/{bin,lib/clang-analyzer}
  for _tool in scan-{build,view}; do
    cp -r "tools/${_tool}" "${pkgdir}/usr/lib/clang-analyzer"
    ln -s "/usr/lib/clang-analyzer/${_tool}/${_tool}" "${pkgdir}/usr/bin"
  done

  # Python scripts
  python2 -m compileall "${pkgdir}/usr/lib/clang-analyzer"
  python2 -O -m compileall "${pkgdir}/usr/lib/clang-analyzer"

  install -D -m644 LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

