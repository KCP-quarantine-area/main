
pkgname=libxss
pkgver=1.2.2
pkgrel=2
pkgdesc="X11 Screen Saver extension library"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxext' 'scrnsaverproto')
makedepends=('xorg-util-macros')
options=('!libtool')
source=("http://xorg.freedesktop.org/releases/individual/lib/libXScrnSaver-${pkgver}.tar.bz2")
md5sums=('7a773b16165e39e938650bcc9027c1d5')

build() {
  cd "${srcdir}/libXScrnSaver-${pkgver}"
  
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/libXScrnSaver-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
