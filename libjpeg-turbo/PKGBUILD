
pkgname=libjpeg-turbo
pkgver=1.3.0
pkgrel=2
pkgdesc="libjpeg derivative with accelerated baseline JPEG compression and decompression"
arch=('x86_64')
url="http://libjpeg-turbo.virtualgl.org/"
license=('GPL' 'custom')
makedepends=('nasm')
provides=('libjpeg=8.0.2' 'libjpeg6')
replaces=('libjpeg' 'libjpeg6')
conflicts=('libjpeg' 'libjpeg6')
options=('!libtool')
source=("http://sourceforge.net/projects/$pkgname/files/$pkgver/$pkgname-$pkgver.tar.gz"
        'cve-2013-6629.patch')
md5sums=('e1e65cc711a1ade1322c06ad4a647741'
         '16a4f154584712c201a781350d377ec4')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  # Security issue https://bugzilla.redhat.com/show_bug.cgi?id=1031734
  patch -p0 -i $srcdir/cve-2013-6629.patch

  # create libjpeg6
  ./configure --prefix=/usr --with-jpeg6 --mandir=/usr/share/man
  make
  mkdir -p $srcdir/libjpeg6
  cp -d .libs/libjpeg.so.6* $srcdir/libjpeg6
  rm -R .libs
  # create libjpeg8
  ./configure --prefix=/usr --with-jpeg8 --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # install libjpeg6
  mkdir -p $pkgdir/usr/lib
  cp -vd $srcdir/libjpeg6/libjpeg.so.6* $pkgdir/usr/lib
  
  # install libjpeg8
  make DESTDIR="$pkgdir/" install

  # provide jpegint.h as it is required by various software
  install -m644 jpegint.h $pkgdir/usr/include/

  install -Dm644 README-turbo.txt $pkgdir/usr/share/licenses/libjpeg-turbo/README-turbo.txt
  install -Dm644 README $pkgdir/usr/share/licenses/libjpeg-turbo/README
}
