 
pkgname=lpsolve
_pkgname=lp_solve
pkgver=5.5.2.0
_pkgver=5.5
pkgrel=3
pkgdesc="Mixed Integer Linear Programming (MILP) solver"
arch=('x86_64')
url="http://lpsolve.sourceforge.net/"
license=('GPL')
depends=('glibc')
source=("http://downloads.sourceforge.net/project/lpsolve/lpsolve/${pkgver}/${_pkgname}_${pkgver}_source.tar.gz"
        'cflags.patch')
md5sums=('167c0fb4ab178e0b7ab50bf0a635a836'
         '7403f745d06619f59f52800b655d8751')

build() {
  cd "${srcdir}/${_pkgname}_${_pkgver}"
  patch -p1 -i $srcdir/cflags.patch

  cd lpsolve55
  sh -x ccc
  rm bin/ux*/liblpsolve55.a
  cd ../lp_solve
  sh -x ccc
}

package() {
  cd "${srcdir}/${_pkgname}_${_pkgver}"

 install -d ${pkgdir}/usr/{bin,lib,include/lpsolve}
 install -m 755 lp_solve/bin/ux*/lp_solve ${pkgdir}/usr/bin/
 install -m 755 lpsolve55/bin/ux*/liblpsolve55.so ${pkgdir}/usr/lib/
 install -m 644 lp*.h ${pkgdir}/usr/include/lpsolve/
}
