
pkgname=pygtk
pkgver=2.24.0
pkgrel=1
pkgdesc="Python bindings for the GTK widget set"
arch=('x86_64')
license=('LGPL')
depends=('libglade' 'python2-cairo' 'python2-gobject')
makedepends=('python2-numpy')
optdepends=('python2-numpy')
options=('!libtool')
url="http://www.pygtk.org/"
source=("http://ftp.gnome.org/pub/gnome/sources/pygtk/2.24/${pkgname}-${pkgver}.tar.bz2"
        'python27.patch')
md5sums=('a1051d5794fd7696d3c1af6422d17a49'
         '12acfacd26f19c504a0a2d0edeb66121')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  #https://bugzilla.gnome.org/show_bug.cgi?id=623965
  patch -p1 -i "${srcdir}/python27.patch"
  
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -m644 gtk/gtk-extrafuncs.defs "${pkgdir}/usr/share/pygtk/2.0/defs/"
  sed -i -e 's#env python$#env python2#' "${pkgdir}"/usr/lib/pygtk/2.0/{,demos/}*.py
}
