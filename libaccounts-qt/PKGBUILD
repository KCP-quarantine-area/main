
pkgbase=libaccounts-qt
pkgname=('libaccounts-qt' 'libaccounts-qt-doc')
pkgver=1.10
pkgrel=1
arch=('x86_64')
url="http://code.google.com/p/accounts-sso/"
license=('LGPL')
makedepends=('qt' 'libaccounts-glib' 'doxygen')
source=("http://accounts-sso.googlecode.com/files/accounts-qt-$pkgver.tar.bz2")
md5sums=('db43bcca9554ba5b1108d17ac8ecfed0')

build() {
  cd accounts-qt-$pkgver
  sed -i 's|SUBDIRS  += Accounts tests|SUBDIRS += Accounts|' accounts-qt.pro
  
  qmake PREFIX=/usr LIBDIR=/usr/lib
  make
}

package_libaccounts-qt() {
  pkgdesc="Qt-based client library for accessing the online accounts database"
  depends=('qt' 'libaccounts-glib')

  cd accounts-qt-$pkgver
  make INSTALL_ROOT="$pkgdir" install_subtargets
}

package_libaccounts-qt-doc() {
  pkgdesc="Developer documention of accounts-qt"
  
  cd accounts-qt-$pkgver
  
  make INSTALL_ROOT="$pkgdir" install_documentation
}
