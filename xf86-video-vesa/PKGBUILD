
pkgname=xf86-video-vesa
pkgver=2.3.2
pkgrel=2
pkgdesc="X.org vesa video driver"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('glibc')
makedepends=('pkg-config' 'xorg-server>=1.14' 'resourceproto' 'scrnsaverproto')
conflicts=('xorg-server<1.12.3')
groups=('xorg' 'xorg-video-drivers')
options=('!libtool')
source=("${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2"
        'revert-kernelcheck.patch'
        'git-fixes.patch')
md5sums=('3eddd393fba79550e012d717499d58ad'
         '9a6a2c5fe01b96536f67495359c4bcf1'
         'dd02800ec67514447f73878d142560a8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  patch -p1 -R -i "${srcdir}/revert-kernelcheck.patch"
  patch -p1 -i "${srcdir}/git-fixes.patch"

  ./configure --prefix=/usr 
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install 
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/" 
}
