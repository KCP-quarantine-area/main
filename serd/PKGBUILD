
pkgname=serd
pkgver=0.20.0
pkgrel=1
pkgdesc="A lightweight C library for RDF syntax which supports reading and writing Turtle and NTriples."
arch=('x86_64')
url="http://drobilla.net/software/$pkgname/"
license=('custom:ISC')
depends=('glibc')
makedepends=('python2')
source=("http://download.drobilla.net/$pkgname-$pkgver.tar.bz2")
md5sums=('cea0f240a6c94f3a7644a35d2f60ed8c')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i "/ldconfig/d" wscript

  python2 ./waf configure --prefix=/usr \
                          --mandir=/usr/share/man
  python2 ./waf
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  DESTDIR="$pkgdir" python2 ./waf install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

