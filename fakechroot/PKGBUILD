
pkgname=fakechroot
pkgver=2.16
pkgrel=2
pkgdesc="Gives a fake chroot environment"
arch=('x86_64')
url="http://fakechroot.alioth.debian.org"
license=('LGPL')
options=('!libtool')
install=fakechroot.install
source=("http://ftp.debian.org/debian/pool/main/f/${pkgname}/${pkgname}_${pkgver}.orig.tar.gz")
md5sums=('3ea5efb76664e787450e0cfcefc1b807')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  sed -i "s#usr/sbin/chroot#usr/bin/chroot#" test/testtree.sh test/t/chroot.t
  
  ./configure --prefix=/usr --libdir=/usr/lib/libfakeroot --sysconfdir=/etc
  make 
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -dm755 ${pkgdir}/etc/ld.so.conf.d/
  echo '/usr/lib/libfakeroot/fakechroot/' > ${pkgdir}/etc/ld.so.conf.d/fakechroot.conf
}
