
pkgname=fakechroot
pkgver=2.17.1
pkgrel=2
pkgdesc="Gives a fake chroot environment"
arch=('x86_64')
url="http://fakechroot.alioth.debian.org"
license=('LGPL')
options=('!libtool')
install=fakechroot.install
source=("http://ftp.debian.org/debian/pool/main/f/${pkgname}/${pkgname}_${pkgver}.orig.tar.gz"
        'chroot-path.patch')
md5sums=('3c17eff0c854a77f98b4d0db086bc5bc'
         'c80473c388bdc1e5d6af5f6485556508')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/chroot-path.patch
  
  ./configure --prefix=/usr --libdir=/usr/lib/libfakeroot --sysconfdir=/etc
  make 
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  make -j1 check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -dm755 ${pkgdir}/etc/ld.so.conf.d/
  echo '/usr/lib/libfakeroot/fakechroot/' > ${pkgdir}/etc/ld.so.conf.d/fakechroot.conf
}
