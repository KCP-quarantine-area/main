
pkgname=xf86-video-vmware
pkgver=13.0.1
pkgrel=2
pkgdesc="X.org vmware video driver"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libdrm' 'mesa' 'llvm')
makedepends=('xorg-server' 'resourceproto' 'scrnsaverproto')
conflicts=('xorg-server<1.12.3')
groups=('xorg-video-drivers')
options=('!libtool' '!emptydirs')
source=("${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2"
        'mesa10.patch')
md5sums=('b08e0195ebf3f88a82129322cb93da08'
         'db4763717d4f5286d6a7661feadcb2ed')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}"/mesa10.patch

  ./configure --prefix=/usr --enable-vmwarectrl-client
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
