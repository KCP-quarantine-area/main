
pkgname=imlib2
pkgver=1.4.5
pkgrel=4
pkgdesc="Library that does image file loading and saving as well as rendering, manipulation, arbitrary polygon support"
url="http://sourceforge.net/projects/enlightenment/"
arch=('x86_64')
license=('BSD')
depends=('libtiff' 'giflib' 'bzip2' 'freetype2' 'libxext' 'libpng>=1.5.0' 'libid3tag' 'libjpeg-turbo')
options=('!libtool')
source=("http://downloads.sourceforge.net/enlightenment/$pkgname-$pkgver.tar.bz2"
        'giflib5.patch')
md5sums=('859e5fede51ec819f4314eee11da3ea5'
         '31dbb6682bff23145cb41c4c90499257')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p0 -i $srcdir/giflib5.patch

  ./configure --prefix=/usr \
              --sysconfdir=/etc/imlib2 \
              --x-libraries=/usr/lib \
              --enable-amd64
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver" 
  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
