
pkgname=imlib2
pkgver=1.4.6
pkgrel=2
pkgdesc="Library that does image file loading and saving as well as rendering, manipulation, arbitrary polygon support"
url="http://sourceforge.net/projects/enlightenment/"
arch=('x86_64')
license=('BSD')
depends=('libtiff' 'giflib' 'bzip2' 'freetype2' 'libxext' 'libpng>=1.5.0' 'libid3tag' 'libjpeg-turbo')
options=('!libtool')
source=("http://downloads.sourceforge.net/enlightenment/$pkgname-$pkgver.tar.bz2"
        'giflib51.patch')
md5sums=('5c7104121ec6db652b37f74a6d7048e2'
         'c34a57b7d8526ee17ebfc4b472867930')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 -i $srcdir/giflib51.patch

  ./configure --prefix=/usr \
              --sysconfdir=/etc/imlib2 \
              --x-libraries=/usr/lib \
              --enable-amd64
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver" 
  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
