
pkgname=mcpp
pkgver=2.7.2
pkgrel=5
pkgdesc="Matsui's CPP implementation precisely conformed to standards"
arch=('x86_64')
license=('custom')
url="http://mcpp.sourceforge.net"
depends=('glibc')
options=('!libtool')
source=("http://downloads.sourceforge.net/mcpp/${pkgname}-${pkgver}.tar.gz")
md5sums=('512de48c87ab023a69250edc7a0c7b05')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  ./configure --prefix=/usr --enable-mcpplib --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -sf ../doc/${pkgname}/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/"
}
