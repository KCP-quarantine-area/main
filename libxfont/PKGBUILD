
pkgname=libxfont
pkgver=1.4.6
pkgrel=1
pkgdesc="X11 font rasterisation library"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('freetype2' 'libfontenc' 'xproto' 'fontsproto')
makedepends=('xorg-util-macros' 'xtrans')
options=('!libtool')
source=("${url}/archive/individual/lib/libXfont-${pkgver}.tar.bz2")
md5sums=('351a9b7348d165029bda52c9fdcb5c7a')

build() {
  cd "${srcdir}/libXfont-${pkgver}"
  
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/libXfont-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
