
pkgname=giflib
pkgver=5.0.6
pkgrel=1
pkgdesc="A library for reading and writing gif images"
url="http://sourceforge.net/projects/giflib/"
arch=('x86_64')
license=('MIT')
depends=('libx11' 'libsm')
makedepends=('xmlto' 'docbook-xml' 'docbook-xsl')
options=('!libtool')
source=("http://downloads.sourceforge.net/sourceforge/giflib/${pkgname}-${pkgver}.tar.bz2"
        'stdeff.patch')
md5sums=('603838feeed62e9eaa90415742adddf9'
         '93986c1b831179f4e9ec675bc1cf203d')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i $srcdir/stdeff.patch
  
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
