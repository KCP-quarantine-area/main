
pkgbase=libsigc++
pkgname=('libsigc++' 'libsigc++-docs')
pkgver=2.3.1
pkgrel=1
arch=('x86_64')
license=('LGPL')
url="http://libsigc.sourceforge.net/"
source=("http://ftp.gnome.org/pub/GNOME/sources/libsigc++/2.3/libsigc++-${pkgver}.tar.xz")
makedepends=('gcc-libs')
options=('!libtool' '!emptydirs')
md5sums=('06a0569e8ba161d3428e7daa838682f8')

build() {
  cd "${srcdir}/libsigc++-${pkgver}"
  
  ./configure --prefix=/usr 
  make 
}

package_libsigc++() {
  pkgdesc="Libsigc++ implements a full callback system for use in widget libraries - V2."
  depends=('gcc-libs')

  cd "${srcdir}/libsigc++-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile 
  make DESTDIR="${pkgdir}" install 
}

package_libsigc++-docs() {
  pkgdesc="Developer documentation for libsigc++"
  provides=("libsigc++2.0-docs=${pkgver}")
  replaces=('libsigc++2.0-docs')
  conflicts=('libsigc++2.0-docs')

  cd "${srcdir}/libsigc++-${pkgver}/docs"
  make DESTDIR="${pkgdir}" install 
}

