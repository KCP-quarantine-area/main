
# Find the kernel name inside the chroot
_extramodules=extramodules-4.0
_kver="$(cat /lib/modules/${_extramodules}/version)"

pkgname=broadcom-wl
pkgver=6.30.223.248
_pkgver=6_30_223_248
pkgrel=8
pkgdesc='Broadcom 802.11abgn hybrid Linux networking device driver'
arch=('x86_64')
url='http://www.broadcom.com/support/802.11/linux_sta.php'
license=('custom')
depends=('linux>=4.0' 'linux<4.1')
makedepends=('linux-headers')

source=("http://www.broadcom.com/docs/linux_sta/hybrid-v35_64-nodebug-pcoem-${_pkgver}.tar.gz"
        'modprobe.d'
        'license.patch'
        #'user-ioctl.patch'
        'linux-recent.patch'
        'gcc.patch')
md5sums=('0237917f75d121589ec16a44eac5f5b0'
         '3600df7db49c759c655bc6a7789b28d4'
         '52f9dcef8112f8e325d9236e5290cb88'
         '31a14883407e63d821adcc06a0aac81d'
         'cded58315b4fde54044a5a0d90ff8148')

backup=('etc/modprobe.d/broadcom-wl.conf')
install=install

build() {
	cd "${srcdir}"

	patch -p1 -i license.patch
	patch -p1 -i linux-recent.patch
	patch -p1 -i gcc.patch

	make -C /lib/modules/"${_kver}"/build M=`pwd`
}

package() {
	cd "${srcdir}"

	install -D -m 755 wl.ko "${pkgdir}/lib/modules/${_extramodules}/wl.ko"
	gzip "${pkgdir}/lib/modules/${_extramodules}/wl.ko"

	install -D -m 644 lib/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -D -m 644 modprobe.d "${pkgdir}"/etc/modprobe.d/broadcom-wl.conf
}
