
# Find the kernel name inside the chroot
_extramodules=extramodules-3.14
_kver="$(cat /lib/modules/${_extramodules}/version)"

pkgname=broadcom-wl
pkgver=6.30.223.141
pkgrel=3
pkgdesc='Broadcom 802.11abgn hybrid Linux networking device driver'
arch=('x86_64')
url='http://www.broadcom.com/support/802.11/linux_sta.php'
license=('custom')
depends=('linux>=3.14' 'linux<3.15')
makedepends=('linux-headers')

source=("http://www.broadcom.com/docs/linux_sta/hybrid-v35_64-nodebug-pcoem-${pkgver//./_}.tar.gz"
        'modprobe.d'
        'license.patch'
        #'user-ioctl.patch'
        'linux-recent.patch')
md5sums=('039f33d2a3ff2890e42717092d1eb0c4'
         '9e27f32fc90bf7b3045117edb9a58d65'
         '52f9dcef8112f8e325d9236e5290cb88'
         'e525df93e028563654e7af08d2dd9168')

backup=('etc/modprobe.d/broadcom-wl.conf')
install=install

build() {
	cd "${srcdir}"

	patch -p1 -i license.patch
	patch -p1 -i linux-recent.patch
	#patch -p1 -i user-ioctl.patch

	make -C /lib/modules/"${_kver}"/build M=`pwd`
}

package() {
	cd "${srcdir}"

	install -D -m 755 wl.ko "${pkgdir}/lib/modules/${_extramodules}/wl.ko"
	gzip "${pkgdir}/lib/modules/${_extramodules}/wl.ko"

	install -D -m 644 lib/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -D -m 644 modprobe.d "${pkgdir}"/etc/modprobe.d/broadcom-wl.conf
}
