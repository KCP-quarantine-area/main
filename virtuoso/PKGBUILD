
pkgname=virtuoso
pkgver=6.1.8
pkgrel=1
arch=('x86_64')
pkgdesc='A scalable cross-platform server that combines SQL/RDF/XML Data Management with Web Application Server and Web Services Platform functionality'
url='http://virtuoso.openlinksw.com/wiki/main/Main/'
license=('GPL')
depends=('libldap')
makedepends=('bison' 'flex' 'gperf' 'gawk' 'm4' 'openssl' 'zlib' 'python2' 'net-tools')
options=('!libtool')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-opensource-${pkgver}.tar.gz")
md5sums=('94ef4f93cad915f73bee547fe95db725')

build() {
  cd ${srcdir}/virtuoso-opensource-${pkgver}

  ./configure --with-layout=debian \
    --program-transform-name='s/isql$$/isql-vt/;s/isqlw/isqlw-vt/' \
    --with-readline 
  make
}

package() {
  # install server
  cd ${srcdir}/virtuoso-opensource-${pkgver}/binsrc/virtuoso
  make DESTDIR=${pkgdir} install

  # install driver
  cd ${srcdir}/virtuoso-opensource-${pkgver}/binsrc/driver
  make DESTDIR=${pkgdir} install

  # Install some useful tools; rename to avoid conflicts
  for bin in isql isqlw; do
    install -Dm755 ${srcdir}/virtuoso-opensource-${pkgver}/binsrc/tests/$bin \
      ${pkgdir}/usr/bin/$bin-vt
  done
}
