
pkgname=fuse
pkgver=2.9.5
_pkgver=2_9_5
pkgrel=1
pkgdesc="A library that makes it possible to implement a filesystem in a userspace program."
arch=('x86_64')
url="https://github.com/libfuse/libfuse"
license="GPL2"
depends=('glibc')
makedepends=('pkgconfig')
backup=('etc/fuse.conf')
options=('!libtool')
source=("https://github.com/libfuse/libfuse/releases/download/fuse_${_pkgver}/${pkgname}-${pkgver}.tar.gz"
        'fuse.conf')
md5sums=('c901b77a1c4584c7ac6c2b67c0713f2b'
         'c9457cf5b2196da67d5ac816d1c86a4f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
        --libdir=/usr/lib \
        --enable-lib \
        --enable-util \
        --bindir=/bin

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make install DESTDIR="${pkgdir}"

  # static device nodes will cause collision.
  rm -rf "${pkgdir}/dev"

  # Remove init script in wrong path
  rm -rf "${pkgdir}/etc/init.d"

  # install sample config file
  install -D -m644 "${srcdir}/fuse.conf" "${pkgdir}/etc/fuse.conf"

  # remove udev rules (is in the udev package}
  rm -rf ${pkgdir}/etc/udev
}

