
pkgname=pulseaudio
pkgdesc="A featureful, general-purpose sound server"
pkgver=4.99.4
pkgrel=1
arch=('x86_64')
url="http://pulseaudio.org/"
license=('GPL' 'LGPL')
depends=('dbus' 'xcb-util' 'libasyncns' 'libcap' 'libxtst' 'libsm'
         'libsamplerate' 'json-c' 'rtkit' 'libltdl' 'speex' 'tdb'
         'fftw' 'orc' 'webrtc-audio-processing' 'systemd' 'sbc')
makedepends=('attr' 'libltdl' 'avahi' 'bluez' 'intltool' 'jack' 'lirc-utils' 'openssl'
             'libsndfile' 'pkgconfig' 'sh')
optdepends=('avahi: zeroconf support'
            'bluez: bluetooth support'
            'jack: jack support'
            'openssl: RAOP support')
backup=(etc/pulse/{daemon.conf,default.pa,system.pa})
install="pulseaudio.install"
provides=("libpulse=${pkgver}")
options=('!emptydirs' '!libtool' '!makeflags')
source=("http://freedesktop.org/software/${pkgname}/releases/${pkgname}-${pkgver}.tar.xz")
#source=("$pkgname-$pkgver::git://anongit.freedesktop.org/pulseaudio/pulseaudio#commit=9eb0bd0")
md5sums=('2fbfe082a3c6fdaeb535f5b85f86a653')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
               --sysconfdir=/etc \
               --libexecdir=/usr/lib \
               --localstatedir=/var \
               --with-udev-rules-dir=/usr/lib/udev/rules.d \
               --with-database=tdb \
               --disable-rpath \
               --disable-bluez4 \
               --disable-default-build-tests

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Disable cork-request module
  sed -e 's|/usr/bin/pactl load-module module-x11-cork-request|#&|' \
      -i "${pkgdir}/usr/bin/start-pulseaudio-x11"

  rm "${pkgdir}/etc/dbus-1/system.d/pulseaudio-system.conf"
  rm "${pkgdir}/etc/xdg/autostart/pulseaudio.desktop"
}
