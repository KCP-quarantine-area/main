
pkgname=octave
pkgver=4.0.1
pkgrel=3
pkgdesc="A high-level language, primarily intended for numerical computations."
arch=('x86_64')
url="http://www.octave.org"
license=('GPL')
depends=('fftw' 'curl' 'graphicsmagick' 'glpk' 'hdf5' 'gcc-libs' 'qhull' 'fltk'
         'arpack' 'suitesparse' 'gperf' 'glu' 'gnuplot' 'gl2ps' 'ghostscript') # 'qt' 'qscintilla'
makedepends=('texlive-core' 'gcc-fortran' 'python2')
options=('!emptydirs')
install='octave.install'
source=("ftp://ftp.gnu.org/gnu/octave/octave-$pkgver.tar.xz")
md5sums=('3e406ec2574b6acc78c92cc8c192eaf7')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  autoreconf -vfi

  ./configure --prefix=/usr \
       --libexecdir=/usr/lib \
       --enable-shared \
       --disable-static \
       --with-quantum-depth=16 \
       --with-umfpack="-lumfpack -lsuitesparseconfig" 
       #MOC=moc UIC=uic RCC=rcc

  LANG=C make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # add path to ld.so.conf.d
  install -d "${pkgdir}/etc/ld.so.conf.d"
  echo "/usr/lib/${pkgname}/${pkgver}" > "${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf"
  # remove .desktop until ported to Qt 5
  rm "${pkgdir}/usr/share/applications/www.octave.org-octave.desktop"
}
