
_extramodules=extramodules-3.10-lts
_kver=$(cat /lib/modules/${_extramodules}/version)

pkgname=nvidia-304xx-lts
pkgver=304.125
pkgrel=1
arch=('x86_64')
pkgdesc="NVIDIA drivers for linux."
url="http://www.nvidia.com/"
license=('custom')
install=nvidia.install
depends=('linux-lts>=3.10' 'linux-lts<3.11' "nvidia-304xx-utils=${pkgver}")
makedepends=('linux-lts-headers' "nvidia-304xx-utils=${pkgver}")
conflicts=('nvidia')
source=('uapi-3.7.patch'
        'conftest_new.diff')
md5sums=('42f2ad1cf9b48b1da8ed82200a045a8a'
         'd33b6506540aa3c52272b6b655d52647')

_pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"
source+=("ftp://download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/${_pkg}.run")
md5sums+=('291d41ccdbcc08f752a38f9e0a7eeea0')

build() {
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    
    cd "${_pkg}/kernel"
    
    make SYSSRC=/lib/modules/"${_kver}/build" module
}

package () {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/etc/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}

