
pkgbase=gstreamer0.10-good
pkgname=('gstreamer0.10-good' 'gstreamer0.10-good-plugins')
pkgver=0.10.31
pkgrel=8
arch=('x86_64')
license=('LGPL')
makedepends=('intltool' 'pkgconfig' 'gstreamer0.10-base>=0.10.36' 'libavc1394' 'libiec61883'
             'aalib' 'libshout' 'libdv' 'flac' 'wavpack' 'taglib' 'libsoup' 'v4l-utils'
             'libcaca' 'bzip2' 'gdk-pixbuf2' 'pulseaudio' 'jack' 'systemd')
url="http://gstreamer.freedesktop.org/"
options=('!libtool' '!emptydirs')
source=("${url}/src/gst-plugins-good/gst-plugins-good-${pkgver}.tar.xz"
        'linux-3.6.patch')
md5sums=('555845ceab722e517040bab57f9ace95'
         '50b1a8403d9a21de7d1978fc7504883c')

build() {
  cd "${srcdir}/gst-plugins-good-${pkgver}"
  patch -p1 -i "${srcdir}/linux-3.6.patch"

  sed -i '/AC_PATH_XTRA/d' configure.ac
  sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' configure.ac
  ./autogen.sh --disable-gtk-doc
  
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental \
    --disable-schemas-install \
    --disable-hal \
    --disable-gconf \
    --disable-esd \
    --with-package-name="GStreamer Good Plugins (KaOS)" \
    --with-package-origin="http://kaosx.us/"

  make 
  sed -e 's/gst sys ext/gst/' -i Makefile 
}

package_gstreamer0.10-good() {
  depends=('gstreamer0.10-base' 'bzip2')
  pkgdesc="GStreamer Multimedia Framework Good plugin libraries"
  conflicts=('gstreamer0.10-bad<=0.10.17')

  cd "${srcdir}/gst-plugins-good-${pkgver}"

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install 
  rm -rf "${pkgdir}/etc/gconf"
}

package_gstreamer0.10-good-plugins() {
  depends=("gstreamer0.10-good=${pkgver}" 'libavc1394' 'libiec61883' 'aalib' 'libshout' 'libdv' 'flac' 
	   'wavpack' 'taglib' 'libsoup' 'v4l-utils' 'libcaca' 'libpng' 'libjpeg' 'gdk-pixbuf2' 'jack' 'libpulse' 'udev')
  pkgdesc="GStreamer Multimedia Framework Good Plugins (gst-plugins-good)"
  groups=('gstreamer0.10-plugins')
  conflicts=('gstreamer0.10-pulse')
  replaces=('gstreamer0.10-pulse')
  provides=('gstreamer0.10-pulse')

  cd "${srcdir}/gst-plugins-good-${pkgver}"

  make -C sys DESTDIR="${pkgdir}" install 
  make -C ext GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install 
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  install -m644 gconf/gstreamer-0.10.schemas "${pkgdir}/usr/share/gconf/schemas/gstreamer0.10-good-plugins.schemas"
}
