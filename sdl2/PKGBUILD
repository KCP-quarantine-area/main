
pkgname=sdl2
pkgver=2.0.1
pkgrel=1
pkgdesc="Library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (Version 2)"
arch=('x86_64')
url="http://www.libsdl.org"
license=('MIT')
depends=('glibc' 'libxext' 'libxrender' 'libx11' 'libgl' 'pulseaudio' 'alsa-lib')
makedepends=( 'mesa' 'libxrandr' 'libxinerama')
source=("http://www.libsdl.org/release/SDL2-${pkgver}.tar.gz")
md5sums=('0eb97039488bf463e775295f7b18b227')

prepare() {
  mkdir build
}

build() {
  cd build
  
  ../SDL2-$pkgver/configure --prefix=/usr \
    --enable-sdl-dlopen \
    --disable-arts --disable-esd --disable-nas \
    --enable-alsa --enable-pulseaudio-shared \
    --disable-rpath
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  
  install -Dm644 ../SDL2-$pkgver/COPYING.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
