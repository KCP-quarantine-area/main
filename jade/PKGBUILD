
pkgname=jade
pkgver=1.2.1
pkgrel=8
_debpatch=47.3
pkgdesc="James Clark's DSSSL Engine"
url='http://www.jclark.com/jade/'
arch=('x86_64')
license=('custom')
depends=('gcc-libs')
options=('!libtool' '!makeflags')
source=("ftp://ftp.jclark.com/pub/jade/jade-${pkgver}.tar.gz"
        "http://ftp.debian.org/debian/pool/main/j/jade/jade_${pkgver}-${_debpatch}.diff.gz")
md5sums=('4239670ca6b578bff68b8c2e7cd1225f'
         'dc50fb2aad40ad4b26373953805fe4fc')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  export CXXFLAGS="$CXXFLAGS -fpermissive"
  
  patch -p1 -i ${srcdir}/jade_${pkgver}-${_debpatch}.diff 
  chmod 755 configure 
  
  ./configure --prefix=/usr --enable-default-catalog=/usr/share/sgml/catalog 
  make 
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  
  install -d -m755 ${pkgdir}/usr/lib
  make prefix=${pkgdir}/usr install 
  
  mv ${pkgdir}/usr/bin/sx ${pkgdir}/usr/bin/sgml2xml
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/ 
}
