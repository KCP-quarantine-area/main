
pkgname=tlp
pkgver=0.7
pkgrel=1
pkgdesc='Linux Advanced Power Management'
arch=('x86_64')
url='http://linrunner.de/en/tlp/docs/tlp-linux-advanced-power-management.html'
license=('GPL2' 'GPL3')
depends=('bash' 'hdparm' 'perl' 'rfkill' 'wireless_tools' 'networkmanager' 'smartmontools' 
         'iw' 'tp_smapi' 'acpi_call')
conflicts=('pm-utils')
backup=('etc/default/tlp')
source=("https://github.com/linrunner/TLP/archive/${pkgver}.tar.gz")
md5sums=('660ee2e507c02f11c62d70ff4ca8329c')

prepare() {
    cd TLP-${pkgver}
    sed -i -e 's|/lib/udev/tlp-usb-udev|/usr/lib/udev/tlp-usb-udev|' ${srcdir}/TLP-${pkgver}/tlp.rules
}

package() {
    cd TLP-${pkgver}
    
    export TLP_ULIB='/usr/lib/udev'
    export TLP_NO_INIT='1'
    export TLP_NO_PMUTILS='1'

    make DESTDIR="${pkgdir}" install


    install -dm 755 "${pkgdir}"/usr/lib/systemd/system
    install -m 644 tlp.service "${pkgdir}"/usr/lib/systemd/system/
    install -m 644 tlp-sleep.service "${pkgdir}"/usr/lib/systemd/system/

    install -dm 755 "${pkgdir}"/usr/share/man/man{1,8}
    install -m 644 man/*.1 "${pkgdir}"/usr/share/man/man1/
    install -m 644 man/*.8 "${pkgdir}"/usr/share/man/man8/
}

