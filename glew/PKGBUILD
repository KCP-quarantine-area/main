
pkgname=glew
pkgver=1.13.0
pkgrel=1
pkgdesc="A cross-platform C/C++ extension loading library"
arch=('x86_64')
url="http://glew.sourceforge.net"
license=('BSD' 'MIT' 'GPL')
depends=('libxmu' 'libxi' 'mesa' 'glu')
source=("http://downloads.sourceforge.net/glew/${pkgname}-${pkgver}.tgz")
md5sums=('7cbada3166d2aadfc4169c4283701066')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|lib64|lib|' config/Makefile.linux
  
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make GLEW_DEST="${pkgdir}/usr" install.all
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  rm "${pkgdir}"/usr/lib/{libGLEW,libGLEWmx}.a
  chmod 0755 "${pkgdir}"/usr/lib/libGLEW*.so.${pkgver}
}
