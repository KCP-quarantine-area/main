
pkgname=webkitgtk2
pkgver=1.10.2
pkgrel=2
arch=('x86_64')
url="http://webkitgtk.org/"
pkgdesc="GTK+ Web content engine library for GTK+ 2.0"
license=('custom')
depends=('libxt' 'libxslt' 'sqlite3' 'libsoup' 'enchant' 'libgl' 'gtk2' 'gstreamer0.10-base' 'icu')
makedepends=('gperf' 'gobject-introspection' 'python2' 'mesa' 'ruby' 'gtk-doc')
options=('!libtool' '!emptydirs')
source=("http://webkitgtk.org/releases/webkitgtk-$pkgver.tar.xz")
md5sums=('7b1a652af1eb11bee5bf7209e9ff67e6')

build() {
  cd "$srcdir/webkitgtk-$pkgver"
  
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
    --enable-introspection \
    --libexecdir=/usr/lib/webkitgtk2 \
    --with-gstreamer=0.10 \
    --with-gtk=2.0 \
    --enable-geolocation=no \
    --disable-webkit2
    
  sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make all stamp-po
}

package() {
  cd "$srcdir/webkitgtk-$pkgver"
  make -j1 DESTDIR="$pkgdir" install

  install -Dm644 Source/WebKit/LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}