
pkgname=cups-filters
pkgver=1.0.71
pkgrel=2
pkgdesc="OpenPrinting CUPS Filters"
arch=('x86_64')
url="http://www.linuxfoundation.org/collaborate/workgroups/openprinting"
license=('GPL')
depends=('lcms2' 'poppler' 'qpdf')
makedepends=('ghostscript' 'ttf-dejavu') 
optdepends=('ghostscript: for non-PostScript printers to print with CUPS to convert PostScript to raster images'
	    'foomatic-db: drivers use Ghostscript to convert PostScript to a printable form directly'
	    'foomatic-db-engine: drivers use Ghostscript to convert PostScript to a printable form directly'
	    'foomatic-db-nonfree: drivers use Ghostscript to convert PostScript to a printable form directly')
conflicts=('foomatic-filters')
provides=('foomatic-filters')
replaces=('foomatic-filters')
backup=(etc/fonts/conf.d/99pdftoopvp.conf)
options=('!libtool')
source=("http://www.openprinting.org/download/cups-filters/$pkgname-$pkgver.tar.xz"
        'poppler34.diff')
md5sums=('f1e11dfe5fa52eb65aa0bdd3a7ee0117'
         'da7b236ab95d9a58606b8878496247ba')

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 -i $srcdir/poppler34.diff
  
  ./configure --prefix=/usr  \
    --sysconfdir=/etc \
    --with-rcdir=no \
    --enable-avahi \
    --with-test-font-path=/usr/share/fonts/TTF/DejaVuSans.ttf
  make
}

check() {
  cd $srcdir/$pkgname-$pkgver
  
  make -k check
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR="$pkgdir/" install

  rm -f ${pkgdir}/usr/lib/*.a
  # systemd service
  install -Dm644 utils/cups-browsed.service ${pkgdir}/usr/lib/systemd/system/cups-browsed.service
  sed -i 's|cups.service|org.cups.cupsd.service|g' ${pkgdir}/usr/lib/systemd/system/cups-browsed.service
}
