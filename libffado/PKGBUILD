
pkgname=libffado
pkgver=2.2.1
pkgrel=4
pkgdesc="Driver for FireWire audio devices"
arch=('x86_64')
url="http://www.ffado.org/"
license=('GPL')
depends=('libiec61883' 'libavc1394' 'libsigc++' 'libxml++' 'alsa-lib' 'dbus' 'libconfig'
         'dbus-c++')
makedepends=('scons' 'jack')
source=("http://www.ffado.org/files/${pkgname}-${pkgver}.tgz"
        'glibmm2.46.patch')
md5sums=('e113d828835051f835fb4a329cb0cbd4'
         '8f6ae7707c3bb018543f001ad513ba52')

prepare() {
  cd ${pkgname}-${pkgver}
  
  patch -p1 -i $srcdir/glibmm2.46.patch
}

build() {
  cd ${pkgname}-${pkgver}

  scons PREFIX=/usr ENABLE_OPTIMIZATIONS=yes UDEVDIR=/usr/lib/udev/rules.d COMPILE_FLAGS="$CFLAGS -std=gnu++11"
}

package() {
  cd ${pkgname}-${pkgver}

  scons DESTDIR=${pkgdir} WILL_DEAL_WITH_XDG_MYSELF="True" install
}

