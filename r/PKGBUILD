
pkgname=r
pkgver=3.1.2
pkgrel=1
pkgdesc="Language and environment for statistical computing and graphics"
arch=('x86_64')
license=('GPL')
url=('http://www.r-project.org/')
depends=('lapack' 'blas' 'bzip2'  'libpng' 'libjpeg' 'libtiff' 'icu'
         'ncurses' 'pcre' 'readline' 'zlib' 'perl' 'gcc-libs' 'zip' 'unzip'
         'tk' 'libxt' 'libxmu' 'pango' 'xz' 'desktop-file-utils')
makedepends=('openjdk' 'gcc-fortran')
backup=('etc/R/Makeconf' 'etc/R/Renviron' 'etc/R/ldpaths' 'etc/R/repositories')
options=('!makeflags' '!emptydirs')
source=("http://cran.r-project.org/src/base/R-3/R-${pkgver}.tar.gz"
	'r.desktop'
	'r.png'
	'R.conf')
md5sums=('3af29ec06704cbd08d4ba8d69250ae74'
         'f6d54d32e510d90c748a0d16d2abcbdc'
         '00659f39e90627fe87f1645df5d4e3a7'
         '1dfa62c812aed9642f6e4ac34999b9fe')

build() {
   cd "${srcdir}/R-${pkgver}"
   sed -i 's|$(rsharedir)/texmf|${datarootdir}/texmf|' share/Makefile.in
   sed -i 's|test ${makeinfo_version_min} -lt 7|test ${makeinfo_version_min} -lt 0|' configure
   
   ./configure  --prefix=/usr \
		--libdir=/usr/lib \
		--sysconfdir=/etc/R \
		--datarootdir=/usr/share \
		  rsharedir=/usr/share/R/ \
		  rincludedir=/usr/include/R/ \
		  rdocdir=/usr/share/doc/R/ \
                --with-x \
		--enable-R-shlib \
                --with-lapack \
		--with-blas \
                F77=gfortran \
		LIBnn=lib
   make
   
   cd src/nmath/standalone
   make shared
}

package() {
   cd "${srcdir}/R-${pkgver}"
   make -j1 DESTDIR="${pkgdir}" install
   
   cd src/nmath/standalone
   make DESTDIR="${pkgdir}" install

   #  Fixup R wrapper scripts.
   sed -i "s|${pkgdir} ||" "${pkgdir}/usr/bin/R"
   rm "${pkgdir}/usr/lib/R/bin/R"
   cd "${pkgdir}/usr/lib/R/bin"
   ln -s ../../../bin/R

  # install some freedesktop.org compatibility
  install -Dm644 "${srcdir}/r.desktop" \
	"${pkgdir}/usr/share/applications/r.desktop"
  install -Dm644 "${srcdir}/r.png" \
	"${pkgdir}/usr/share/pixmaps/r.png"

  # move the config directory to /etc and create symlinks
  install -d "${pkgdir}/etc/R"
  cd "${pkgdir}/usr/lib/R/etc"
  for i in *; do
    mv -f ${i} "${pkgdir}/etc/R"
    ln -s /etc/R/${i} ${i}
  done
  
  install -Dm644 "${srcdir}/R.conf" "${pkgdir}/etc/ld.so.conf.d/R.conf"
}
