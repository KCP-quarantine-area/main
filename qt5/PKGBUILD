
pkgbase=qt5
pkgname=('qt5' 'qt5-private-headers')
pkgver=5.2.0
_pkgver=5.2
pkgrel=4
arch=('x86_64')
url='http://qt.nokia.com/'
license=('GPL3' 'LGPL' 'FDL' 'custom')
makedepends=('libtiff' 'libpng' 'libmng' 'sqlite3' 'ca-certificates' 'glib2' 'dbus'
    'fontconfig' 'libgl' 'libsm' 'libxrandr' 'libxv' 'libxi' 'alsa-lib' 'icu'
    'xdg-utils' 'hicolor-icon-theme' 'desktop-file-utils' 'mesa' 'postgresql-libs'
    'mariadb' 'unixodbc' 'cups' 'libxinerama' 'libxcursor' 'python2' 'gperf' 'ruby'
    'xcb-util-image' 'xcb-util-keysyms' 'xcb-util-renderutil' 'xcb-util-wm' 'pulseaudio'
    'gstreamer0.10' 'gstreamer0.10-base' 'gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins'
    'gstreamer0.10-bad-plugins' 'gstreamer0.10-ffmpeg' 'gstreamer0.10-ugly-plugins' 'libxkbcommon')
options=('!libtool')
_pkgfqn="qt-everywhere-opensource-src-${pkgver}"
#_pkgfqn=qt5
source=("http://download.qt-project.org/official_releases/qt/${_pkgver}/${pkgver}/single/${_pkgfqn}.tar.xz"
        'assistant.desktop' 'designer.desktop' 'linguist.desktop' 'qtconfig.desktop'
        'youtubeview.desktop' 'flickrview.desktop' 'calculator.desktop' 'clocks.desktop'
        'samegame.desktop' 'snake.desktop' 'tic-tac-toe.desktop')
md5sums=('8f60b47ca9461831d940f579ee90517e'
         'fc211414130ab2764132e7370f8e5caa'
         '85179f5e0437514f8639957e1d8baf62'
         'f11852b97583610f3dbb669ebc3e21bc'
         '6b771c8a81dd90b45e8a79afa0e5bbfd'
         '85fc45501d232c8104cbff218d88199b'
         'f24376558f74f1f97ab236db4ca5c616'
         '6d493bd132cd20bb108cf938082013d9'
         'ce9eee942e50391eb1672725298999af'
         '73204871cc91cfa6e8ce97c07be1e72e'
         '4655d41e152196ec1ad8e1ebecf7ec08'
         'f66a2053e8e94dff4be6c7a12ada69da')

build() {
	cd $srcdir/$_pkgfqn

        unset QTDIR
        export PATH="$PWD/qtbase/bin:$PWD/qtrepotools/bin:$PATH"

	./configure -confirm-license -opensource \
		-prefix /usr \
                -bindir /usr/lib/qt5/bin \
                -headerdir /usr/include/qt5 \
                -archdatadir /usr/lib/qt5 \
                -docdir /usr/share/doc/qt5 \
                -plugindir /usr/lib/qt5/plugins \
                -importdir /usr/lib/qt5/imports \
                -qmldir /usr/lib/qt5/qml \
                -datadir /usr/share/qt5 \
                -testsdir /usr/share/qt5/tests \
                -translationdir /usr/share/qt5/translations \
                -sysconfdir /etc/xdg/qt5 \
                -examplesdir /usr/share/qt5/examples \
		-system-sqlite \
                -openssl-linked \
                -make examples \
                -optimized-qmake \
                -reduce-relocations \
                -dbus-linked \
                -no-openvg \
                -no-separate-debug-info \
                -no-gtkstyle \
                -opengl \
                -feature-menu \
                -feature-textdate \
                -feature-ftp \
                -xcursor
	 make
	 #./configure -prefix $PWD/qtbase -opensource
         #make module-qtwebkit
}

package_qt5() {
    pkgdesc='A cross-platform application and UI framework'
    depends=('libtiff' 'libpng' 'libmng' 'sqlite3' 'ca-certificates' 'glib2' 'dbus'
             'fontconfig' 'libgl' 'libsm' 'libxrandr' 'libxv' 'libxi' 'alsa-lib' 'ruby'
             'xdg-utils' 'hicolor-icon-theme' 'desktop-file-utils' 'libxinerama' 'libxcursor'
             'pulseaudio' 'gstreamer0.10' 'gstreamer0.10-base' 'gstreamer0.10-base-plugins'
             'gstreamer0.10-good-plugins' 'gstreamer0.10-bad-plugins' 'gstreamer0.10-ffmpeg'
             'gstreamer0.10-ugly-plugins' 'xcb-util-image' 'xcb-util-keysyms'
             'xcb-util-renderutil' 'xcb-util-wm' 'libxkbcommon' 'icu')
    optdepends=('postgresql-libs: PostgreSQL driver'
	  'libmysqlclient: MySQL driver'
	  'unixodbc: ODBC driver'
	  'libxinerama: Xinerama support'
	  'libxfixes: Xfixes support')
	
    cd $srcdir/$_pkgfqn
    make INSTALL_ROOT=$pkgdir install
	
    install -d ${pkgdir}/usr/share/applications
    install -m644 ${srcdir}/youtubeview.desktop ${pkgdir}/usr/share/applications/
    install -m644 ${srcdir}/flickrview.desktop ${pkgdir}/usr/share/applications/
    install -m644 ${srcdir}/calculator.desktop ${pkgdir}/usr/share/applications/
    install -m644 ${srcdir}/clocks.desktop ${pkgdir}/usr/share/applications/
    install -m644 ${srcdir}/samegame.desktop ${pkgdir}/usr/share/applications/
    install -m644 ${srcdir}/snake.desktop ${pkgdir}/usr/share/applications/
    install -m644 ${srcdir}/tic-tac-toe.desktop ${pkgdir}/usr/share/applications/
    
    mkdir -p ${pkgdir}/usr/bin
    for i in $(ls ${pkgdir}/usr/lib/qt5/bin); do
        ln -s /usr/lib/qt5/bin/${i} ${pkgdir}/usr/bin/${i}-qt5
    done
}

package_qt5-private-headers() {
    pkgdesc="Qt private headers for development"
    depends=("qt5=${pkgver}")

    install -d ${pkgdir}/usr/include/qt5/{QtCore,QtDeclarative,QtGui,QtScript}
    install -d ${pkgdir}/usr/src/qt5/{corelib,declarative,gui,script}
    
    cp -r ${srcdir}/$_pkgfqn/qtbase/include/QtCore/${pkgver}/QtCore/private/ ${pkgdir}/usr/include/qt5/QtCore/
    cp -r ${srcdir}/$_pkgfqn/qtbase/include/QtGui/${pkgver}/QtGui/private/ ${pkgdir}/usr/include/qt5/QtGui/
    cp -r ${srcdir}/$_pkgfqn/qtdeclarative/include/QtQml/${pkgver}/QtQml/private/ ${pkgdir}/usr/include/qt5/QtQml/
    cp -r ${srcdir}/$_pkgfqn/qtscript/include/QtScript/${pkgver}/QtScript/private/ ${pkgdir}/usr/include/qt5/QtScript/

    for i in corelib gui; do
      cp -r ${srcdir}/$_pkgfqn/qtbase/src/${i} ${pkgdir}/usr/src/qt5/
    done
    cp -r ${srcdir}/$_pkgfqn/qtscript/src/script ${pkgdir}/usr/src/qt5/
    cp -r ${srcdir}/$_pkgfqn/qtdeclarative/src/qml ${pkgdir}/usr/src/qt5/
}

