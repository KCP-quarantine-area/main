
pkgname=groff
pkgver=1.22.3
pkgrel=3
pkgdesc="GNU troff text-formatting system"
arch=('x86_64')
url="http://www.gnu.org/software/groff/groff.html"
license=('GPL')
depends=('perl' 'gcc-libs' 'texinfo')
makedepends=('netpbm' 'psutils' 'ghostscript')
optdepends=('netpbm: for use together with man -H command interaction in browsers'
            'psutils: for use together with man -H command interaction in browsers')
source=("ftp://ftp.gnu.org/gnu/groff/groff-$pkgver.tar.gz"
        'site.tmac'
        'gropdf.patch')
options=('!docs' '!emptydirs')
install=groff.install
md5sums=('cc825fa64bc7306a885f2fb2268d3ec5'
         'a1fedafd7863b37968d32ad9ae1d8c13'
         '96f261605bd4b31c1f0b56cf590cf459')

build() {
  cd $srcdir/$pkgname-$pkgver
  # https://savannah.gnu.org/bugs/index.php?43581
  patch -p1 -i "${srcdir}/gropdf.patch"
  
  
  ./configure --prefix=/usr --without-x
  make
}

check() {
  cd $srcdir/$pkgname-$pkgver
  
  make check
}


package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  
  # needed for xman
  ln -s eqn $pkgdir/usr/bin/geqn
  ln -s tbl $pkgdir/usr/bin/gtbl
  ln -s soelim $pkgdir/usr/bin/zsoelim
  
  # Fix some issues when encoding to utf8 man pages
  #  The output chars don't match keyboard chars...
  cat $srcdir/site.tmac >> \
    $pkgdir/usr/share/groff/site-tmac/man.local
  cat $srcdir/site.tmac >> \
    $pkgdir/usr/share/groff/site-tmac/mdoc.local
}
