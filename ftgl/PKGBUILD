
pkgname=ftgl
pkgver=2.1.3rc5
_pkgver=2.1.3-rc5
_buildver=2.1.3~rc5
pkgrel=2
pkgdesc="OpenGL library to use arbitrary fonts"
arch=('x86_64')
url="http://ftgl.wiki.sourceforge.net/"
license=('MIT')
depends=('freetype2' 'glu')
makedepends=('doxygen' 'mesa')
options=('!libtool')
source=("http://downloads.sourceforge.net/ftgl/$pkgname-$_pkgver.tar.bz2")
md5sums=('c7879018cde844059495b3029b0b6503')

build() {
  cd "$srcdir/$pkgname-${_buildver}"
  sed -i '/^SUBDIRS =/s/demo//' Makefile.in
  
  ./configure --prefix=/usr --with-pic --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-${_buildver}"
  make DESTDIR="$pkgdir" install

  install -m644 src/FT{Face,Library,Size}.h "$pkgdir/usr/include/FTGL/"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
