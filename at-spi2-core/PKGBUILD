
pkgname=at-spi2-core
pkgver=2.20.2
_pkgver=2.20
pkgrel=1
pkgdesc="Protocol definitions and daemon for D-Bus at-spi"
arch=('x86_64')
url="http://www.gnome.org"
license=('GPL2')
depends=('dbus' 'glib2' 'libxtst')
makedepends=('intltool' 'gobject-introspection')
options=('!libtool')
source=("http://download.gnome.org/sources/at-spi2-core/${_pkgver}/$pkgname-$pkgver.tar.xz")
md5sums=('f6f6303da2e72317f881703f40bf0c62')

build() {
  cd $pkgname-$pkgver

  sed -i -e '/AC_PATH_XTRA/d' configure.ac
  autoreconf --force --install

  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/at-spi2-core --disable-xevie
  make
}

package() {
  cd $pkgname-$pkgver
  
  make DESTDIR="$pkgdir" install
}

