
pkgname=libxinerama
pkgver=1.1.2
pkgrel=2
pkgdesc="X11 Xinerama extension library"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxext' 'xineramaproto')
makedepends=('xorg-util-macros')
options=(!libtool)
source=("${url}/releases/individual/lib/libXinerama-${pkgver}.tar.bz2")
md5sums=('cb45d6672c93a608f003b6404f1dd462')

build() {
  cd "${srcdir}/libXinerama-${pkgver}"
  
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/libXinerama-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
