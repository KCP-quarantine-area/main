
pkgname=xine-lib
pkgver=1.2.2
pkgrel=6
pkgdesc="A multimedia playback engine"
arch=('x86_64')
url="http://www.xine-project.org"
license=('LGPL' 'GPL')
depends=('libxvmc' 'ffmpeg' 'libxinerama')
makedepends=('wavpack' 'faad2' 'libmng' 'imagemagick' 'mesa' 'libmodplug'
             'vcdimager' 'jack' 'aalib' 'libdca' 'a52dec' 'libmad'
             'libmpcdec' 'libcaca' 'libbluray' 'libvdpau' 'pulseaudio')
optdepends=('imagemagick: for using the imagemagick plugin' \
            'jack: for using the jack plugin' \
            'vcdimager: for using the vcd plugin' \
            'mesa: for using the opengl plugin' \
            'wavpack: for using the wavpack plugin' \
            'faad2: for using the faad plugin' \
            'libmng: for using the mng plugin' \
            'aalib: for using the aalib plugin' \
            'libmodplug: for using the modplug plugin' \
            'libdca: for using the dca plugin' \
            'a52dec: for using the a52 plugin' \
            'libmad: for using the mp3 plugin' \
            'libmpcdec: for using the musepack plugin' \
            'libcaca: for using the caca plugin' \
            'libbluray: for using the bluray plugin' \
            'libvdpau: for using the VDPAU plugin'
            'pulseaudio: for using the pulse plugin')
options=('!libtool')
source=("http://downloads.sourceforge.net/sourceforge/xine/${pkgname}-${pkgver}.tar.xz"
        'ffmpeg_decoder.patch')
md5sums=('6fa70fa336f708452ce9bf311b401de2'
         'de1712b76c32d3ff3da5c645dfd442e0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "$srcdir/ffmpeg_decoder.patch"
  
  ./configure --prefix=/usr \
              --with-wavpack \
              --enable-vdpau \
              --disable-optimizations \
              --disable-gnomevfs
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make DESTDIR="${pkgdir}" install
}

