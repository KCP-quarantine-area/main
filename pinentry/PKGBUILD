
pkgname=pinentry
pkgver=0.8.3
pkgrel=1
pkgdesc="a collection of simple PIN or passphrase entry dialogs which utilize the Assuan protocol"
arch=('x86_64')
license=('GPL')
url="http://www.gnupg.org/aegypten2"
depends=('ncurses' 'libcap>=2.16')
makedepends=('qt')
optdepends=('qt: for qt4 backend')  
install=pinentry.install
source=("ftp://ftp.gnupg.org/gcrypt/$pkgname/$pkgname-$pkgver.tar.bz2"
        'window.patch')
md5sums=('2ae681cbca0d9fb774b2c90b11ebf56c'
         '450b8713fe42f1bd93311ef84767c3d8')

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 -i $srcdir/window.patch
  
  for file in qt4/*.moc; do
       /usr/bin/moc ${file/.moc/.h} > ${file}
   done
  ./configure --prefix=/usr \
	--enable-pinentry-curses \
	--disable-pinentry-gtk \
	--disable-pinentry-gtk2 \
        --disable-pinentry-qt \
        --enable-pinentry-qt4 \
	--enable-fallback-curses
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  
  make DESTDIR=${pkgdir} install
}

