
pkgname=gnuplot 
pkgver=5.0.1
pkgrel=1
pkgdesc="Plotting package which outputs to X11, PostScript, PNG, GIF, and others" 
arch=('x86_64') 
url="http://www.gnuplot.info" 
license=('custom') 
depends=('readline' 'gd' 'pango' 'libjpeg-turbo' 'qt5-svg' 'gnutls') 
makedepends=('texinfo' 'texlive-core' 'texlive-latexextra' 'qtchooser' 'qt5-tools') 
install=gnuplot.install 
source=("http://downloads.sourceforge.net/sourceforge/gnuplot/$pkgname-$pkgver.tar.gz")
md5sums=('79b4f9e203728f76b60b28bcd402d3c7')

build() {
  cd $srcdir/$pkgname-$pkgver

  # fix default source location; use the GDFONTPATH variable to modify at runtime 
  sed -i 's|/usr/X11R6/lib/X11/fonts/truetype|/usr/share/fonts/TTF|' src/variable.c
  sed -i -e 's|/usr/X11R6/lib/X11/fonts/Type1|/usr/share/fonts/Type1|' \
  -e 's|$(X11ROOT)/X11R6/lib/X11/fonts/Type1|$(X11ROOT)/usr/share/fonts/Type1|' \
  src/variable.c

  ./configure --prefix=/usr \
              --libexecdir=/usr/bin \
              --with-gihdir=/usr/share/gnuplot \
              --with-readline=gnu \
              --datadir=/usr/share \
              --without-lua \
              --with-qt=qt5
  make pkglibexecdir=/usr/bin
}


package() {
  cd $srcdir/$pkgname-$pkgver
  make pkglibexecdir=/usr/bin DESTDIR=$pkgdir install 

  install -Dm644 Copyright $pkgdir/usr/share/licenses/$pkgname/Copyright

  rm -f $pkgdir/usr/share/texmf-dist/ls-R
}
