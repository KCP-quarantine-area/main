
pkgname=graphite
pkgver=1.2.3
pkgrel=1
arch=('x86_64')
url="http://graphite.sil.org/"
pkgdesc='reimplementation of the SIL Graphite text processing engine'
license=('custom_SIL Dual license')
depends=('gcc-libs')
makedepends=('cmake' 'freetype2' 'python2')
replaces=('libgraphite' 'pango-graphite')
options=('!libtool' '!emptydirs')
source=("http://downloads.sourceforge.net/project/silgraphite/graphite2/graphite2-${pkgver}.tgz"
        'cmakepath.patch')
md5sums=('7042305e4208af4c2d5249d814ccce58'
         '00353b67941dbc30b76a43253760769e')

build() {
  cd "${srcdir}"
  # patch taken from FC/Slackware
  pushd graphite2-${pkgver}
  patch -p1 -i ${srcdir}/cmakepath.patch
  popd
  
  mkdir build
  sed -i "s:\/usr\/bin\/python:\/usr\/bin\/python2:" graphite2-${pkgver}/tests/{jsoncmp,fuzztest,defuzz,corrupt.py}
  
  cd build
  cmake -G "Unix Makefiles" ../graphite2-${pkgver} \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_BUILD_TYPE:STRING=Release \
	-DGRAPHITE2_COMPARE_RENDERER=OFF 
  make
}

check() {
  cd "${srcdir}"/build
  sed -i "s:python:python2:g" tests/CTestTestfile.cmake
  
  ctest
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="$pkgdir/" install

  mkdir -p "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 "${srcdir}"/graphite2-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/
}