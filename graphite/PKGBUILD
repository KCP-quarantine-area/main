
pkgname=graphite
pkgver=1.2.1
pkgrel=1
arch=('x86_64')
url="http://graphite.sil.org/"
pkgdesc='reimplementation of the SIL Graphite text processing engine'
license=('custom_SIL Dual license')
depends=('gcc-libs')
makedepends=('cmake' 'freetype2' 'python2')
replaces=('libgraphite' 'pango-graphite')
options=('!libtool' '!emptydirs')
source=("http://downloads.sourceforge.net/project/silgraphite/graphite2/graphite2-${pkgver}.tgz")
md5sums=('edf70ab2f7d789be219f47b1df00d67e')

build() {
  cd "${srcdir}"
  mkdir build
  sed -i "s:\/usr\/bin\/python:\/usr\/bin\/python2:" graphite2-${pkgver}/tests/{jsoncmp,fuzztest,defuzz,corrupt.py}
  
  cd build
  cmake -G "Unix Makefiles" ../graphite2-${pkgver} \
	-DCMAKE_INSTALL_PREFIX=/usr
  make
}

check() {
  cd "${srcdir}"/build
  sed -i "s:python:python2:g" tests/CTestTestfile.cmake
  
  ctest
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="$pkgdir/" install

  mkdir -p "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 "${srcdir}"/graphite2-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/
}