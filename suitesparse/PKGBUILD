
#keep at 4.2.1 until octave is at 4.1.x
pkgname=suitesparse
pkgver=4.2.1
pkgrel=1
epoch=1
pkgdesc="A collection of sparse matrix libraries"
url="http://www.cise.ufl.edu/research/sparse/SuiteSparse/"
arch=('x86_64')
depends=('blas' 'lapack')
makedepends=('gcc-fortran')
license=('GPL')
source=("http://faculty.cse.tamu.edu/davis/SuiteSparse/SuiteSparse-$pkgver.tar.gz")
md5sums=('4628df9eeae10ae5f0c486f1ac982fce')

build() {
   cd "$srcdir"/SuiteSparse
   export CFLAGS=" ${CFLAGS} -DNPARTITION"
   
   make -C SuiteSparse_config/xerbla
   make -C SuiteSparse_config
   for _lib in AMD CAMD COLAMD BTF KLU LDL CCOLAMD UMFPACK CHOLMOD CXSparse SPQR; do
      make -C ${_lib} library
   done
}


package() {
   cd "${srcdir}"/SuiteSparse
   install -dm755 "${pkgdir}"/usr/{lib,include}
   
   for _lib in SuiteSparse_config AMD CAMD COLAMD BTF KLU LDL CCOLAMD UMFPACK CHOLMOD CXSparse SPQR; do
      make -C ${_lib} INSTALL_LIB="${pkgdir}"/usr/lib INSTALL_INCLUDE="${pkgdir}"/usr/include install
   done

   chmod 644 "${pkgdir}"/usr/include/*.{h,hpp}
}