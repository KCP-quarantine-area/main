
pkgname=opencv
_realname=OpenCV
pkgver=2.4.9
pkgrel=2
pkgdesc="Open Source Computer Vision Library"
arch=('x86_64')
license=('BSD')
url="http://opencv.willowgarage.com/"
depends=('jasper' 'gstreamer0.10-base' 'openexr'
         'xine-lib' 'libdc1394' 'v4l-utils' 'ffmpeg' 'qt')
makedepends=('pkgconfig' 'cmake' 'python2-numpy' 'eigen' 'python2-sphinx' 'mesa')
optdepends=('python2-numpy: Python 2.x interface' 'eigen')
options=('!libtool')
source=("http://sourceforge.net/projects/opencvlibrary/files/opencv-unix/$pkgver/$pkgname-$pkgver.zip")
md5sums=('7f958389e71c77abdf5efe1da988b80c')

build() {
  cd "$srcdir/$pkgname-$pkgver"
 
  mkdir build 
  cd build

  cmake .. -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_SKIP_RPATH=ON \
        -DBUILD_TESTS=OFF \
        -DENABLE_SSE3=OFF \
        -DBUILD_EXAMPLES=ON \
        -DINSTALL_C_EXAMPLES=ON \
        -DINSTALL_PYTHON_EXAMPLES=ON \
        -DWITH_XINE=ON \
        -DWITH_QT=ON \
	-DWITH_OPENGL=ON \
        -DWITH_TBB=OFF \
        -DWITH_CUDA=OFF \
        -DWITH_GTK=OFF \
        -DENABLE_FAST_MATH=ON 
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/build"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/$pkgname-$pkgver/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
