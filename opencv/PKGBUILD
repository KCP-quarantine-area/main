
pkgname=opencv
_realname=OpenCV
pkgver=2.4.9
pkgrel=4
pkgdesc="Open Source Computer Vision Library"
arch=('x86_64')
license=('BSD')
url="http://opencv.willowgarage.com/"
depends=('jasper' 'gst-plugins-base' 'openexr'
         'xine-lib' 'libdc1394' 'v4l-utils' 'ffmpeg' 'qt')
makedepends=('pkgconfig' 'cmake' 'python2-numpy' 'eigen' 'python2-sphinx' 'mesa')
optdepends=('python2-numpy: Python 2.x interface' 'eigen')
options=('!libtool')
source=("http://sourceforge.net/projects/opencvlibrary/files/opencv-unix/$pkgver/$pkgname-$pkgver.zip"
        'OpenCVFindLibsVideo.cmake.diff'
        'CMakeLists.diff')
md5sums=('7f958389e71c77abdf5efe1da988b80c'
         '4cf6951adead9d012a6603fdba3a3850'
         '9241cd6cbecfc70ae4163a6f82aa26b9')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 -i "${srcdir}/OpenCVFindLibsVideo.cmake.diff"
  patch -p1 -i "${srcdir}/CMakeLists.diff"
 
  mkdir build 
  cd build

  cmake .. -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_SKIP_RPATH=ON \
        -DBUILD_TESTS=OFF \
        -DENABLE_SSE3=OFF \
        -DBUILD_EXAMPLES=ON \
        -DINSTALL_C_EXAMPLES=ON \
        -DINSTALL_PYTHON_EXAMPLES=ON \
        -DWITH_XINE=ON \
        -DWITH_QT=ON \
        -DWITH_GSTREAMER=ON \
	-DWITH_OPENGL=ON \
        -DWITH_TBB=OFF \
        -DWITH_CUDA=OFF \
        -DWITH_GTK=OFF \
        -DENABLE_FAST_MATH=ON 
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/build"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/$pkgname-$pkgver/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
