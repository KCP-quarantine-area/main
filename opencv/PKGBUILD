
pkgname=opencv
pkgver=3.0.0
_pkgver=3.0.0
pkgrel=2
pkgdesc="Open Source Computer Vision Library"
arch=('x86_64')
license=('BSD')
url="http://opencv.org/"
depends=('jasper' 'gst-plugins-base' 'openexr' 'xine-lib' 'libdc1394' 'v4l-utils' 'ffmpeg' 'qt5-base'
         'libgphoto2' 'libwebp')
makedepends=('pkgconfig' 'cmake' 'python2-numpy' 'eigen3' 'python2-sphinx' 'mesa')
optdepends=('python2-numpy: Python 2.x interface' 'eigen3') #eigen3 for 3.0.0
options=('!libtool')
conflicts=('opencv-qt5')
replaces=('opencv-qt5')
provides=('opencv-qt5')
source=("https://github.com/Itseez/opencv/archive/${_pkgver}.tar.gz"
        "opencv_contrib-${_pkgver}::https://github.com/Itseez/opencv_contrib/archive/${_pkgver}.tar.gz")
md5sums=('e6c72f54f7127161b371ef798f34d5c9'
         '2029c16ea4bf62113372a68e906fad78')

build() {
  cd "$srcdir/opencv-${_pkgver}"
 
  mkdir build 
  cd build

  cmake .. -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_SKIP_RPATH=ON \
        -DBUILD_TESTS=OFF \
        -DENABLE_SSE3=OFF \
        -DBUILD_EXAMPLES=ON \
        -DINSTALL_C_EXAMPLES=ON \
        -DINSTALL_PYTHON_EXAMPLES=ON \
        -DWITH_XINE=ON \
        -DWITH_QT=ON \
	-DWITH_OPENGL=ON \
        -DWITH_TBB=OFF \
        -DWITH_CUDA=OFF \
        -DWITH_GTK=OFF \
        -DENABLE_FAST_MATH=ON \
        -DOPENCV_EXTRA_MODULES_PATH=${srcdir}/opencv_contrib-${_pkgver}/modules
  make
}

package() {
  cd "$srcdir/opencv-${_pkgver}/build"
  make DESTDIR="$pkgdir" install

  cd ..
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}


