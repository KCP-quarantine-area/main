
pkgbase=opencv
pkgname=('opencv' 'opencv-samples')
_realname=OpenCV
pkgver=2.4.6.1
pkgrel=1
pkgdesc="Open Source Computer Vision Library"
arch=('x86_64')
license=('BSD')
url="http://opencv.willowgarage.com/"
depends=('jasper' 'gstreamer0.10-base' 'openexr'
         'xine-lib' 'libdc1394' 'v4l-utils' 'ffmpeg' 'qt')
makedepends=('pkgconfig' 'cmake' 'python2-numpy' 'eigen' 'python2-sphinx' 'mesa')
optdepends=('python2-numpy: Python 2.x interface' 'eigen')
options=('!libtool')
source=("http://downloads.sourceforge.net/opencvlibrary/$pkgname-$pkgver.tar.gz")
md5sums=('d756bfa460891697571d5c90050e1cfe')

_cmakeopts=('-D CMAKE_BUILD_TYPE=Release'
            '-D CMAKE_INSTALL_PREFIX=/usr'
            '-D CMAKE_SKIP_RPATH=ON'
            '-D BUILD_TESTS=OFF'
            '-D ENABLE_SSE3=OFF'
            '-D BUILD_EXAMPLES=ON'
            '-D INSTALL_C_EXAMPLES=ON'
            '-D INSTALL_PYTHON_EXAMPLES=ON'
            '-D WITH_XINE=ON'
            '-D WITH_QT=ON'
	    '-D WITH_OPENGL=ON'
            '-D WITH_TBB=OFF'
            '-D WITH_CUDA=OFF'
            '-D WITH_GTK=OFF'
            '-D ENABLE_FAST_MATH=ON')

build() {
  cd "$srcdir/$pkgname-$pkgver"
 
  mkdir build && cd build

  cmake ${_cmakeopts[@]} ..
  make
}

package_opencv() {
  install=$pkgname.install

  cd "$srcdir/$pkgname-$pkgver/build"

  make DESTDIR="$pkgdir" install

  # install license file
  install -Dm644 "$srcdir/$pkgname-$pkgver/doc/license.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd "$pkgdir/usr/share"

  # separate samples package
  [ -d $pkgname/samples ] && mv $pkgname/samples "$srcdir/opencv-samples"
}

package_opencv-samples() {
  pkgdesc+=" (samples)"
  depends=('bash')
  optdepends=()
  options=()

  cd "$srcdir"

  mkdir -p "$pkgdir/usr/share/opencv"
  cp -r opencv-samples "$pkgdir/usr/share/opencv/samples"
  install -Dm644 "$srcdir/opencv-$pkgver/doc/license.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

