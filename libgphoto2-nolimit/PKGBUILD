
pkgname=libgphoto2-nolimit
oldpkgname=libgphoto2
pkgver=2.5.3.1
pkgrel=1
pkgdesc="The core library of gphoto2.  This variation is only to increase the limit of files!"
arch=('x86_64')
url="http://www.gphoto.org"
license=('LGPL')
conflicts=('libgphoto2')
provides=("libgphoto2=${pkgver}")
depends=('libexif' 'libjpeg>=8' 'gd' 'libtool>=2.4' 'libusb' 'libusb-compat')
install=libgphoto2.install
options=('libtool')
source=("http://downloads.sourceforge.net/gphoto/${oldpkgname}-${pkgver}.tar.bz2")
md5sums=('aad2607a84442769c14f6acce2ca1ddf')

build() {
  cd "${srcdir}/${oldpkgname}-${pkgver}"

  #Change the limit which cause "Fixed limit exceeded"
  sed -i "s#MAX_ENTRIES 1024#MAX_ENTRIES 8192#" libgphoto2/gphoto2-list.c

  ./configure \
    --prefix=/usr \
    --disable-rpath
  make
}

package() {
  cd "${srcdir}/${oldpkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/lib/udev/rules.d"
  LD_LIBRARY_PATH="${pkgdir}/usr/lib${LD_LIBRARY_PATH:+:}$LD_LIBRARY_PATH" \
  CAMLIBS="${pkgdir}/usr/lib/libgphoto2/${pkgver}" \
      "${pkgdir}/usr/lib/libgphoto2/print-camera-list" udev-rules version 175 > \
      "${pkgdir}/usr/lib/udev/rules.d/40-gphoto.rules"
      
  install -m755 -d "${pkgdir}/usr/lib/udev/hwdb.d"
  install -Dm644 "${srcdir}/${oldpkgname}-${pkgver}/packaging/generic/print-camera-list" \
      "${pkgdir}/usr/lib/udev/hwdb.d/20-gphoto.hwdb"

  # Remove recursive symlink
  rm -f "${pkgdir}/usr/include/gphoto2/gphoto2"
}
