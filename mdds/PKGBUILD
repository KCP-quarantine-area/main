
pkgname=mdds
pkgver=1.0.0
pkgrel=1
pkgdesc="Collection of multi-dimensional data structures and indexing algorithm"
arch=('x86_64')
url="https://gitlab.com/mdds/mdds"
license=('MIT')
checkdepends=('boost')
#source=("https://gitlab.com/mdds/mdds/repository/archive.tar.gz?ref=${pkgver}")
source=("http://kohei.us/files/mdds/src/mdds_${pkgver}.tar.bz2")
md5sums=('74e0a81c5af4137285fe6a52217f71e4')

build() {
  cd ${pkgname}_${pkgver}

  ./autogen.sh
  ./configure --prefix=/usr
  # header files only, so no "make"
}

check() {
  cd ${pkgname}_${pkgver}

  make check
}

package() {
  cd ${pkgname}_${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}