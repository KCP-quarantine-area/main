
pkgname=nasm
pkgver=2.11.06
pkgrel=1
pkgdesc="Netwide Assembler for 80x86 and x86-64 designed for portability and modularity"
url="http://www.nasm.us"
license=('BSD')
arch=('x86_64')
depends=('glibc')
makedepends=('ghostscript')
source=("http://www.nasm.us/pub/nasm/releasebuilds/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('9d93e8df5f5c005567f47ed6ca3a93d4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make INSTALLROOT="${pkgdir}" install install_rdf
  
  install -d "${pkgdir}/usr/share/info"
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/nasm/LICENSE"
}
