
pkgname=pyqt-python2
pkgver=4.11.3
_snapshot=9d5a6843b580
pkgrel=1
pkgdesc="A set of Python bindings for the Qt toolkit"
arch=('x86_64')
url="http://riverbankcomputing.co.uk/software/pyqt/intro"
license=('GPL')
depends=('sip' 'qtwebkit' 'dbus-python')
makedepends=('phonon' 'python2-opengl' 'qt-assistant-compat' 'mesa')
optdepends=('phonon: enable audio and video in PyQt applications'
            'python2-opengl: enable OpenGL 3D graphics in PyQt applications'
            'qscintilla: QScintilla API'
            'qt-assistant-compat: add PyQt online help in Qt Assistant')
provides=('pyqt4' 'pyqt')
replaces=('pyqt4' 'pyqt')
conflicts=('pyqt4' 'pyqt')
source=("http://sourceforge.net/projects/pyqt/files/PyQt4/PyQt-${pkgver}/PyQt-x11-gpl-${pkgver}.tar.gz")
#source=("http://www.riverbankcomputing.com/static/Downloads/PyQt4/PyQt-x11-gpl-${pkgver}-snapshot-$_snapshot.tar.gz")
md5sums=('997c3e443165a89a559e0d96b061bf70')

build() {
  cd ${srcdir}/PyQt-x11-gpl-${pkgver}

  python2 configure.py \
    --confirm-license \
    -v /usr/share/sip \
    --qsci-api

  find -name 'Makefile' | xargs sed -i 's|-Wl,-rpath,/usr/lib||g;s|-Wl,-rpath,.* ||g'

  make
}

package(){
  cd ${srcdir}/PyQt-x11-gpl-${pkgver}
  # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
  make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install
}
