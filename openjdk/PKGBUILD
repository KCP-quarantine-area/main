
pkgname=('openjdk' 'openjdk-src')
pkgbase='openjdk'
_icedtea_release="2.4.2"
_icedtea_jdkver="7"
_openjdk_build="b31"
_pkgver="7u40"
pkgver="${_pkgver}_${_openjdk_build}_${_icedtea_release}"
pkgrel=1
pkgdesc="An open-source implementation of the seventh edition of the Java SE Platform."
arch=('x86_64')
url="http://icedtea.classpath.org/"
license=('GPL2 with Classpath exception')
makedepends=('java-environment=7' 'apache-ant>=1.6.5' 'zip' 'unzip'
             'fastjar' 'cpio' 'cups' 'orbit2' 'gtk2' 'coreutils'
             'wget' 'gzip' 'alsa-lib' 'pulseaudio' 'zlib' 'libjpeg-turbo'
             'libpng' 'giflib' 'lcms2' 'freetype2' 'rhino' 'nss' 'libxt'
             'libxtst' 'libxp' 'libxrender' 'ca-certificates-java')

_hg_url="http://icedtea.classpath.org/hg/release/icedtea7-forest-2.4"
_corba_changeset="5ea58899ae9f"
_jaxp_changeset="135f46e0e653"
_jaxws_changeset="a2ff16e5361f"
_jdk_changeset="4bf5ac86e0e4"
_langtools_changeset="06ea051f7ffe"
_openjdk_changeset="6125b83238df"
_hotspot_changeset="b732355257e3"
_jvmdir="/usr/lib/jvm/java-7-openjdk"

source=("http://icedtea.classpath.org/download/source/icedtea-${_icedtea_release}.tar.gz"
        "${_hg_url}/corba/archive/${_corba_changeset}.tar.gz"
        "${_hg_url}/jaxp/archive/${_jaxp_changeset}.tar.gz"
        "${_hg_url}/jaxws/archive/${_jaxws_changeset}.tar.gz"
        "${_hg_url}/jdk/archive/${_jdk_changeset}.tar.gz"
        "${_hg_url}/langtools/archive/${_langtools_changeset}.tar.gz"
        "${_hg_url}/archive/${_openjdk_changeset}.tar.gz"
        "${_hg_url}/hotspot/archive/${_hotspot_changeset}.tar.gz"
        'fontconfig-paths.diff'
        'fix_corba_cmds_path.diff'
        'openjdk7_fix_jdk_cmds_path.diff'
        'openjdk7_nonreparenting-wm.diff'
        'openjdk.profile.sh'
        'openjdk.profile.csh')
md5sums=('54a739ba98b9acd439d423f2a5713aa6'
         'ab73495be841cdc3f18f2478a3401500'
         '287018ab678bdbcb8addd3d6afbdd018'
         'eca301049557f504e7349878d2faf70a'
         '5612ce683b250a8f8d35901efa202591'
         'ef00d60244156f537d3c5fdf2047dfa3'
         'd59e1859070bd15462a41372c71cabd1'
         'aad98f91c9ad9eb5631b9d4e7aab289f'
         'ee1afda124d5927345014ab382ef581e'
         'f198c2ba43d1f4408a948ac95b9a39ba'
         '7a8f14be514b40519fcd6780a31d4f79'
         '2f6733af3fea184196a720267b8acaac'
         '64885c74918f07afe88f31692177b0c5'
         '3baf9768e5be92d6fe80641dcc35f017')
noextract=("${_openjdk_changeset}.tar.gz"
           "${_corba_changeset}.tar.gz"
           "${_jaxp_changeset}.tar.gz"
           "${_jaxws_changeset}.tar.gz"
           "${_jdk_changeset}.tar.gz"
           "${_langtools_changeset}.tar.gz"
           "${_hotspot_changeset}.tar.gz")
           
  [ "$CARCH" = "x86_64" ] && _JARCH=amd64

build() {
  cd "${srcdir}"

  # Prepare the patches
  cp *.diff "${srcdir}/icedtea-${_icedtea_release}/patches"
  export DISTRIBUTION_PATCHES="patches/fontconfig-paths.diff \
                               patches/fix_corba_cmds_path.diff \
                               patches/openjdk7_fix_jdk_cmds_path.diff \
                               patches/openjdk7_nonreparenting-wm.diff"

  rm -rf build
  cp -r "icedtea-${_icedtea_release}" \
        "${srcdir}/build"

  cd "${srcdir}/build"

  export ALT_PARALLEL_COMPILE_JOBS="${MAKEFLAGS/-j}"
  export HOTSPOT_BUILD_JOBS="${ALT_PARALLEL_COMPILE_JOBS}"

  . /etc/profile.d/apache-ant.sh


  # Start the configuration and build step
  ./configure --prefix=/usr \
              --with-parallel-jobs="${MAKEFLAGS/-j}" \
              --host="${CARCH}-unknown-linux-gnu" \
              --with-rhino \
              --with-jdk-home="${JAVA_HOME}" \
              --with-abs-install-dir="${_jvmdir}" \
              --with-openjdk-src-zip="${srcdir}/${_openjdk_changeset}.tar.gz" \
              --with-hotspot-src-zip="${srcdir}/${_hotspot_changeset}.tar.gz" \
              --with-corba-src-zip="${srcdir}/${_corba_changeset}.tar.gz" \
              --with-jaxp-src-zip="${srcdir}/${_jaxp_changeset}.tar.gz" \
              --with-jaxws-src-zip="${srcdir}/${_jaxws_changeset}.tar.gz" \
              --with-jdk-src-zip="${srcdir}/${_jdk_changeset}.tar.gz" \
              --with-langtools-src-zip="${srcdir}/${_langtools_changeset}.tar.gz" \
              --with-pkgversion="Linux build ${pkgver}-${pkgrel}-${CARCH}" \
              --disable-tests \
              --disable-downloading \
              --disable-Werror \
              --disable-bootstrap \
              --disable-system-gtk \
              --enable-nss \
              --enable-pulse-java \
              --enable-system-zlib \
              --enable-system-jpeg \
              --enable-system-png \
              --enable-system-gif \
              --enable-system-lcms \
              --enable-system-gio \
              --enable-system-fontconfig 
  make
}

package_openjdk() {
pkgdesc="An open-source implementation of the seventh edition of the Java SE Platform."
install="openjdk.install"
conflicts=('java-environment' 'java-runtime' 'openjdk6' 'openjdk7')
provides=("java-environment=${_icedtea_jdkver}" "java-runtime=${_icedtea_jdkver}")
replaces=('openjdk6' 'openjdk7')
depends=('alsa-lib' 'pulseaudio' 'giflib' 'libjpeg-turbo' 'libpng' 'freetype2'
         'ca-certificates-java')
optdepends=('icedtea-web: web browser plugin and Java Web Start support'
            'gtk2: for the GTK+ Look And Feel')

  cd "${srcdir}/build"

  # Copy the built OpenJDK image to the target directory
  install -d -m755 "${pkgdir}/${_jvmdir%/*}"
  cp -r "openjdk.build/j2sdk-image" \
        "${pkgdir}/${_jvmdir#/}"

  # Remove the .zip file which is going to be inside openjdk-src
  rm "${pkgdir}/${_jvmdir#/}/src.zip"

  # Symlink the binaries into /usr/bin
  install -d -m755 "${pkgdir}/usr/bin"
  pushd "${pkgdir}/${_jvmdir#/}/bin" >/dev/null
    for executable in *; do
      ln -s "${_jvmdir}/bin/${executable}" \
            "${pkgdir}/usr/bin"
    done
  popd >/dev/null

  # Move the manual pages in the right place
  install -d -m755 "${pkgdir}/usr/share"
  mv "${pkgdir}/${_jvmdir#/}/man" \
     "${pkgdir}/usr/share/man"
  rm "${pkgdir}/usr/share/man/ja" # avoid conflicts with man-db

  # link JKS keystore from ca-certificates-java
  rm -f "${pkgdir}/${_jvmdir#/}/jre/lib/security/cacerts"
  ln -s /etc/ssl/certs/java/cacerts \
        "${pkgdir}/${_jvmdir#/}/jre/lib/security/cacerts"

  # Setup the shell profile dropin configuration
  install -D -m755 "${srcdir}/openjdk.profile.sh" \
                   "${pkgdir}/etc/profile.d/openjdk.sh"
  install -D -m755 "${srcdir}/openjdk.profile.csh" \
                   "${pkgdir}/etc/profile.d/openjdk.csh"

  # Install the license file
  install -D -m644 "openjdk/LICENSE" \
                   "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_openjdk-src() {
pkgdesc="An open-source implementation of the seventh edition of the Java SE Platform. (Source package)"
arch=('x86_64')
depends=("openjdk=${pkgver}")

  cd "${srcdir}/build"

  # Install the source package
  install -D -m644 "openjdk.build/j2sdk-image/src.zip" \
                   "${pkgdir}/${_jvmdir#/}/src.zip"
}

