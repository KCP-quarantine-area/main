
pkgname=('openjdk' 'openjdk-src')
pkgbase='openjdk'
_icedtea_release="2.6.5"
_icedtea_jdkver="7"
# ${srcdir}/build/ChangeLog for versions
_openjdk_build="b00"
_pkgver="7u99"
pkgver="${_pkgver}_${_openjdk_build}_${_icedtea_release}"
pkgrel=2
pkgdesc="An open-source implementation of the seventh edition of the Java SE Platform."
arch=('x86_64')
url="http://icedtea.classpath.org/"
license=('GPL2 with Classpath exception')
makedepends=('java-environment=7' 'apache-ant>=1.6.5' 'zip' 'unzip'
             'fastjar' 'cpio' 'cups' 'gtk2' 'coreutils'
             'wget' 'gzip' 'alsa-lib' 'pulseaudio' 'zlib' 'libjpeg-turbo'
             'libpng' 'giflib' 'lcms2' 'freetype2' 'rhino' 'nss' 'libxt'
             'libxtst' 'libxp' 'libxrender' 'ca-certificates-java' 'eclipse-ecj' 'pcsclite')

_hg_url="http://icedtea.wildebeest.org/download/drops/icedtea7/${_icedtea_release}"
_jvmdir="/usr/lib/jvm/java-7-openjdk"

source=("http://icedtea.classpath.org/download/source/icedtea-${_icedtea_release}.tar.gz"
        "${_hg_url}/corba.tar.bz2"
        "${_hg_url}/jaxp.tar.bz2"
        "${_hg_url}/jaxws.tar.bz2"
        "${_hg_url}/jdk.tar.bz2"
        "${_hg_url}/langtools.tar.bz2"
        "${_hg_url}/openjdk.tar.bz2"
        "${_hg_url}/hotspot.tar.bz2"
        'fontconfig-paths.diff'
        'corba_path.diff'
        'command_path.diff'
        'openjdk7_nonreparenting-wm.diff'
        'openjdk.profile.sh'
        'openjdk.profile.csh')
md5sums=('cb66b595860c3d99a87c3a853667c145'
         '3345d1939663d1ef94459cf215b76b3f'
         '872a458cde954124eee9913e6ea906b0'
         'd0d559ca0d797c5dc9640bfa277a23c5'
         '13fa99cc310f27818867e8a9d0841892'
         '2e951b311d0cfbd7ebc6e9fbb75de891'
         '48d9f0e76f92d18aff1b9379b052b0cb'
         '7e1bdaf4385c3b7e77d3599c52151485'
         'ee1afda124d5927345014ab382ef581e'
         '986131efd2636724ae37870a5cca6d43'
         '05afd9c7fedcc05d4ebbb3427e068dba'
         '029383f5aba2121facc19377504be3df'
         '64885c74918f07afe88f31692177b0c5'
         '3baf9768e5be92d6fe80641dcc35f017')
noextract=("openjdk.tar.gz"
           "corba.tar.gz"
           "jaxp.tar.gz"
           "jaxws.tar.gz"
           "jdk.tar.gz"
           "langtools.tar.gz"
           "hotspot.tar.gz")

build() {
  cd "${srcdir}"

  # Prepare the patches
  cp *.diff "${srcdir}/icedtea-${_icedtea_release}/patches"
  export DISTRIBUTION_PATCHES="patches/fontconfig-paths.diff \
                               patches/corba_path.diff \
                               patches/command_path.diff \
                               patches/openjdk7_nonreparenting-wm.diff"

  rm -rf build
  cp -r "icedtea-${_icedtea_release}" \
        "${srcdir}/build"

  cd "${srcdir}/build"

  export ALT_PARALLEL_COMPILE_JOBS="${MAKEFLAGS/-j}"
  export HOTSPOT_BUILD_JOBS="${ALT_PARALLEL_COMPILE_JOBS}"

  . /etc/profile.d/apache-ant.sh


  # Start the configuration and build step
  ./configure --prefix=/usr \
              --with-parallel-jobs="${MAKEFLAGS/-j}" \
              --host="x86_64-unknown-linux-gnu" \
              --with-rhino \
              --with-jdk-home="${JAVA_HOME}" \
              --with-openjdk-src-zip="${srcdir}/openjdk.tar.bz2" \
              --with-hotspot-src-zip="${srcdir}/hotspot.tar.bz2" \
              --with-corba-src-zip="${srcdir}/corba.tar.bz2" \
              --with-jaxp-src-zip="${srcdir}/jaxp.tar.bz2" \
              --with-jaxws-src-zip="${srcdir}/jaxws.tar.bz2" \
              --with-jdk-src-zip="${srcdir}/jdk.tar.bz2" \
              --with-langtools-src-zip="${srcdir}/langtools.tar.bz2" \
              --with-pkgversion="Linux build ${pkgver}-${pkgrel}-x86_64" \
              --disable-tests \
              --disable-downloading \
              --disable-Werror \
              --disable-bootstrap \
              --enable-nss \
              --disable-system-sctp
  make
}

package_openjdk() {
pkgdesc="An open-source implementation of the seventh edition of the Java SE Platform."
install="openjdk.install"
conflicts=('openjdk6' 'openjdk7')
provides=("java-environment=${_icedtea_jdkver}" "java-runtime=${_icedtea_jdkver}")
replaces=('openjdk6' 'openjdk7')
depends=('alsa-lib' 'pulseaudio' 'giflib' 'libjpeg-turbo' 'libpng' 'freetype2'
         'ca-certificates-java')
optdepends=('icedtea-web: web browser plugin and Java Web Start support'
            'gtk2: for the GTK+ Look And Feel')

  cd "${srcdir}/build"

  # Copy the built OpenJDK image to the target directory
  install -d -m755 "${pkgdir}/${_jvmdir%/*}"
  cp -r "openjdk.build/j2sdk-image" \
        "${pkgdir}/${_jvmdir#/}"

  # Remove the .zip file which is going to be inside openjdk-src
  rm "${pkgdir}/${_jvmdir#/}/src.zip"

  # Symlink the binaries into /usr/bin
  install -d -m755 "${pkgdir}/usr/bin"
  pushd "${pkgdir}/${_jvmdir#/}/bin" >/dev/null
    for executable in *; do
      ln -s "${_jvmdir}/bin/${executable}" \
            "${pkgdir}/usr/bin"
    done
  popd >/dev/null

  # Move the manual pages in the right place, avoid conflicts with jdk8
  install -d -m755 "${pkgdir}"/usr/share/man/{,ja/}man2/
  mv "${pkgdir}"/${_jvmdir#/}/man/man1/* \
     "${pkgdir}"/usr/share/man/man2/
  mv "${pkgdir}"/${_jvmdir#/}/man/ja_JP.UTF-8/* \
     "${pkgdir}"/usr/share/man/ja

  # link JKS keystore from ca-certificates-java
  rm -f "${pkgdir}/${_jvmdir#/}/jre/lib/security/cacerts"
  ln -s /etc/ssl/certs/java/cacerts \
        "${pkgdir}/${_jvmdir#/}/jre/lib/security/cacerts"

  # Setup the shell profile dropin configuration
  install -D -m755 "${srcdir}/openjdk.profile.sh" \
                   "${pkgdir}/etc/profile.d/openjdk.sh"
  install -D -m755 "${srcdir}/openjdk.profile.csh" \
                   "${pkgdir}/etc/profile.d/openjdk.csh"

  # Install the license file
  install -D -m644 "openjdk/LICENSE" \
                   "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_openjdk-src() {
pkgdesc="An open-source implementation of the seventh edition of the Java SE Platform. (Source package)"
arch=('x86_64')
depends=("openjdk=${pkgver}")

  cd "${srcdir}/build"

  # Install the source package
  install -D -m644 "openjdk.build/j2sdk-image/src.zip" \
                   "${pkgdir}/${_jvmdir#/}/src.zip"
}

