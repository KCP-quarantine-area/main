
pkgname=('openjdk' 'openjdk-src')
pkgbase='openjdk'
_icedtea_release="2.4.6"
_icedtea_jdkver="7"
_openjdk_build="b31"
_pkgver="7u51"
pkgver="${_pkgver}_${_openjdk_build}_${_icedtea_release}"
pkgrel=1
pkgdesc="An open-source implementation of the seventh edition of the Java SE Platform."
arch=('x86_64')
url="http://icedtea.classpath.org/"
license=('GPL2 with Classpath exception')
makedepends=('java-environment=7' 'apache-ant>=1.6.5' 'zip' 'unzip'
             'fastjar' 'cpio' 'cups' 'orbit2' 'gtk2' 'coreutils'
             'wget' 'gzip' 'alsa-lib' 'pulseaudio' 'zlib' 'libjpeg-turbo'
             'libpng' 'giflib' 'lcms2' 'freetype2' 'rhino' 'nss' 'libxt'
             'libxtst' 'libxp' 'libxrender' 'ca-certificates-java')

_hg_url="http://icedtea.classpath.org/hg/release/icedtea7-forest-2.4"
_corba_changeset="48ef1bb6d120"
_jaxp_changeset="e0ba4b9a8b91"
_jaxws_changeset="4bd947cd146b"
_jdk_changeset="b5282042aae0"
_langtools_changeset="06eeb77dac24"
_openjdk_changeset="b028e58c1b77"
_hotspot_changeset="172674e0ab65"
_jvmdir="/usr/lib/jvm/java-7-openjdk"

source=("http://icedtea.classpath.org/download/source/icedtea-${_icedtea_release}.tar.gz"
        "${_hg_url}/corba/archive/${_corba_changeset}.tar.gz"
        "${_hg_url}/jaxp/archive/${_jaxp_changeset}.tar.gz"
        "${_hg_url}/jaxws/archive/${_jaxws_changeset}.tar.gz"
        "${_hg_url}/jdk/archive/${_jdk_changeset}.tar.gz"
        "${_hg_url}/langtools/archive/${_langtools_changeset}.tar.gz"
        "${_hg_url}/archive/${_openjdk_changeset}.tar.gz"
        "${_hg_url}/hotspot/archive/${_hotspot_changeset}.tar.gz"
        'fontconfig-paths.diff'
        'corba_path.diff'
        'command_path.diff'
        'openjdk7_nonreparenting-wm.diff'
        'openjdk.profile.sh'
        'openjdk.profile.csh')
md5sums=('f832ad661d78890c2332ec66c1ea68fe'
         '8a725ed6fd9cf8d8b4b978e95d990d12'
         '5c8e75f4e67d60f766d443de210a8710'
         '7b52ff1abbd5343f95e1ef6de724aabd'
         '5204915d151730e8345530471dc58e9a'
         '0a0ca66666dafcd5ad2a856a83ddb6cc'
         '3d319d270c57fa206475cf713d7fdcb9'
         '969192666fed1827295aaf2459217a2c'
         'ee1afda124d5927345014ab382ef581e'
         '986131efd2636724ae37870a5cca6d43'
         '05afd9c7fedcc05d4ebbb3427e068dba'
         '029383f5aba2121facc19377504be3df'
         '64885c74918f07afe88f31692177b0c5'
         '3baf9768e5be92d6fe80641dcc35f017')
noextract=("${_openjdk_changeset}.tar.gz"
           "${_corba_changeset}.tar.gz"
           "${_jaxp_changeset}.tar.gz"
           "${_jaxws_changeset}.tar.gz"
           "${_jdk_changeset}.tar.gz"
           "${_langtools_changeset}.tar.gz"
           "${_hotspot_changeset}.tar.gz")
           
  [ "$CARCH" = "x86_64" ] && _JARCH=amd64

build() {
  cd "${srcdir}"

  # Prepare the patches
  cp *.diff "${srcdir}/icedtea-${_icedtea_release}/patches"
  export DISTRIBUTION_PATCHES="patches/fontconfig-paths.diff \
                               patches/corba_path.diff \
                               patches/command_path.diff \
                               patches/openjdk7_nonreparenting-wm.diff"

  rm -rf build
  cp -r "icedtea-${_icedtea_release}" \
        "${srcdir}/build"

  cd "${srcdir}/build"

  export ALT_PARALLEL_COMPILE_JOBS="${MAKEFLAGS/-j}"
  export HOTSPOT_BUILD_JOBS="${ALT_PARALLEL_COMPILE_JOBS}"

  . /etc/profile.d/apache-ant.sh


  # Start the configuration and build step
  ./configure --prefix=/usr \
              --with-parallel-jobs="${MAKEFLAGS/-j}" \
              --host="${CARCH}-unknown-linux-gnu" \
              --with-rhino \
              --with-jdk-home="${JAVA_HOME}" \
              --with-abs-install-dir="${_jvmdir}" \
              --with-openjdk-src-zip="${srcdir}/${_openjdk_changeset}.tar.gz" \
              --with-hotspot-src-zip="${srcdir}/${_hotspot_changeset}.tar.gz" \
              --with-corba-src-zip="${srcdir}/${_corba_changeset}.tar.gz" \
              --with-jaxp-src-zip="${srcdir}/${_jaxp_changeset}.tar.gz" \
              --with-jaxws-src-zip="${srcdir}/${_jaxws_changeset}.tar.gz" \
              --with-jdk-src-zip="${srcdir}/${_jdk_changeset}.tar.gz" \
              --with-langtools-src-zip="${srcdir}/${_langtools_changeset}.tar.gz" \
              --with-pkgversion="Linux build ${pkgver}-${pkgrel}-${CARCH}" \
              --disable-tests \
              --disable-downloading \
              --disable-Werror \
              --disable-bootstrap \
              --disable-system-gtk \
              --enable-nss \
              --enable-pulse-java 
  make
}

package_openjdk() {
pkgdesc="An open-source implementation of the seventh edition of the Java SE Platform."
install="openjdk.install"
conflicts=('java-environment' 'java-runtime' 'openjdk6' 'openjdk7')
provides=("java-environment=${_icedtea_jdkver}" "java-runtime=${_icedtea_jdkver}")
replaces=('openjdk6' 'openjdk7')
depends=('alsa-lib' 'pulseaudio' 'giflib' 'libjpeg-turbo' 'libpng' 'freetype2'
         'ca-certificates-java')
optdepends=('icedtea-web: web browser plugin and Java Web Start support'
            'gtk2: for the GTK+ Look And Feel')

  cd "${srcdir}/build"

  # Copy the built OpenJDK image to the target directory
  install -d -m755 "${pkgdir}/${_jvmdir%/*}"
  cp -r "openjdk.build/j2sdk-image" \
        "${pkgdir}/${_jvmdir#/}"

  # Remove the .zip file which is going to be inside openjdk-src
  rm "${pkgdir}/${_jvmdir#/}/src.zip"

  # Symlink the binaries into /usr/bin
  install -d -m755 "${pkgdir}/usr/bin"
  pushd "${pkgdir}/${_jvmdir#/}/bin" >/dev/null
    for executable in *; do
      ln -s "${_jvmdir}/bin/${executable}" \
            "${pkgdir}/usr/bin"
    done
  popd >/dev/null

  # Move the manual pages in the right place
  install -d -m755 "${pkgdir}/usr/share"
  mv "${pkgdir}/${_jvmdir#/}/man" \
     "${pkgdir}/usr/share/man"
  rm "${pkgdir}/usr/share/man/ja" # avoid conflicts with man-db

  # link JKS keystore from ca-certificates-java
  rm -f "${pkgdir}/${_jvmdir#/}/jre/lib/security/cacerts"
  ln -s /etc/ssl/certs/java/cacerts \
        "${pkgdir}/${_jvmdir#/}/jre/lib/security/cacerts"

  # Setup the shell profile dropin configuration
  install -D -m755 "${srcdir}/openjdk.profile.sh" \
                   "${pkgdir}/etc/profile.d/openjdk.sh"
  install -D -m755 "${srcdir}/openjdk.profile.csh" \
                   "${pkgdir}/etc/profile.d/openjdk.csh"

  # Install the license file
  install -D -m644 "openjdk/LICENSE" \
                   "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_openjdk-src() {
pkgdesc="An open-source implementation of the seventh edition of the Java SE Platform. (Source package)"
arch=('x86_64')
depends=("openjdk=${pkgver}")

  cd "${srcdir}/build"

  # Install the source package
  install -D -m644 "openjdk.build/j2sdk-image/src.zip" \
                   "${pkgdir}/${_jvmdir#/}/src.zip"
}

