
pkgname=usb_modeswitch
pkgver=1.2.5
pkgrel=1
_pkgdata=20121109
pkgdesc="Activating switchable USB devices on Linux."
arch=('x86_64')
url="http://www.draisberghof.de/usb_modeswitch/"
license=('GPL3')
depends=('libusb-compat' 'tcl')
makedepends=('gcc' 'make')
conflicts=('usb-modeswitch')
replaces=('usb-modeswitch')
groups=('system')
backup=("etc/$pkgname.conf")
source=("http://www.draisberghof.de/$pkgname/usb-modeswitch-$pkgver.tar.bz2"
	"http://www.draisberghof.de/$pkgname/usb-modeswitch-data-$_pkgdata.tar.bz2"
	"usb-modeswitch-${pkgver}-device_reference.txt::http://www.draisberghof.de/$pkgname/device_reference.txt")
md5sums=('c393603908eceab95444c5bde790f6f0'
         'a7d23a03157871013a0d708ab2b1b6df'
         '9918bbdc0f68873410688604c80d25c4')

build() {
  cd $srcdir/usb-modeswitch-$pkgver/
  make clean
  
  make
}

package() {
  cd $srcdir/usb-modeswitch-$pkgver/

  mkdir -p $pkgdir/usr/lib/udev
  mkdir -p $pkgdir/etc
  mkdir -p $pkgdir/usr/share/man/man1

  make DESTDIR=${pkgdir} UDEVDIR=${pkgdir}/usr/lib/udev install

  cp $srcdir/usb-modeswitch-${pkgver}-device_reference.txt $pkgdir/etc/usb_modeswitch.setup

  cd $srcdir/usb-modeswitch-data-${_pkgdata}
  make DESTDIR=$pkgdir install RULESDIR=${pkgdir}/usr/lib/udev/rules.d
}
