
pkgname=hwinfo
pkgver=20.1
pkgrel=2
pkgdesc="A simple program that lists results from the hardware detection library."
arch=('x86_64')
url="http://www.opensuse.org"
license=('GPL2')
depends=('perl' 'libx86emu')
makedepends=('sysfsutils' 'rpmextract' 'flex')
options=('!emptydirs')
source=("http://download.opensuse.org/source/factory/repo/oss/suse/src/${pkgname}-${pkgver}-3.10.src.rpm"
        "kbd.patch")
md5sums=('307b5391c64c96c93f9719d2524be113'
         '1500ecfa26f6aaf4d7967eea74e86e5d')

build() {
  cd "${srcdir}"

  rpmextract.sh "${pkgname}-${pkgver}-3.10.src.rpm"
  tar -xjf ${pkgname}-${pkgver}.tar.bz2
  cd "${pkgname}-${pkgver}"

  patch -p0 < ../kbd.patch 
  
  make -j1 CFLAGS+="-fPIC -I${srcdir}/${pkgname}-${pkgver}/src/hd" LIBDIR=/usr/lib
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make LIBDIR=/usr/lib DESTDIR=${pkgdir} install
}
