
pkgname=hwinfo
pkgver=21.5
_minorver=1.1
pkgrel=1
pkgdesc="Simple program that lists results from the hardware detection library."
arch=('x86_64')
url="http://www.opensuse.org"
license=('GPL2')
depends=('perl' 'libx86emu')
makedepends=('sysfsutils' 'rpmextract' 'flex')
options=('!emptydirs')
source=("http://download.opensuse.org/source/factory/repo/oss/suse/src/${pkgname}-${pkgver}-${_minorver}.src.rpm"
        "kbd.patch")
md5sums=('636cb92a68ceb0cb127cf313f042c3f5'
         '1500ecfa26f6aaf4d7967eea74e86e5d')

build() {
  cd "${srcdir}"

  rpmextract.sh "${pkgname}-${pkgver}-${_minorver}.src.rpm"
  tar -xJf ${pkgname}-${pkgver}.tar.xz
  cd "${pkgname}-${pkgver}"

  patch -p0 < ../kbd.patch 
  
  make -j1 CFLAGS+="-fPIC -I${srcdir}/${pkgname}-${pkgver}/src/hd" LIBDIR=/usr/lib
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make LIBDIR=/usr/lib DESTDIR=${pkgdir} install
}
