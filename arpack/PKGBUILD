
pkgname=arpack
pkgver=3.1.4
pkgrel=1
arch=('x86_64')
pkgdesc="Fortran77 subroutines designed to solve large scale eigenvalue problems"
url="http://forge.scilab.org/index.php/p/arpack-ng/"
license=('BSD')
depends=('glibc' 'lapack' 'openmpi')
options=('!libtool')
source=("http://forge.scilab.org/index.php/p/arpack-ng/downloads/get/$pkgname-ng_$pkgver.tar.gz")
md5sums=('af0c56f3aeb7e6e57ae46acdcb8b104b')

build() {
  cd "$srcdir/$pkgname-ng-$pkgver"

  ./configure --prefix=/usr --enable-mpi
  make \
    F77="mpif77" \
    CFLAGS+=" `pkg-config --cflags ompi` " \
    LIBS+=" `pkg-config --libs ompi` "
}

package() {
  cd "$srcdir/$pkgname-ng-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
