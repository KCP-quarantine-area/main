
pkgname=libatomic_ops
pkgver=7.2.1
_pkgver=7.2e
pkgrel=1
pkgdesc="Provides semi-portable access to hardware-provided atomic memory update operations"
arch=('x86_64')
url="https://github.com/ivmai/libatomic_ops"
license=('GPL2' 'MIT')
depends=('glibc')
source=("http://www.hpl.hp.com/research/linux/atomic_ops/download/libatomic_ops-${_pkgver}.tar.gz")
md5sums=('7035692fec4db2659b06485040829e43')

build() {
  cd libatomic_ops-7.2

  sed -i 's#AM_CONFIG_HEADER#AC_CONFIG_HEADERS#' configure.ac
  sed -i 's#AC_PROG_RANLIB#AC_LIBTOOL_DLOPEN\nAC_PROG_LIBTOOL#' configure.ac
  sed -i 's#b_L#b_LTL#;s#\.a#.la#g;s#_a_#_la_#' src/Makefile.am
  sed -i 's#\.a#.so#g;s#\.\./src/#../src/.libs/#g' tests/Makefile.am
  sed -i 's#pkgdata#doc#' doc/Makefile.am

  autoreconf -fi
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd libatomic_ops-7.2

  LD_LIBRARY_PATH="${srcdir}/libatomic_ops-7.2/src/.libs" make check
}

package() {
  cd libatomic_ops-7.2
  make DESTDIR="${pkgdir}" install

  install -D -m644 doc/LICENSING.txt "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}