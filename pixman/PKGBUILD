
# odd numbers rls skip....
pkgname=pixman
pkgver=0.33.6
pkgrel=1
pkgdesc="Pixman library"
arch=('x86_64')
url="http://xorg.freedesktop.org"
license=('custom')
depends=('glibc')
options=('!libtool')
source=("http://xorg.freedesktop.org/releases/individual/lib/${pkgname}-${pkgver}.tar.bz2")
md5sums=('e903e990f906bf76166717d6a6582950')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make check 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
