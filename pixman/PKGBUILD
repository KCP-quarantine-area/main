
pkgname=pixman
pkgver=0.30.2
pkgrel=1
pkgdesc="Pixman library"
arch=('x86_64')
url="http://xorg.freedesktop.org"
license=('custom')
depends=('glibc')
options=('!libtool')
source=("http://xorg.freedesktop.org/releases/individual/lib/${pkgname}-${pkgver}.tar.bz2")
md5sums=('2d0588e20dc1308b29b2fca68dad9a9c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
