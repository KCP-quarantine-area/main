
pkgname=libpng12
_realname=libpng
pkgver=1.2.50
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=('zlib')
options=('!libtool')
source=("http://sourceforge.net/projects/libpng/files/libpng-${pkgver}.tar.xz"
        "http://sourceforge.net/projects/apng/files/libpng/libpng12/libpng-${pkgver}-apng.patch.gz")
md5sums=('a3e00fccbfe356174ab515b5c00641c7'
         'b06ac3f6a6f982abc2036359665e82a9')


build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p0 -i "${srcdir}/libpng-${pkgver}-apng.patch"
  sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' configure.ac

  libtoolize --force --copy 
  aclocal 
  autoconf 
  automake --add-missing 
  
  ./configure --prefix=/usr 
  make ECHO=echo 
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make ECHO=echo DESTDIR="${pkgdir}" install 

  rm -rf "${pkgdir}/usr/share"
  rm -rf "${pkgdir}/usr/bin/libpng-config"
  rm -rf "${pkgdir}/usr/lib/"{libpng.so,libpng.a}
  rm -fr "${pkgdir}/usr/lib/pkgconfig/libpng.pc"
  rm -rf "${pkgdir}/usr/include/"{pngconf.h,png.h}
}