
pkgname=networkmanager
_pkgname=NetworkManager
pkgver=1.0.12
_pkgver=1.0
pkgrel=2
pkgdesc="Network Management daemon"
arch=('x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
depends=('dbus-glib' 'iproute2' 'libnl' 'nss' 'polkit' 'systemd' 'wpa_supplicant' 'ppp' 'dhcpcd'
         'modemmanager' 'libndp' 'libteam' 'libsoup' 'libgudev')
makedepends=('intltool' 'dhclient' 'iptables' 'gobject-introspection' 'netctl')
optdepends=('dhclient: alternative DHCP/DHCPv6 client'
            'iptables: Connection sharing'
            'dnsmasq: Connection sharing'
            'bluez: Bluetooth support'
            'openresolv: openresolv support')
options=('!libtool')
backup=('etc/NetworkManager/NetworkManager.conf')
install=networkmanager.install
source=("https://download.gnome.org/sources/NetworkManager/${_pkgver}/$_pkgname-$pkgver.tar.xz"
        'NetworkManager.conf' 
        'disable_set_hostname.patch'
        'wifiutils.patch')
md5sums=('ebb273456a81ccf9dfaf2461061b0e96'
         'e6549aab4cb721a87becd4585efd5148'
         '6b5415350c04304a28d29294441a07ae'
         '252b17d19be093698e4b66e84a49f3bc')

build() {
  cd $_pkgname-$pkgver

  patch -p1 -i ../disable_set_hostname.patch
  # wifi failures with 1.0.12 https://plus.google.com/+TonatiuhMiramontesP%C3%A9rez/posts/c5W7ySB83SD
  patch -p1 -i ../wifiutils.patch

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/networkmanager \
    --with-crypto=nss \
    --with-dhclient=/usr/sbin/dhclient \
    --with-dhcpcd=/usr/sbin/dhcpcd \
    --with-pppd=/usr/sbin/pppd \
    --with-pppoe=/usr/sbin/pppoe \
    --with-iptables=/usr/sbin/iptables \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --with-kernel-firmware-dir=/lib/firmware \
    --with-udev-dir=/usr/lib/udev \
    --with-resolvconf=/usr/sbin/resolvconf \
    --with-session-tracking=systemd \
    --with-modem-manager-1 \
    --disable-static \
    --enable-more-warnings=no \
    --disable-wimax \
    --enable-modify-system \
    --with-pppd-plugin-dir=/usr/lib/pppd/2.4.7

  make
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -m644 ../NetworkManager.conf "$pkgdir/etc/NetworkManager/"
  install -m755 -d "$pkgdir/etc/NetworkManager/dnsmasq.d"

  rm -r "$pkgdir/var/run"  
}
