
pkgname=suil
pkgver=0.8.0
pkgrel=1
pkgdesc="A lightweight C library for loading and wrapping LV2 plugin UIs"
arch=('x86_64')
url="http://drobilla.net/software/suil/"
license=('custom:ISC')
depends=('lv2')
makedepends=('python2' 'qt')
source=("http://download.drobilla.net/$pkgname-$pkgver.tar.bz2")
md5sums=('8b6039593b2b8d6838b3b29e36874c1c')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i "/ldconfig/d" wscript

  python2 waf configure --prefix=/usr
  python2 waf
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf install --destdir="$pkgdir"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

