
pkgname=pyqt5-python3
pkgver=5.5.1
pkgrel=2
pkgdesc="A set of Python3 bindings for the Qt toolkit"
arch=('x86_64')
url="http://riverbankcomputing.co.uk/software/pyqt/intro"
license=('GPL')
depends=('sip3' 'dbus-python3' 'qt5-multimedia' 'qt5-serialport' 'qt5-svg' 'qt5-webkit')
makedepends=('qt5-tools')
source=("http://downloads.sourceforge.net/pyqt/PyQt5/PyQt-gpl-${pkgver}.tar.gz")
#source=("http://www.riverbankcomputing.com/static/Downloads/PyQt5/PyQt-gpl-${pkgver}.tar.gz")
md5sums=('586ed481b734c665b52fbb4f32161ff7')

build() {
  cd ${srcdir}/PyQt-gpl-${pkgver}

  python3 configure.py \
    --confirm-license \
    --qsci-api \
    -q /usr/lib/qt5/bin/qmake \
    --designer-plugindir /usr/lib/qt5/plugins/designer

  find -name 'Makefile' | xargs sed -i 's|-Wl,-rpath,/usr/lib||g;s|-Wl,-rpath,.* ||g'

  make
}

package(){
  cd ${srcdir}/PyQt-gpl-${pkgver}
  # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
  make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install
}