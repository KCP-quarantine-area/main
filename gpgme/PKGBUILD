
pkgname=gpgme
pkgver=1.6.0
pkgrel=1
pkgdesc="A C wrapper library for GnuPG"
arch=('x86_64')
url="http://www.gnupg.org/related_software/gpgme/"
license=('GPL')
depends=('libgpg-error' 'gnupg2')
install=${pkgname}.install
options=('!libtool' '!emptydirs')
source=("ftp://ftp.gnupg.org/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('60d730d22e8065fd5de309e8b98e304b')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
       --disable-static \
       --disable-gpgsm-test \
       --disable-fd-passing
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
