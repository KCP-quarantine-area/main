
pkgname=maxima
pkgver=5.33.0
pkgrel=2
pkgdesc="Maxima - a sophisticated Computer Algebra System."
arch=('x86_64')
license=('GPL')
url="http://maxima.sourceforge.net"
depends=('sbcl=1.1.18' 'texinfo' 'sh')
optdepends=('gnuplot: plotting capabilities' \
            'tk: graphical xmaxima interface')
# needs rebuild when bash changes version
options=('!makeflags' '!zipman') 
install="maxima.install"
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.desktop")
md5sums=('340b7eedfdd62dd4ef72e61cca66f5fb'
         '24aa81126fbb8b726854e5a80d4c2415')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i "s|${PYTHONBIN:-python}|python2|" doc/info/extract_categories.sh

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --libexecdir=/usr/lib \
              --enable-sbcl \
              --with-default-lisp=sbcl

  touch doc/info/maxima.info
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  install -d "${pkgdir}/usr/share/pixmaps"
  ln -s /usr/share/maxima/${pkgver}/xmaxima/maxima-new.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}
