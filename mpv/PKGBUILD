
pkgname=mpv
pkgver=0.13.0
pkgrel=1
pkgdesc='Video player based on MPlayer/mplayer2'
arch=('x86_64')
license=('GPL')
url='http://mpv.io'
depends=('ffmpeg' 'lcms2' 'libdvdread' 'libcdio-paranoia' 'libgl' 'enca' 'libxinerama'
         'libxv' 'libxkbcommon' 'libva' 'wayland' 'desktop-file-utils' 'hicolor-icon-theme'
         'xdg-utils' 'libdvdnav' 'youtube-dl' 'libass' 'libbluray' 'lua' 'libxrandr' 'jack'
         'rubberband' 'samba' 'libxss' 'libcaca')
makedepends=('mesa' 'python2-docutils' 'ladspa')
options=('!emptydirs' '!buildflags')
install=mpv.install
source=("https://github.com/mpv-player/mpv/archive/v${pkgver}.tar.gz")
#source=("https://github.com/mpv-player/mpv/archive/${_commit}.zip")
md5sums=('553ff7a2d7f9c6a1e456ccdc0c352287')

prepare() {
    cd ${pkgname}-${pkgver}

    ./bootstrap.py
}

build() {
    cd ${pkgname}-${pkgver}

    ./waf configure --prefix=/usr \
	--confdir=/etc/mpv \
	--enable-libmpv-shared \
	--enable-cdda \
	--enable-sdl1
		
    ./waf build
}

package() {
    cd ${pkgname}-${pkgver}
    ./waf install --destdir="${pkgdir}"

    install -d "${pkgdir}/usr/share/doc/mpv/examples"
    install -m644 etc/{input,example}.conf "${pkgdir}/usr/share/doc/mpv/examples"
    install -m644 DOCS/{encoding.rst,tech-overview.txt} "${pkgdir}/usr/share/doc/mpv"
}
