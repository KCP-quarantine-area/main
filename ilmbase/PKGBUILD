
pkgname=ilmbase
pkgver=2.1.0
pkgrel=1
pkgdesc="IlmThread is a thread abstraction library for use with OpenEXR"
url="http://www.openexr.com"
arch=('x86_64')
license=('custom')
depends=('gcc-libs')
options=('!libtool')
source=("http://download.savannah.nongnu.org/releases/openexr/$pkgname-$pkgver.tar.gz")
md5sums=('8ba2f608191ad020e50277d8a3ba0850')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/$pkgname-$pkgver
  # one of the tests fails randomly for an unknown reason
  make check || true
}

package() {
  cd ${srcdir}/$pkgname-$pkgver
  
  make DESTDIR=${pkgdir} install
  install -D -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}
