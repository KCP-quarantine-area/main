
pkgname=geoip-database
pkgver=20160303
pkgrel=1
pkgdesc="GeoLite country geolocation database compiled by MaxMind"
arch=('x86_64')
url="http://www.maxmind.com/app/ip-location"
license=('custom:OPEN DATA LICENSE')
# version: curl -sI https://geolite.maxmind.com/download/geoip/database/GeoIPv6.dat.gz | grep ^Last-Modified: | cut -d' ' -f2- | tr -d '\r'
source=("GeoIP-${pkgver}.dat.gz::http://geolite.maxmind.com/download/geoip/database/GeoLiteCountry/GeoIP.dat.gz"
        "GeoIPv6-${pkgver}.dat.gz::http://geolite.maxmind.com/download/geoip/database/GeoIPv6.dat.gz"
        "http://geolite.maxmind.com/download/geoip/database/LICENSE.txt")
noextract=("GeoIP-${pkgver}.dat.gz"
           "GeoIPv6-${pkgver}.dat.gz")
md5sums=('f846c35e71b663e544dcd42fd11389f6'
         'e0c4228c8f72e0bfbc5c1e54a55dade3'
         'a1381bd1aa0a0c91dc31b3f1e847cf4a')

build() {
  cd "${srcdir}"

  for _database_name in GeoIP GeoIPv6; do
    gunzip -c ${_database_name}-${pkgver}.dat.gz >${_database_name}.dat
  done
}

check() {
  cd "${srcdir}"

  for _database in {GeoIP,GeoIPv6}.dat; do
    if ! grep -q 'MaxMind Inc All Rights Reserved' ${_database}; then
      error "Copyright notice is missing from ${_database}; something is fishy!"
      return 1
    fi
  done
}

package() {
  cd "${srcdir}"

  install -d "${pkgdir}/usr/share/GeoIP"
  install -m0644 -t "${pkgdir}/usr/share/GeoIP" GeoIP.dat GeoIPv6.dat

  install -Dm0644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
