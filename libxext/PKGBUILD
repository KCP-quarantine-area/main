
pkgname=libxext
pkgver=1.3.1
pkgrel=2
pkgdesc="X11 miscellaneous extensions library"
arch=('x86_64')
url=('http://xorg.freedesktop.org/')
license=('custom')
depends=('libx11' 'xextproto')
makedepends=('pkg-config')
options=('!libtool')
source=(${url}/releases/individual/lib/libXext-${pkgver}.tar.bz2)
md5sums=('71251a22bc47068d60a95f50ed2ec3cf')

build() {
  cd "${srcdir}/libXext-${pkgver}"
  
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/libXext-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}