
pkgname=djvulibre
pkgver=3.5.25.3
_pkgver=3.5.25
pkgrel=3
pkgdesc="Suite to create, manipulate and view DjVu ('déjà vu') documents"
arch=("x86_64")
license=('GPL')
url="http://djvu.sourceforge.net/"
depends=('gcc-libs' 'libtiff' 'bash' 'hicolor-icon-theme')
makedepends=('librsvg')
options=('!libtool')
source=("http://downloads.sourceforge.net/djvu/${pkgname}-${pkgver}.tar.gz")
md5sums=('5f45d6cd5700b4dd31b1eb963482089b')

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  
  ./configure --prefix=/usr --disable-desktopfiles
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  make DESTDIR="${pkgdir}" install

  #Install icons for mime-types.
  install -m755 -d ${pkgdir}/usr/share/icons/hicolor/{22x22,32x32,48x48,64x64}/mimetypes
  for sz in 22 32 48 64; do
    install -m644 desktopfiles/prebuilt-hi${sz}-djvu.png "${pkgdir}/usr/share/icons/hicolor/${sz}x${sz}/mimetypes/image-vnd.djvu.mime.png"
  done
}
