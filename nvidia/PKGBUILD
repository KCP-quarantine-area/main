
_extramodules=extramodules-3.13
_kver=$(cat /lib/modules/${_extramodules}/version)

pkgname=('nvidia' 'khd-nvidia')
pkgver=337.25
pkgrel=1
arch=('x86_64')
url="http://www.nvidia.com/"
makedepends=('linux-headers' 'linux>=3.13' 'linux<3.14' "nvidia-utils=${pkgver}")
license=('custom')
# get from github once hardware-detection is up:
source=('nvidia_supported')
md5sums=('62d0cdb856cfae64753c0f3acf729d47')

_pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"
#_pkg="NVIDIA-Linux-x86_64-${pkgver}"
source+=("http://us.download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/${_pkg}.run")
md5sums+=('66e19d6d621c9e70fbd5374dd8b44341')

build() {
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only

    cd "${_pkg}/kernel"
    
    make SYSSRC=/lib/modules/"${_kver}/build" module
}

package_nvidia() {
    pkgdesc="NVIDIA drivers for linux."
    depends=('linux>=3.13' 'linux<3.14' "nvidia-utils=${pkgver}")
    conflicts=('nvidia-96xx' 'nvidia-173xx' 'nvidia-304xx')
    install=nvidia.install
    
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/etc/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}

package_khd-nvidia() {
    pkgdesc="KHD ids for nvidia $pkgver"
    arch=('x86_64')
    conflicts=('chd-nvidia')
    replaces=('chd-nvidia')

    cd "${srcdir}/${_pkg}"

    install -dm755 "${pkgdir}/opt/kdeos/hwdb"
    sh -e "${srcdir}/nvidia_supported" README.txt kernel/nv-kernel.o \
        > "${pkgdir}/opt/kdeos/hwdb/hw_nvidia_hwdb"
}
