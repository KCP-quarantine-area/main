
pkgname=libmbim
pkgver=1.12.2
pkgrel=2
pkgdesc="Glib-based library for talking to WWAN modems and devices which speak the Mobile Interface Broadband Model"
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/libmbim/"
license=('GPL2')
depends=('glib2' 'bash' 'libgudev' 'systemd')
makedepends=('python2')
options=('!libtool')
source=("http://www.freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.xz")
md5sums=('921fb5ab3f13f1e00833e009d8f3b4f6')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --disable-static 
        
  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}