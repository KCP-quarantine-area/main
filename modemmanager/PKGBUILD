
pkgname=modemmanager
_realname=ModemManager
pkgver=0.6.0.0
pkgrel=2
pkgdesc="Mobile broadband modem management service"
arch=('x86_64')
url="http://cgit.freedesktop.org/ModemManager/ModemManager/"
license=('GPL2')
depends=('dbus-glib' 'systemd' 'ppp' 'polkit')
makedepends=('intltool')
options=('!libtool')
source=("http://download.gnome.org/sources/${_realname}/0.6/${_realname}-${pkgver}.tar.xz"
	'ModemManager.service')
optdepends=('usb_modeswitch: install if your modem shows up as a storage drive')
md5sums=('f32640f6672d997ec0887307186e9639'
         'b291fdc32caa83b81ba624cae7850c22')

build() {
  cd "$srcdir/${_realname}-${pkgver}"
  
   ./configure --prefix=/usr \
   	--sysconfdir=/etc \
	--localstatedir=/var \
	--with-udev-base-dir=/usr/lib/udev \
	--disable-static \
	--with-polkit
  make
}

package() {
  cd "$srcdir/${_realname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  
  install -Dm644 "$srcdir/ModemManager.service" "$pkgdir/usr/lib/systemd/system/ModemManager.service"
  echo "SystemdService=ModemManager.service" \
    >> "$pkgdir/usr/share/dbus-1/system-services/org.freedesktop.ModemManager.service"
}
