
pkgname=modemmanager
_realname=ModemManager
pkgver=1.0.0
pkgrel=2
pkgdesc="Mobile broadband modem management service"
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/ModemManager/"
license=('GPL2')
depends=('dbus-glib' 'systemd' 'ppp' 'polkit' 'hicolor-icon-theme' 'libqmi' 'libmbim')
makedepends=('intltool' 'python2')
optdepends=('usb_modeswitch: install if your modem shows up as a storage drive')
options=('!libtool')
source=("http://www.freedesktop.org/software/$_realname/$_realname-$pkgver.tar.xz")
md5sums=('bb59fda111d8e1038b02ed7e57efe83a')

build() {
  cd "$srcdir/${_realname}-$pkgver"
  
   ./configure --prefix=/usr \
   	--sysconfdir=/etc \
	--localstatedir=/var \
	--with-udev-base-dir=/usr/lib/udev \
	--disable-static \
	--disable-gtk-doc \
	--with-polkit=strict #default, permissive better?
	#--with-newest-qmi-commands **not clear what the needed deps are to enable
  make
}

package() {
  cd "$srcdir/${_realname}-$pkgver"
  
  make DESTDIR="$pkgdir" install
}
