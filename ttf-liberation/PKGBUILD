
pkgname=ttf-liberation
pkgver=2.00.1
pkgrel=2
pkgdesc="Red Hats Liberation fonts"
arch=('x86_64')
license=('custom' 'GPL2')
url="https://www.redhat.com/promo/fonts/"
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-font-utils')
makedepends=('fontforge' 'python2-fonttools')
install=$pkgname.install
source=("https://fedorahosted.org/releases/l/i/liberation-fonts/liberation-fonts-${pkgver}.tar.gz"
        '30-0-liberation-mono.conf'
        '30-0-liberation-sans.conf'
        '30-0-liberation-serif.conf')
md5sums=('a0dfdcffcd0398afe5f57269198846e9'
         '23ce227d5d72c6df2c69caf367d96341'
         '88e91e718053d46cb1215067ffb0024a'
         '5bc229f0f7ad387f1ba311148fcbff00')

build() {
  cd "${srcdir}/liberation-fonts-${pkgver}"
  
  make 
}
package() {
  cd "$srcdir/liberation-fonts-${pkgver}/liberation-fonts-ttf-${pkgver}"

  install -d "$pkgdir/usr/share/fonts/TTF/"
  install -m644 *.ttf "$pkgdir/usr/share/fonts/TTF/"
  
  install -Dm0644 ${srcdir}/30-0-liberation-mono.conf "$pkgdir/etc/fonts/conf.avail/30-$pkgname-mono.conf"
  install -Dm0644 ${srcdir}/30-0-liberation-sans.conf "$pkgdir/etc/fonts/conf.avail/30-$pkgname-sans.conf"
  install -Dm0644 ${srcdir}/30-0-liberation-serif.conf "$pkgdir/etc/fonts/conf.avail/30-$pkgname-serif.conf"

  install -Dm0644 "${srcdir}/liberation-fonts-${pkgver}/LICENSE" \
	"${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

