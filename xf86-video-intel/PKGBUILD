
pkgname=xf86-video-intel
pkgver=2.21.11
pkgrel=1
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers"
arch=('x86_64')
url="http://xorg.freedesktop.org"
license=('custom')
depends=('mesa' 'libxvmc' 'libpciaccess' 'libdrm' 'xcb-util' 'libxfixes' 'systemd')
makedepends=('xorg-server>=1.12.3' 'libx11' 'xf86driproto' 'glproto' 'libxrender'
             'resourceproto' 'scrnsaverproto')
conflicts=('xorg-server<1.12.3' 'xf86-video-i810' 'xf86-video-intel-legacy')
options=('!libtool')
groups=('xorg-drivers' 'xorg')
install=xf86-video-intel.install
source=("${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2")
md5sums=('739b5671df7a29590234ffb5535c9f7f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --enable-dri \
              --enable-sna \
              --enable-uxa \
              --with-default-accel=sna
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
