
pkgname=xf86-video-intel
pkgver=2.99.911
pkgrel=3
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers"
arch=('x86_64')
url="http://xorg.freedesktop.org"
license=('custom')
depends=('mesa' 'libxvmc' 'libpciaccess' 'libdrm' 'xcb-util' 'libxfixes' 'systemd' 'glamor-egl')
makedepends=('xorg-server>=1.15.0' 'libx11' 'xf86driproto' 'glproto' 'libxrender'
             'resourceproto' 'scrnsaverproto')
options=('!libtool')
groups=('xorg-drivers' 'xorg')
install=xf86-video-intel.install
source=("${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2")
md5sums=('6130343b314e6a43be4bc02461d09fa7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --libexecdir=/usr/lib \
              --enable-dri \
              --enable-glamor \
              --enable-sna \
              --enable-uxa \
              --with-default-accel=glamor
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
