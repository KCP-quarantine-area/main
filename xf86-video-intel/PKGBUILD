
pkgname=xf86-video-intel
pkgver=2.99.908
pkgrel=2
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers"
arch=('x86_64')
url="http://xorg.freedesktop.org"
license=('custom')
depends=('mesa' 'libxvmc' 'libpciaccess' 'libdrm' 'xcb-util' 'libxfixes' 'systemd' 'glamor-egl')
makedepends=('xorg-server>=1.15.0' 'libx11' 'xf86driproto' 'glproto' 'libxrender'
             'resourceproto' 'scrnsaverproto')
options=('!libtool')
groups=('xorg-drivers' 'xorg')
install=xf86-video-intel.install
source=("${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2"
        'sna_cpu.patch')
md5sums=('602774a8d810d092c75d8eb62d9b63f4'
         '5a423e124fab47513a678ebb73e66ad6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/sna_cpu.patch

  ./configure --prefix=/usr \
              --enable-dri \
              --enable-glamor \
              --enable-sna \
              --enable-uxa \
              --with-default-accel=sna
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
