
# NOTE: Disabled the gtk2 module so no gtk-preview

pkgname=mlt
pkgver=0.9.2
pkgrel=2
pkgdesc="An open source multimedia framework"
arch=('x86_64')
url="http://www.mltframework.org"
license=('GPL')
depends=('sdl_image' 'libsamplerate' 'libdv' 'qt5-svg' 'sox' 'libxml2' 'ffmpeg'
         'frei0r-plugins' 'python2' 'jack' 'ladspa')
makedepends=('swig')
conflicts=('mlt-python-bindings')
provides=('mlt-python-bindings')
replaces=('mlt-python-bindings')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('f88ead8fcea628a65ebf329cc4470b6d')

build() {
  # mlt
  cd "${srcdir}/mlt-${pkgver}"

  ./configure --prefix=/usr \
              --enable-gpl \
              --enable-gpl3 \
              --qt-libdir=/usr/lib/ \
              --qt-includedir=/usr/include/qt5 \
              --avformat-vdpau \
              --disable-gtk2 \
              --avformat-swscale  
  make

  # mlt python bindings
  cd "${srcdir}/mlt-${pkgver}/src/swig/python"
  sed -i 's_path=`which python_path=`which python2_' build
  sed -i 's_`python -c_`python2 -c_' build
  sed -i 's#python-config#python2-config#' build
  ./build
}

package () {
  cd "${srcdir}/mlt-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  # install python bindings
  cd "$srcdir/mlt-$pkgver/src/swig/python"
  mkdir -p "${pkgdir}/usr/lib/python2.7/"
  install -m0755 mlt.py "${pkgdir}/usr/lib/python2.7/"
  install -m0755 _mlt.so "${pkgdir}/usr/lib/python2.7/"
  install -m0755 mlt_wrap.o "${pkgdir}/usr/lib/python2.7/"
}
