
pkgname=syslinux
pkgver=4.07
pkgrel=1
arch=('x86_64')
pkgdesc="Collection of boot loaders that boot from FAT, ext2/3/4 and btrfs filesystems, from CDs and via PXE"
url="http://syslinux.zytor.com/"
license=('GPL2')
depends=('perl' 'glibc')
makedepends=('nasm')
backup=('boot/syslinux/syslinux.cfg')
install=syslinux.install
source=("http://www.kernel.org/pub/linux/utils/boot/syslinux/$pkgname-${pkgver}.tar.bz2"
        'syslinux-dont-build-dos-windows-targets.patch'
        'syslinux.cfg'
        'syslinux-install_update')
md5sums=('9ff6e1b94efab931fb4717b600d88779'
         '1528c376e43f0eaccaa80d8ad1bc13b4'
         'a8ed499502fb265db36c0793b29d5a99'
         'b5b4953c0a5a01cd16441402f6321765')

build() {
  # Do not try to build syslinux with our default LDFLAGS, it will fail
  unset LDFLAGS
  cd "$srcdir"/$pkgname-${pkgver}
  # Do not try to build the Windows or DOS installers
  patch -p1 -i "$srcdir"/syslinux-dont-build-dos-windows-targets.patch
  # Fix FHS manpage path
  sed 's|/usr/man|/usr/share/man|g' -i mk/syslinux.mk
  make installer
  make -C utils
  make -C com32
}

package() {
  cd "$srcdir"/$pkgname-${pkgver}
  make INSTALLROOT="$pkgdir" AUXDIR=/usr/lib/syslinux install

  # Install the default configuration
  install -D -m644 "$srcdir"/syslinux.cfg "$pkgdir"/boot/syslinux/syslinux.cfg
  # Install the installation and update script
  # This script is maintained at git://gist.github.com/772138.git
  install -D -m755 "$srcdir"/syslinux-install_update "$pkgdir"/usr/sbin/syslinux-install_update
}
