
pkgname=gegl
pkgver=0.2.0
pkgrel=2
pkgdesc="Graph based image processing framework."
arch=('x86_64')
url="http://www.gegl.org/"
license=('GPL3' 'LGPL3')
depends=('babl' 'gtk2')
makedepends=('ruby' 'lua' 'openexr' 'ffmpeg' 'librsvg' 'jasper' 'exiv2' 'intltool')
optdepends=('openexr: for using the openexr plugin' 
            'ffmpeg: for using the ffmpeg plugin'
            'librsvg: for using the svg plugin' 
            'jasper: for using the jasper plugin')
options=('!libtool')
source=("ftp://ftp.gimp.org/pub/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2"
	'gentoo-gegl-0.2.0-ffmpeg-0.11.diff')
md5sums=('32b00002f1f1e316115c4ed922e1dec8'
         'aada2b2e776b8934ab115df940f74b9d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/gentoo-gegl-0.2.0-ffmpeg-0.11.diff"

  ./configure --prefix=/usr \
              --with-sdl \
              --with-openexr \
              --with-librsvg \
              --with-libavformat \
              --with-jasper \
              --disable-docs 
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make install DESTDIR="${pkgdir}"
}

