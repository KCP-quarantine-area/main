
pkgbase=gtkmm
pkgname=('gtkmm' 'gtkmm-docs')
pkgver=2.24.4
_pkgver=2.24
pkgrel=5
url="http://www.gtkmm.org/"
arch=('x86_64')
makedepends=('gtk2' 'pangomm' 'atkmm' 'glibmm-docs')
license=('LGPL')
options=('!libtool' '!emptydirs')
source=("http://ftp.gnome.org/pub/GNOME/sources/gtkmm/${_pkgver}/${pkgbase}-${pkgver}.tar.xz"
        'glibmm_includes.patch')
md5sums=('b9ac60c90959a71095f07f84dd39961d'
         '6c649470185f750ef879c7fcda1ea32b')

build() {
  cd ${pkgbase}-${pkgver}
  patch -p1 -i $srcdir/glibmm_includes.patch
  
  CXXFLAGS+=' -std=c++11'
  ./configure --prefix=/usr
  make
}

package_gtkmm() {
  pkgdesc="C++ bindings for gtk2"
  depends=('gtk2' 'pangomm' 'atkmm')
  cd ${pkgbase}-${pkgver}

  sed -i -e 's|^doc_subdirs|#doc_subdirs|' Makefile

  make DESTDIR=${pkgdir} install
}

package_gtkmm-docs() {
  pkgdesc="Developer documentation for gtkmm v2"

  cd ${pkgbase}-${pkgver}
  make -C docs DESTDIR=${pkgdir} install
}
