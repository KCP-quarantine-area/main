
pkgname=libxft
pkgver=2.3.1
pkgrel=2
pkgdesc="FreeType-based font drawing library for X"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('fontconfig' 'libxrender')
makedepends=('pkgconfig')
options=('!libtool')
source=(${url}/releases/individual/lib/libXft-${pkgver}.tar.bz2
        'freetype2.patch')
md5sums=('78d64dece560c9e8699199f3faa521c0'
         '91aaca2853fc9f80004ae0f05c956eb6')

build() {
  cd "${srcdir}/libXft-${pkgver}"
  # lists.freedesktop.org/archives/xorg-devel/2013-December/039311.html
  patch -p1 -i ${srcdir}/freetype2.patch
  
  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --disable-static
  make
}

package() {
  cd "${srcdir}/libXft-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

}
