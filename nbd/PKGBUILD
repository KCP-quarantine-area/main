
pkgname=nbd
pkgver=3.7
pkgrel=1
pkgdesc="Tools for network block devices, allowing you to use remote block devices over TCP/IP."
arch=('x86_64')
url="http://nbd.sourceforge.net"
license=('GPL')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.xz"
	'config'
	'nbd.service')
depends=('glib2')
md5sums=('09a17fd19596b6908bff8b7db74141fc'
         '2d05d426b8c2708d5f8a0d028fcbae05'
         '5d1acc143018117174f79eff15afa038')

build(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --enable-syslog
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}"/config "${pkgdir}"/etc/nbd-server/config
  install -D -m755 "${srcdir}"/nbd.service "${pkgdir}"/usr/lib/systemd/system/nbd.service
  install -D -m644 README.md "${pkgdir}"/usr/share/doc/nbd/README.md
}
