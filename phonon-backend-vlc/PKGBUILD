
pkgname=phonon-backend-vlc
_pkgname=phonon-vlc
pkgver=0.9.0
pkgrel=1
pkgdesc='Phonon VLC backend'
arch=('x86_64')
url='http://phonon.kde.org'
license=('LGPL')
depends=('vlc')
makedepends=('qt' 'pkgconfig' 'cmake' 'automoc4' 'mesa' 'phonon')
provides=('phonon-backend')
source=("http://download.kde.org/stable/phonon/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('66615ae8df37408f47f369e64d836746')

build() {
	mkdir -p build
	cd build
	
	cmake ../${_pkgname}-${pkgver} \
		 -DCMAKE_BUILD_TYPE=Release \
		 -DCMAKE_INSTALL_PREFIX=/usr \
		 -DCMAKE_INSTALL_LIBDIR=lib 
	make
}

package() {
	cd build
	
	make DESTDIR=${pkgdir} install
} 
