
pkgname=phonon-backend-vlc
pkgver=0.8.0
pkgrel=1
pkgdesc='Phonon VLC backend'
arch=('x86_64')
url='http://phonon.kde.org'
license=('LGPL')
depends=('vlc')
makedepends=('qt' 'pkgconfig' 'cmake' 'automoc4' 'mesa' 'phonon')
groups=("kde-complete" "kde-uninstall")
provides=('phonon-backend')
source=("http://download.kde.org/stable/phonon/${pkgname}/$pkgver/${pkgname}-$pkgver.tar.xz")
md5sums=('f4c1f5c75d15931f02508cccef107e3d')

build() {
	cd "${srcdir}"
	mkdir -p build
	
	cd build
	cmake "../${pkgname}-$pkgver" \
		 -DCMAKE_BUILD_TYPE=Release \
		 -DCMAKE_INSTALL_PREFIX=/usr \
		 -DCMAKE_INSTALL_LIBDIR=lib 
	make
}

package() {
	cd "${srcdir}/build"
	
	make DESTDIR="${pkgdir}" install
} 
