
pkgname=libdrm
pkgver=2.4.62
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=('x86_64')
license=('custom')
depends=('glibc' 'libpciaccess')
makedepends=('cairo' 'valgrind' 'xorg-util-macros')
options=('!libtool')
url="http://dri.freedesktop.org/"
source=("http://dri.freedesktop.org/libdrm/${pkgname}-${pkgver}.tar.bz2"
        'COPYING')
md5sums=('c9291bae0e5ca65d1483821d3698d3ab'
         'ba65e71c481b94ef0fb6c23c7f21ffa1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i "/pthread-stubs/d" configure.ac
  
#  libtoolize --force
  autoreconf --force --install
  ./configure --prefix=/usr \
      --enable-udev \
      --enable-omap-experimental-api \
      --enable-exynos-experimental-api 
  make
}

check() {
  cd $pkgname-$pkgver
  
  make -k check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/"
}
