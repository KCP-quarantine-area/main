
_extramodules=extramodules-4.0-next
_kver=$(cat /lib/modules/${_extramodules}/version)

pkgname=nvidia-next
pkgver=346.59
pkgrel=4
pkgdesc="NVIDIA drivers for linux."
arch=('x86_64')
url="http://www.nvidia.com/"
depends=('linux-next>=4.0' 'linux-next<4.1' "nvidia-utils=${pkgver}")
makedepends=('linux-next-headers')
license=('custom')
install=nvidia-next.install
options=('!strip')
_pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"
#_pkg="NVIDIA-Linux-x86_64-${pkgver}"
source=("ftp://download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/${_pkg}.run")
md5sums=('db1cebc2a78eac85777bb4c7c2c8c70c')

build() {
  cd "${srcdir}" 
  sh "${_pkg}.run" --extract-only
  
  #cd "${_pkg}"
  #patch -p0 -i ${srcdir}/nv-drm.patch
  
  cd "${srcdir}/${_pkg}/kernel"
  make SYSSRC=/lib/modules/"${_kver}/build" module
  
  cd uvm
  make SYSSRC=/lib/modules/"${_kver}/build" module
}

package() {
  install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
  install -D -m644 "${srcdir}/${_pkg}/kernel/uvm/nvidia-uvm.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia-uvm.ko"
  gzip "${pkgdir}/lib/modules/${_extramodules}/"*.ko
  install -d -m755 "${pkgdir}/lib/modprobe.d"
  
  echo "blacklist nouveau" >> "${pkgdir}/lib/modprobe.d/nvidia.conf"
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-next.install"
}
