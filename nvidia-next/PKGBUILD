
_extramodules=extramodules-3.18-next
_kver=$(cat /lib/modules/${_extramodules}/version)

pkgname=nvidia-next
pkgver=346.35
pkgrel=2
pkgdesc="NVIDIA drivers for linux."
arch=('x86_64')
url="http://www.nvidia.com/"
depends=('linux-next>=3.18' 'linux-next<3.19' "nvidia-utils=${pkgver}")
makedepends=('linux-next-headers')
license=('custom')
install=nvidia-next.install
options=('!strip')
_pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"
#_pkg="NVIDIA-Linux-x86_64-${pkgver}"
source=("ftp://download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/${_pkg}.run"
        'nv-drm.patch')
md5sums=('9ca9f07f91361362f304ca8317ddd453'
         '79671a27131da619a33eb02ed0c2c031')

build() {
  cd "${srcdir}" 
  sh "${_pkg}.run" --extract-only
  
  cd "${_pkg}"
  patch -p0 -i ${srcdir}/nv-drm.patch
  
  cd "${srcdir}/${_pkg}/kernel"
  make SYSSRC=/lib/modules/"${_kver}/build" module
  
  cd uvm
  make SYSSRC=/lib/modules/"${_kver}/build" module
}

package() {
  install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
  install -D -m644 "${srcdir}/${_pkg}/kernel/uvm/nvidia-uvm.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia-uvm.ko"
  gzip "${pkgdir}/lib/modules/${_extramodules}/"*.ko
  install -d -m755 "${pkgdir}/lib/modprobe.d"
  
  echo "blacklist nouveau" >> "${pkgdir}/lib/modprobe.d/nvidia.conf"
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-next.install"
}
