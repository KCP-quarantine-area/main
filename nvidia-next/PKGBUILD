
_extramodules=extramodules-4.2-next
_kver=$(cat /lib/modules/${_extramodules}/version)

pkgname=nvidia-next
pkgver=352.55
pkgrel=2
pkgdesc="NVIDIA drivers for linux."
arch=('x86_64')
url="http://www.nvidia.com/"
depends=('linux-next>=4.2rc7' 'linux-next<4.3' "nvidia-utils=${pkgver}")
makedepends=('linux-next-headers')
license=('custom')
install=nvidia-next.install
options=('!strip')
_pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"
#_pkg="NVIDIA-Linux-x86_64-${pkgver}"
source=("ftp://download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/${_pkg}.run"
        'linux-4.2.patch')
md5sums=('b692dd13ffc481fc44af6d7dae2f0226'
         '5bad6b6ba2512c619f1dedb56516efa5')

build() {
  cd "${srcdir}" 
  sh "${_pkg}.run" --extract-only
  
  cd "${_pkg}"
  patch -p1 -i ${srcdir}/linux-4.2.patch
  
  cd "${srcdir}/${_pkg}/kernel"
  make SYSSRC=/lib/modules/"${_kver}/build" module
  
  cd uvm
  make SYSSRC=/lib/modules/"${_kver}/build" module
}

package() {
  install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
  install -D -m644 "${srcdir}/${_pkg}/kernel/uvm/nvidia-uvm.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia-uvm.ko"
  gzip "${pkgdir}/lib/modules/${_extramodules}/"*.ko
  install -d -m755 "${pkgdir}/lib/modprobe.d"
  
  echo "blacklist nouveau" >> "${pkgdir}/lib/modprobe.d/nvidia.conf"
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-next.install"
}
