
_extramodules=extramodules-3.11-next
_kver=$(cat /lib/modules/${_extramodules}/version)

pkgname=nvidia-next
pkgver=331.13
pkgrel=3
pkgdesc="NVIDIA drivers for linux."
arch=('x86_64')
url="http://www.nvidia.com/"
depends=('linux-next>=3.11' 'linux-next<3.12' "nvidia-utils=${pkgver}")
makedepends=('linux-next-headers')
license=('custom')
install=nvidia-next.install
options=('!strip')
_pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"
source=("ftp://download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/${_pkg}.run" \
        "linux3.11.patch")
md5sums=('08ae6c987c30885b2a9f662df23f061d'
         '6615840da025708edefb44a98c68f6f7')

build() {
  cd "${srcdir}"
  sh "${_pkg}.run" --extract-only
  
  cd "${_pkg}/kernel"
  patch -p2 -i ${srcdir}/linux3.11.patch
  
  make SYSSRC=/lib/modules/"${_kver}/build" module
}

package() {
  install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
  install -d -m755 "${pkgdir}/lib/modprobe.d"
  
  echo "blacklist nouveau" >> "${pkgdir}/lib/modprobe.d/nvidia.conf"
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-next.install"
  gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}
