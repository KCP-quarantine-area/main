
pkgname=python2-numpy
pkgver=1.8.0
pkgrel=2
pkgdesc="Scientific tools for Python"
arch=('x86_64')
license=('custom')
url="http://numpy.scipy.org/"
depends=('blas' 'lapack' 'python2')
makedepends=('python2-nose' 'gcc-fortran')
optdepends=('python2-nose: test suite')
provides=('python-numeric')
source=("http://downloads.sourceforge.net/numpy/numpy-${pkgver}.tar.gz")
md5sums=('2a4b0423a758706d592abb6721ec8dcd')

build() {
  cd "${srcdir}/numpy-${pkgver}"
  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
  
  export Atlas=None
  export LDFLAGS="$LDFLAGS -shared"
  
  python2 setup.py config_fc --fcompiler=gnu95 build
}

package() {
  cd "${srcdir}/numpy-${pkgver}"
  python2 setup.py config_fc --fcompiler=gnu95 install --prefix=/usr --root="${pkgdir}" --optimize=1

  install -m755 -d "${pkgdir}/usr/share/licenses/python2-numpy"
  install -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/python2-numpy/"
  
  install -m755 -d "${pkgdir}/usr/include/python2.7"
  ln -sf /usr/lib/python2.7/site-packages/numpy/core/include/numpy "${pkgdir}/usr/include/python2.7/numpy"
}