
pkgname=pkgfile
pkgver=13
pkgrel=1
pkgdesc="An alpm .files metadata explorer"
arch=('x86_64')
url="http://github.com/falconindy/pkgfile"
license=('MIT')
depends=('libarchive' 'curl' 'pcre' 'pacman')
install=pkgfile.install
source=("http://code.falconindy.com/archive/$pkgname/$pkgname-$pkgver.tar.xz")
md5sums=('7b4e20bf3ec6604cd439d5acd6ca871d')

build() {
  cd "$pkgname-$pkgver"

  ./configure
  make

  # generate a license file
  sed '/\*\//q' src/pkgfile.c >LICENSE
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

