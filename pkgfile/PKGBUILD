
pkgname=pkgfile
pkgver=12
pkgrel=2
pkgdesc="An alpm .files metadata explorer"
arch=('x86_64')
url="http://github.com/falconindy/pkgfile"
license=('MIT')
depends=('libarchive' 'curl' 'pcre' 'pacman')
install=pkgfile.install
source=("http://code.falconindy.com/archive/$pkgname/$pkgname-$pkgver.tar.xz")
md5sums=('3366e5dbf01ebb81197c4fc53c6bb3bf')

build() {
  cd "$pkgname-$pkgver"

  ./configure
  make

  # generate a license file
  sed '/\*\//q' src/pkgfile.c >LICENSE
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

