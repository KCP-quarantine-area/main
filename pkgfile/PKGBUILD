
pkgname=pkgfile
pkgver=15
pkgrel=3
pkgdesc="An alpm .files metadata explorer"
arch=('x86_64')
url="http://github.com/falconindy/pkgfile"
license=('MIT')
depends=('libarchive' 'curl' 'pcre' 'pacman')
install=pkgfile.install
source=("http://code.falconindy.com/archive/${pkgname}/${pkgname}-${pkgver}.tar.xz")
md5sums=('30b3c4f0b18a33ca0ce4538492093511')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --with-systemdsystemunitdir=/usr/lib/systemd/system
  make

  # generate a license file
  sed '/\*\//q' src/pkgfile.c >LICENSE
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

