
pkgname=smpeg
pkgver=0.4.4
pkgrel=6
pkgdesc="SDL MPEG Player Library"
arch=('x86_64')
url="http://icculus.org/smpeg/"
license=('LGPL')
depends=('sdl')
makedepends=('glu' 'mesa')
optdepends=('glu: to use glmovie')
options=('!libtool' '!makeflags')
source=("http://mirrors.dotsrc.org/lokigames/open-source/smpeg/${pkgname}-${pkgver}.tar.gz"
        'smpeg-0.4.4-gcc41.patch')
md5sums=('59c76ac704088ef5539210190c4e1fe3'
         '8b979a58307d7196655758bd3d2466c4')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i $srcdir/smpeg-0.4.4-gcc41.patch
  
  ./configure --prefix=/usr --mandir=/usr/share/man --disable-static
  make LDFLAGS+=-lstdc++
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  # fix aclocal warnings
  sed -i "s#(AM_PATH_SMPEG#([AM_PATH_SMPEG]#" ${pkgdir}/usr/share/aclocal/smpeg.m4
}
