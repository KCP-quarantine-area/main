
pkgname=qtscriptgenerator
pkgver=0.2.0
pkgrel=2
pkgdesc="Script generator for qt"
arch=('x86_64')
url="http://code.google.com/p/qtscriptgenerator/"
license=('GPL')
depends=('qtwebkit')
makedepends=('pkgconfig' 'mesa')
source=("https://qtscriptgenerator.googlecode.com/files/qtscriptgenerator-src-0.2.0.tar.gz"
        'phonon.patch'
        'no-phonon.patch')
md5sums=('9f82b0aa212f7938de37df46cd27165c'
         'c0266892ecc645b3407f2203ee11a808'
         'e47b9c362bc7195a0e0c95faf6e88f23')

build() {
  cd "${srcdir}"/$pkgname-src-$pkgver
  
  patch -p1 -i "${srcdir}"/phonon.patch 
  patch -p1 -i "${srcdir}"/no-phonon.patch
  # prepare plugins
  cd generator
  qmake
  make
  ./generator --include-paths=/usr/include

  # build plugins
  cd "${srcdir}"/$pkgname-src-$pkgver/qtbindings
  qmake
  make
}

package() {
  cd "${srcdir}"/$pkgname-src-$pkgver/qtbindings
  # move plugins to correct directory
  install -d "${pkgdir}"/usr/lib/qt/plugins/script
  mv "${srcdir}"/$pkgname-src-$pkgver/plugins/script/* \
    "${pkgdir}"/usr/lib/qt/plugins/script/
}
