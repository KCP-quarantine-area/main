
pkgname=pyqt
pkgver=4.10.2
pkgrel=1
pkgdesc="A set of Python bindings for the Qt toolkit"
arch=('x86_64')
url="http://riverbankcomputing.co.uk/software/pyqt/intro"
license=('GPL')
depends=('sip' 'qtwebkit' 'dbus-python')
makedepends=('phonon' 'python2-opengl' 'qt-assistant-compat' 'mesa')
optdepends=('phonon: enable audio and video in PyQt applications'
            'python2-opengl: enable OpenGL 3D graphics in PyQt applications'
            'qscintilla: QScintilla API'
            'qt-assistant-compat: add PyQt online help in Qt Assistant')
provides=('pyqt4')
replaces=('pyqt4')
conflicts=('pyqt4')
source=("http://sourceforge.net/projects/pyqt/files/PyQt4/PyQt-${pkgver}/PyQt-x11-gpl-${pkgver}.tar.gz"
        'yali.patch')
md5sums=('9257e12fec9b1b21e5a0d76d455d1691'
         '8eac0cc6cfe118052de3092f85e029a0')

build() {
  cd ${srcdir}/PyQt-x11-gpl-${pkgver}
  # only for yali builds, needs 4.9.6 to use this patch
  #patch -p0 -i ${srcdir}/yali.patch

  python2 configure.py \
    --confirm-license \
    -v /usr/share/sip \
    --qsci-api

  find -name 'Makefile' | xargs sed -i 's|-Wl,-rpath,/usr/lib||g;s|-Wl,-rpath,.* ||g'

  make
}

package(){
  cd ${srcdir}/PyQt-x11-gpl-${pkgver}
  # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
  make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install
}
