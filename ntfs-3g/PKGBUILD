
pkgname=ntfs-3g
_realname=ntfs-3g_ntfsprogs
pkgver=2016.2.22
pkgrel=1
pkgdesc="Stable read and write NTFS driver"
url="http://www.tuxera.com/community/ntfs-3g-download/"
arch=('x86_64')
license=('GPL2')
depends=('glibc' 'fuse' 'util-linux')
makedepends=('pkgconfig')
options=('!libtool')
conflicts=('ntfsprogs')
provides=("ntfsprogs=${pkgver}")
source=("http://www.tuxera.com/opensource/${_realname}-${pkgver}.tgz")
md5sums=('ccbe8672d0f757bd0c975b50aa4c512e')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  
  ./configure --prefix=/usr \
       --with-fuse=external \
       --disable-static \
       --enable-posix-acls \
       --enable-extras \
       --disable-ldconfig \
       --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" rootlibdir=/usr/lib install
  
  ln -s /bin/ntfs-3g "${pkgdir}/sbin/mount.ntfs"
}

