
pkgname=ntfs-3g
_realname=ntfs-3g_ntfsprogs
pkgver=2013.1.13
pkgrel=1
pkgdesc="Stable read and write NTFS driver"
url="http://www.tuxera.com/community/ntfs-3g-download/"
arch=('x86_64')
license=('GPL2')
depends=('glibc' 'fuse' 'util-linux')
makedepends=('pkgconfig')
options=('!libtool')
conflicts=('ntfsprogs')
provides=("ntfsprogs=${pkgver}")
source=("http://www.tuxera.com/opensource/${_realname}-${pkgver}.tgz")
md5sums=('2d6fb47ddf62b51733227126fe9227fe')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  
  ./configure --prefix=/usr \
    --with-fuse=external --disable-static \
    --enable-posix-acls --disable-ldconfig \
    --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  ln -s /bin/ntfs-3g "${pkgdir}/sbin/mount.ntfs"

  mv "$pkgdir"/lib/* "$pkgdir"/usr/lib/
  rm -rf "$pkgdir"/lib 
}

