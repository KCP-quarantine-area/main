
pkgname=gstreamer0.10
pkgver=0.10.36.2
pkgrel=1
pkgdesc="GStreamer Multimedia Framework"
arch=('x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('libxml2' 'glib2')
makedepends=('intltool' 'pkgconfig' 'gobject-introspection' 'git')
options=('!libtool')
#source=("${url}/src/gstreamer/gstreamer-${pkgver}.tar.xz")
source=("git://anongit.freedesktop.org/gstreamer-sdk/gstreamer#commit=3ddc31eaa18c3be1613e43430eca78a3e445639e"
        'test_check.patch'
        'bison3.patch')
md5sums=('SKIP'
         '54ca909b522b3626f3a23a8751c0acd7'
         '0c402cb1f1a9629479cff6950ce97624')

build() {
  cd "${srcdir}/gstreamer"
  patch -p1 -i "${srcdir}/test_check.patch"
  patch -p1 -i "${srcdir}/bison3.patch"
  
  sed -e 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' -i configure.ac
  NOCONFIGURE=1 ./autogen.sh

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
	--with-package-name="GStreamer (KaOS)" \
	--with-package-origin="http://kaosx.us/" \
	--disable-static

  make
}

check() {
  cd "${srcdir}/gstreamer"
  
  make check
}

package() {
  cd "${srcdir}/gstreamer"
  make DESTDIR="${pkgdir}" install

  #Remove unversioned gst-* binaries to get rid of conflicts
  cd "${pkgdir}/usr/bin"
  for bins in `ls *-0.10`; do
    rm -f ${bins/-0.10/}
  done
}
