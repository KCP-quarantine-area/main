
pkgname=gconf
pkgver=3.2.6
_pkgver=3.2
pkgrel=4
pkgdesc="A configuration database system"
arch=('x86_64')
license=('LGPL')
depends=('orbit2' 'libxml2' 'polkit' 'libldap' 'dbus-glib')
makedepends=('pkgconfig' 'intltool' 'gobject-introspection')
options=('!libtool')
install=gconf.install
url="http://www.gnome.org"
source=("http://ftp.gnome.org/pub/gnome/sources/GConf/${_pkgver}/GConf-${pkgver}.tar.xz"
        'gconf-merge-schema'
        'gconfpkg'
        'gconf-reload.patch'
        '01_xml-gettext-domain.patch')
md5sums=('2b16996d0e4b112856ee5c59130e822c'
         '240b473fa17b748fc9020caf372d936c'
         'b745f04b4121d8724c002b14a4c110b6'
         'cfcc8e15be7b8a48de4aa34336ff6090'
         '1f9362976d71312bdb57c57fa031681d')

build() {
  cd "${srcdir}/GConf-${pkgver}"
  # Patch from fedora - reloads gconf after installing schemas
  patch -p1 -i "${srcdir}/gconf-reload.patch"
  # http://bugzilla.gnome.org/show_bug.cgi?id=568845
  patch -p1 -i "${srcdir}/01_xml-gettext-domain.patch"

  # Python2 fix
  sed -i '1s|#!/usr/bin/env python$|&2|' gsettings/gsettings-schema-convert

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/GConf \
      --disable-static --enable-defaults-service 

  make pkglibdir=/usr/lib/GConf
}

package() {
  cd "${srcdir}/GConf-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/etc/gconf/gconf.xml.system"
  install -m755 "${srcdir}/gconf-merge-schema" "${pkgdir}/usr/bin/"
  install -Dm755 "${srcdir}/gconfpkg" "${pkgdir}/usr/sbin/gconfpkg"
}
