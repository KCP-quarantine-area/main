
pkgname=imagemagick
pkgver=6.8.7.6
pkgrel=1
pkgdesc="An image viewing/manipulation program"
arch=('x86_64')
url="http://www.imagemagick.org/"
license=('custom')
depends=('libltdl' 'lcms' 'lcms2' 'libxt' 'gcc-libs' 'bzip2' 'freetype2' 'fontconfig'
	 'libxext' 'fftw' 'liblqr' 'libwmf' 'librsvg' 'libxml2' 'perl' 'libwebp')
makedepends=('ghostscript' 'openexr' 'jasper')
optdepends=('ghostscript: for Ghostscript support' 
            'openexr: for OpenEXR support'  
            'jasper: for JPEG-2000 support')
options=('!docs' 'libtool' '!emptydirs')
source=("http://sourceforge.net/projects/imagemagick/files/6.8.7-sources/ImageMagick-6.8.7-6.tar.xz"
	'perlmagick.rpath.patch')
md5sums=('9370a8f672fd1fab779ea9acbd6b0ad4'
         '0faf7158fd3a0904d9a70dd3e3c1fe9c')

build() {
	cd ${srcdir}/ImageMagick-${pkgver%.*}-${pkgver##*.}
	sed '/AC_PATH_XTRA/d' -i configure.ac
        autoreconf --force --install

	patch -p0 -i "${srcdir}/perlmagick.rpath.patch" 

	./configure --prefix=/usr --sysconfdir=/etc --with-modules --disable-static \
	    --enable-hdri --with-wmf --with-openexr --with-xml \
	    --with-gslib --with-gs-font-dir=/usr/share/fonts/Type1 \
	    --with-perl --with-perl-options="INSTALLDIRS=vendor" \
	    --with-lqr --with-rsvg --with-jp2 --with-lcms2 --with-webp \
	    --without-gvc --without-djvu --without-autotrace \
	    --without-jbig --without-fpx --without-dps --without-fftw --with-gcc-arch=x86-64
	make 
}

check() {
  cd ImageMagick-${pkgver%.*}-${pkgver##*.}
  
  make check
}

package() {
	cd ${srcdir}/ImageMagick-${pkgver%.*}-${pkgver##*.}

	make -j1 DESTDIR=${pkgdir} install 

	install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE 
	install -Dm644 NOTICE ${pkgdir}/usr/share/licenses/${pkgname}/NOTICE 

	#Cleaning
	rm -f ${pkgdir}/usr/lib/*.la 
}
