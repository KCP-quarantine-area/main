
pkgname=imagemagick
pkgver=6.8.5.5
pkgrel=1
pkgdesc="An image viewing/manipulation program"
arch=('x86_64')
url="http://www.imagemagick.org/"
license=('custom')
depends=('libltdl' 'lcms' 'lcms2' 'libxt' 'gcc-libs' 'bzip2' 'freetype2' 'fontconfig'
	 'libxext' 'fftw' 'liblqr' 'libwmf' 'librsvg' 'libxml2' 'perl' )
makedepends=('ghostscript' 'openexr' 'jasper')
optdepends=('ghostscript: for Ghostscript support' 
            'openexr: for OpenEXR support'  
            'jasper: for JPEG-2000 support')
options=('!makeflags' '!docs')
source=("http://sourceforge.net/projects/imagemagick/files/6.8.5-sources/ImageMagick-6.8.5-5.tar.xz"
	'perlmagick.rpath.patch')
md5sums=('82b9a9c22847e8ae0a56d5338b7bfff7'
         '46579145268422317d6a845b83738342')

build() {
	cd ${srcdir}/ImageMagick-${pkgver%.*}-${pkgver##*.}

	patch -p0 -i "${srcdir}/perlmagick.rpath.patch" 

	./configure --prefix=/usr --sysconfdir=/etc --with-modules --disable-static \
	    --enable-openmp --with-x --with-wmf --with-openexr --with-xml \
	    --with-gslib --with-gs-font-dir=/usr/share/fonts/Type1 \
	    --with-perl --with-perl-options="INSTALLDIRS=vendor" \
	    --with-lqr --with-rsvg --with-jp2 --with-lcms2 \
	    --without-gvc --without-djvu --without-autotrace --without-webp \
	    --without-jbig --without-fpx --without-dps --without-fftw 
	make 
}

package() {
	cd ${srcdir}/ImageMagick-${pkgver%.*}-${pkgver##*.}

	make DESTDIR=${pkgdir} install 

	install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE 
	install -Dm644 NOTICE ${pkgdir}/usr/share/licenses/${pkgname}/NOTICE 

	#Cleaning
	find ${pkgdir} -name '*.bs' -exec rm {} \; 
	rm -f ${pkgdir}/usr/lib/*.la 
}
