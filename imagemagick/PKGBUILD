
pkgname=imagemagick
pkgver=6.8.2.10
pkgrel=1
pkgdesc="An image viewing/manipulation program"
arch=('x86_64')
url="http://www.imagemagick.org/"
license=('custom')
depends=('libltdl' 'lcms' 'lcms2' 'libxt' 'gcc-libs' 'bzip2' 'freetype2' 'fontconfig'
	 'libxext' 'fftw' 'liblqr' 'libwmf' 'librsvg' 'libxml2' 'perl' )
makedepends=('ghostscript' 'openexr' 'jasper')
optdepends=('ghostscript: for Ghostscript support' 
            'openexr: for OpenEXR support'  
            'jasper: for JPEG-2000 support')
options=('!docs' 'libtool' '!emptydirs')
source=("http://sourceforge.net/projects/imagemagick/files/old-sources/6.x/6.8/ImageMagick-6.8.2-10.tar.gz"
	'perlmagick.rpath.patch')
md5sums=('780da797aa684a37f7a875f380524067'
         'ff9974decbfe9846f8e347239d87e4eb')

build() {
	cd ${srcdir}/ImageMagick-${pkgver%.*}-${pkgver##*.}
	sed '/AC_PATH_XTRA/d' -i configure.ac
        autoreconf --force --install

	patch -p0 -i "${srcdir}/perlmagick.rpath.patch" 

	./configure --prefix=/usr --sysconfdir=/etc --with-modules --disable-static \
	    --disable-openmp --with-wmf --with-openexr --with-xml \
	    --with-gslib --with-gs-font-dir=/usr/share/fonts/Type1 \
	    --with-perl --with-perl-options="INSTALLDIRS=vendor" \
	    --with-lqr --with-rsvg --with-jp2 --with-lcms2 \
	    --without-gvc --without-djvu --without-autotrace --without-webp \
	    --without-jbig --without-fpx --without-dps --without-fftw 
	make 
}

package() {
	cd ${srcdir}/ImageMagick-${pkgver%.*}-${pkgver##*.}

	make -j1 DESTDIR=${pkgdir} install 

	install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE 
	install -Dm644 NOTICE ${pkgdir}/usr/share/licenses/${pkgname}/NOTICE 

	#Cleaning
	rm -f ${pkgdir}/usr/lib/*.la 
}
