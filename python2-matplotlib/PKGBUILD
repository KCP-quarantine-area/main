
pkgname=python2-matplotlib
pkgver=1.3.1
pkgrel=1
pkgdesc="Python plotting library, making publication quality plots"
arch=('x86_64')
url='http://matplotlib.org'
license=('custom')
depends=('python2-pytz' 'python2-numpy' 'python2-cairo' 'pyqt' 'python2-dateutil'
         'python2-pyparsing')
makedepends=('tk' 'python2-gobject3' 'python2-pyparsing' 'python2-six'
             'ghostscript' 'texlive-bin')
optdepends=('tk: used by the TkAgg backend'
            'ghostscript: usetex dependencies'
            'texlive-bin: usetex dependencies')
source=("http://sourceforge.net/projects/matplotlib/files/matplotlib/matplotlib-${pkgver}/matplotlib-${pkgver}.tar.gz")
md5sums=('444624ad58de05f9029b0b5811e11c17')

prepare() {
   cp -a matplotlib-${pkgver} matplotlib-${pkgver}-py3

   cd matplotlib-${pkgver}
   for file in $(find . -name '*.py' -print); do
      sed -i -e "s|^#!.*/usr/bin/python|#!/usr/bin/python2|" \
             -e "s|^#!.*/usr/bin/env *python|#!/usr/bin/env python2|" ${file}
   done
}

build() {
   cd matplotlib-${pkgver}
   
   python2 setup.py build
}

package() {
   cd matplotlib-${pkgver}
   python2 setup.py install -O1 --skip-build --root "${pkgdir}" --prefix=/usr

   install -dm755 "${pkgdir}"/usr/share/licenses/python2-matplotlib
   install -m 644 doc/users/license.rst "${pkgdir}"/usr/share/licenses/python2-matplotlib
}

