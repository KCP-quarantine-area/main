
pkgname=libxcb
pkgver=1.9.1
pkgrel=1
epoch=1
pkgdesc="X11 client-side library"
arch=('x86_64')
url="http://xcb.freedesktop.org/"
depends=('xcb-proto>=1.8' 'libxdmcp' 'libxau')
makedepends=('pkgconfig' 'libxslt' 'python2')
options=('!libtool')
license=('custom')
source=("${url}/dist/${pkgname}-${pkgver}.tar.bz2"
        'libxcb-1.1-no-pthread-stubs.patch')
md5sums=('ed632cb0dc31b6fbd7ea5c0f931cf5a4'
         '450e494cb8f980ed956cb4403678e06f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/libxcb-1.1-no-pthread-stubs.patch"
  
  export PYTHON=/usr/bin/python2
  ./autogen.sh --prefix=/usr \
               --enable-xinput \
               --enable-xkb \
               --disable-static
  make
}

package () {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
