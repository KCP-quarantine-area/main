
pkgname=libxcb
pkgver=1.9.2
pkgrel=1
pkgdesc="X11 client-side library"
arch=('x86_64')
url="http://xcb.freedesktop.org/"
depends=('xcb-proto>=1.9' 'libxdmcp' 'libxau')
makedepends=('pkgconfig' 'libxslt' 'python2')
options=('!libtool')
license=('custom')
source=("${url}/dist/${pkgname}-${pkgver}.tar.bz2"
        'libxcb-1.1-no-pthread-stubs.patch')
md5sums=('7da11889f14b70632f2e560f76e4432d'
         '75089d79e22c6734473a7d9e55bdd1d7')

build() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/libxcb-1.1-no-pthread-stubs.patch
  autoreconf -vfi
  
  export PYTHON=/usr/bin/python2
  ./configure --prefix=/usr \
              --enable-xinput \
              --enable-xkb \
              --disable-static
  make
}

package () {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
