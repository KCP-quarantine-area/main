
pkgname=libxcb
pkgver=1.11.1
pkgrel=2
epoch=2
pkgdesc="X11 client-side library"
arch=('x86_64')
url="http://xcb.freedesktop.org/"
depends=('xcb-proto>=1.11' 'libxdmcp' 'libxau')
makedepends=('pkgconfig' 'libxslt' 'python3' 'xorg-util-macros')
options=('!libtool')
license=('custom')
source=("http://xcb.freedesktop.org/dist/${pkgname}-${pkgver}.tar.bz2"
        'libxcb-1.1-no-pthread-stubs.patch')
md5sums=('f97a65e6158775de518ac391935634c2'
         '4aafeb65b23933664cfb864f5c6b194d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/libxcb-1.1-no-pthread-stubs.patch"
  autoreconf -vfi
  
  ./configure --prefix=/usr \
              --enable-xinput \
              --enable-xkb \
              --disable-static
  make
}

package () {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
