
pkgname=libxcb
pkgver=1.10
pkgrel=1
epoch=1
pkgdesc="X11 client-side library"
arch=('x86_64')
url="http://xcb.freedesktop.org/"
depends=('xcb-proto>=1.10' 'libxdmcp' 'libxau')
makedepends=('pkgconfig' 'libxslt' 'python3')
options=('!libtool')
license=('custom')
source=("http://xcb.freedesktop.org/dist/${pkgname}-${pkgver}.tar.bz2"
        'libxcb-1.1-no-pthread-stubs.patch')
md5sums=('074c335cc4453467eeb234e3dadda700'
         '9494c95ba613315882b8387c86bdf9aa')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/libxcb-1.1-no-pthread-stubs.patch"
  autoreconf -vfi
  
  ./configure --prefix=/usr \
              --enable-xinput \
              --enable-xkb \
              --disable-static
  make
}

package () {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
