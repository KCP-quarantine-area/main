
pkgname=perl-http-date
pkgver=6.02
pkgrel=1
pkgdesc="date conversion routines"
arch=('x86_64')
url="http://search.cpan.org/dist/HTTP-Date"
license=(PerlArtistic GPL)
options=('!emptydirs')
depends=('perl')
makedepends=()
checkdepends=()
conflicts=('perl-libwww<6')
source=("http://search.cpan.org/CPAN/authors/id/G/GA/GAAS/HTTP-Date-$pkgver.tar.gz")
md5sums=('52b7a0d5982d61be1edb217751d7daba')
_distdir="${srcdir}/HTTP-Date-$pkgver"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    
    make test
  )
}

package() {
    cd "$_distdir"
    
    make DESTDIR="$pkgdir" install
}
