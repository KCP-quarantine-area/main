
pkgname=netcdf
pkgver=4.3.1
pkgrel=1
pkgdesc="Network Common Data Form interface for array-oriented data access and corresponding library."
arch=("x86_64")
url="http://www.unidata.ucar.edu/downloads/netcdf/index.jsp"
license=('custom')
depends=('gcc-libs' 'hdf5' 'gcc-fortran' 'curl')
options=('!libtool' '!makeflags')
source=("http://www.unidata.ucar.edu/downloads/netcdf/ftp/netcdf-${pkgver}.tar.gz")
md5sums=('fa76732417ce415b829eac8bcb8544da')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
        --enable-shared \
        --enable-netcdf-4 
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  install -m755 -d ${pkgdir}/usr/lib
  install -m755 -d ${pkgdir}/usr/share/man
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT
}
