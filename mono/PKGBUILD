
pkgname=mono
pkgver=3.8.0
pkgrel=1
pkgdesc="Free implementation of the .NET platform including runtime and compiler"
arch=('x86_64')
license=('GPL' 'LGPL2' 'MPL' 'custom:MITX11')
url="http://www.mono-project.com/"
depends=('zlib' 'libgdiplus>=2.10' 'sh' 'python3')
makedepends=('pkgconfig')
options=('!libtool' '!makeflags')
source=("http://download.mono-project.com/sources/mono/${pkgname}-${pkgver}.tar.bz2"
        'mono.binfmt.d')
md5sums=('f143bc5077fb817c0bedbe2d5bd49035'
         '568c72e1b42f089a9fec20dfe4bcc3b4')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # build mono
  ./configure --prefix=/usr \
         --sysconfdir=/etc \
         --bindir=/usr/bin \
         --disable-static \
         --disable-quiet-build \
         --disable-system-aot 
  make 

  # build jay
  cd ${srcdir}/${pkgname}-${pkgver}/mcs/jay
  make 
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install 

  # install jay
  cd ${srcdir}/${pkgname}-${pkgver}/mcs/jay
  make DESTDIR=${pkgdir} prefix=/usr INSTALL=../../install-sh install
  cd ${srcdir}/${pkgname}-${pkgver}

  # install binfmt conf file
  install -D -m644 "${srcdir}"/mono.binfmt.d "${pkgdir}"/usr/lib/binfmt.d/mono.conf

  #install license
  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 mcs/MIT.X11 ${pkgdir}/usr/share/licenses/${pkgname}/
  
  #fix .pc file to be able to request mono on what it depends
  sed -i -e "s:#Requires:Requires:" ${pkgdir}/usr/lib/pkgconfig/mono.pc
}