
pkgname=mono
pkgver=4.0.5
_pkgver=4.0.5.1
pkgrel=1
pkgdesc="Free implementation of the .NET platform including runtime and compiler"
arch=('x86_64')
license=('GPL' 'LGPL2' 'MPL' 'custom:MITX11')
url="http://www.mono-project.com/"
depends=('zlib' 'libgdiplus' 'sh' 'python3')
makedepends=('pkgconfig')
options=('!libtool' '!makeflags')
source=("http://download.mono-project.com/sources/mono/${pkgname}-${_pkgver}.tar.bz2"
        'mono.binfmt.d')
md5sums=('2ebdc1859cdc03d408684779503691dc'
         '568c72e1b42f089a9fec20dfe4bcc3b4')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr \
         --sysconfdir=/etc \
         --bindir=/usr/bin \
         --disable-static \
         --disable-quiet-build \
         --disable-system-aot \
         --with-mcs-docs=no
  make 

  cd ${srcdir}/${pkgname}-${pkgver}/mcs/jay
  make 
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install 

  cd ${srcdir}/${pkgname}-${pkgver}/mcs/jay
  make DESTDIR=${pkgdir} prefix=/usr INSTALL=../../install-sh install
  cd ${srcdir}/${pkgname}-${pkgver}

  install -D -m644 "${srcdir}"/mono.binfmt.d "${pkgdir}"/usr/lib/binfmt.d/mono.conf

  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 mcs/MIT.X11 ${pkgdir}/usr/share/licenses/${pkgname}/
  
  sed -i -e "s:#Requires:Requires:" ${pkgdir}/usr/lib/pkgconfig/mono.pc
}