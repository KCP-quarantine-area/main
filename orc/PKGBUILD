
pkgname=orc
pkgver=0.4.24
pkgrel=1
pkgdesc="The Oild Runtime Compiler"
arch=('x86_64')
license=('custom')
url="http://code.entropywave.com/projects/orc/"
depends=('glibc')
makedepends=('valgrind')
options=('!libtool')
source=("http://gstreamer.freedesktop.org/src/orc/${pkgname}-${pkgver}.tar.xz")
md5sums=('9e793ec34c0e20339659dd4bbbf62135')

build () {
    cd "$srcdir/$pkgname-$pkgver"
    
    ./configure --prefix=/usr --disable-static
    make
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    
    make check
}
    
package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
    
    install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
