
pkgname=libx86emu
pkgver=1.11.1.1
_pkgver=1.11-1.1
_shortver=1.11
pkgrel=1
pkgdesc="x86 emulation library"
arch=('x86_64')
url="https://github.com/wfeldt/libx86emu"
license=('BSD')
depends=('glibc')
makedepends=('rpmextract')
options=('!emptydirs')
source=("http://download.opensuse.org/source/tumbleweed/repo/oss/suse/src/${pkgname}-${_pkgver}.src.rpm")
md5sums=('92d9f9259ed5a381a950b8b6fd03b754')

build() {
  cd ${srcdir}
  rpmextract.sh ${pkgname}-${_pkgver}.src.rpm
  tar -xJf ${pkgname}-${_shortver}.tar.xz
  
  cd ${srcdir}/${pkgname}-${_shortver}

  make LIBDIR=/usr/lib
}

package() {
  cd ${srcdir}/${pkgname}-${_shortver}
  make LIBDIR=/usr/lib DESTDIR=${pkgdir} install
  
  install -Dm644 LICENSE \
  ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

