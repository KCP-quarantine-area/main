
pkgname=gst-plugins-base
pkgver=1.0.7
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Base Plugins"
arch=('x86_64')
license=('LGPL')
depends=('gstreamer' 'orc' 'libxv' 'alsa-lib' 'cdparanoia' 'libvisual'
         'libvorbis' 'libtheora' 'pango')
makedepends=('pkgconfig' 'gobject-introspection')
options=(!libtool !emptydirs)
url="http://gstreamer.freedesktop.org/"
source=(${url}/src/$pkgname/$pkgname-${pkgver}.tar.xz)
md5sums=('b5b43cfbf82b413ce2e07a190d87e68f')

build() {
  cd $pkgname-$pkgver
  sed -i -e '/AC_PATH_XTRA/d' configure.ac
  
  aclocal -I m4 -I common/m4
  autoconf
  automake --add-missing
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-experimental \
    --with-package-name="GStreamer (KdeOS)" \
    --with-package-origin="http://www.kde-os.tk/" 
  make
  sed -e 's/^SUBDIRS_EXT =.*/SUBDIRS_EXT =/' -i Makefile
}

check() {
  cd $pkgname-$pkgver
 
  make check
}

package() {
  cd $pkgname-$pkgver
  
  make DESTDIR="${pkgdir}" install
}

