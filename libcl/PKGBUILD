
pkgname=libcl
pkgver=1.1.11
_pkgver=349.16
pkgrel=1
pkgdesc="OpenCL library and ICD loader from NVIDIA"
arch=('x86_64')
url="http://www.nvidia.com/"
depends=('gcc-libs')
license=('custom')
options=('!strip')
_pkg="NVIDIA-Linux-x86_64-${_pkgver}-no-compat32"
source=("ftp://download.nvidia.com/XFree86/Linux-x86_64/${_pkgver}/${_pkg}.run")
md5sums=('3e04208affde8c35a8b96975eec0486b')

build() {
    cd $srcdir
    
    sh ${_pkg}.run --extract-only
}

package() {
    cd $srcdir/${_pkg}
    
    install -D -m755 libOpenCL.so.1.0.0 $pkgdir/usr/lib/libOpenCL.so.1.0.0
    ln -s /usr/lib/libOpenCL.so.1.0.0 $pkgdir/usr/lib/libOpenCL.so.1
    ln -s /usr/lib/libOpenCL.so.1 $pkgdir/usr/lib/libOpenCL.so
    
    install -D -m644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}