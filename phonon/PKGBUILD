
pkgname=phonon
pkgver=4.7.1
pkgrel=3
arch=('x86_64')
url="http://phonon.kde.org"
license=('LGPL')
pkgdesc="The multimedia framework for KDE4"
depends=('phonon-backend' 'pulseaudio' 'libqzeitgeist')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('0935e1c00440a03e3032860030398665')

build() {
  cd "${srcdir}"
  mkdir build
  
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DPHONON_INSTALL_QT_EXTENSIONS_INTO_SYSTEM_QT=ON 
  make
}

package(){
  cd "${srcdir}"/build
  
  make DESTDIR="${pkgdir}" install
}