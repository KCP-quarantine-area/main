
pkgname=aspell-pt
_pkgverPT=20160202
_pkgverBR=20090702
pkgver=$_pkgverPT
pkgrel=1
pkgdesc="Portuguese and Brazilian Portuguese dictionary for aspell"
arch=('x86_64')
url="http://aspell.net/"
license=('LGPL' 'GPL')
depends=('aspell')
source=("ftp://ftp.gnu.org/gnu/aspell/dict/pt_BR/aspell6-pt_BR-$_pkgverBR-0.tar.bz2"
       "http://natura.di.uminho.pt/download/sources/Dictionaries/aspell6/aspell6.pt-$_pkgverPT.tar.bz2"
       "http://natura.di.uminho.pt/download/sources/Dictionaries/aspell6/aspell6.pt-preao-$_pkgverPT.tar.bz2")
md5sums=('e082a8956882eb94a67c12e1b8c4a324'
         'aa3d5e80ad615d3ed41d2270e685a87d'
         '2aaeaa495cea37cf3ff0c1a7603c91e4')

build() {
  cd aspell6-pt_BR-$_pkgverBR-0
  ./configure
  make

  cd ../aspell6-pt_PT-$_pkgverPT-0
  ./configure
  make

  cd ../aspell6-pt_PT-preao-$_pkgverPT-0
  ./configure
  make
}

package() {
  cd aspell6-pt_BR-$_pkgverBR-0
  make DESTDIR="$pkgdir" install

  cd ../aspell6-pt_PT-$_pkgverPT-0
  make DESTDIR="$pkgdir" install

  cd ../aspell6-pt_PT-preao-$_pkgverPT-0
  make DESTDIR="$pkgdir" install

  echo "add pt_BR.multi" > "$pkgdir"/usr/lib/aspell-0.60/brasileiro.alias
}
