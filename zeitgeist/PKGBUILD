
pkgname=zeitgeist
pkgver=0.9.5
pkgrel=3
pkgdesc="A service which logs the users's activities and events"
arch=('x86_64')
url="http://zeitgeist-project.com/"
license=('GPL')
depends=('dbus-python' 'python2-gobject' 'pyxdg' 'xapian-core')
makedepends=('intltool' 'raptor' 'python2-rdflib' 'vala')
source=("http://launchpad.net/$pkgname/0.9/$pkgver/+download/$pkgname-$pkgver.tar.bz2")
md5sums=('5841e4624b07ba74866fb177f1c9aae7')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  PYTHON=/usr/bin/python2 LIBS+="-lgmodule-2.0" ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/zeitgeist \
    --localstatedir=/var \
    --disable-telepathy 
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" zeitgeistdocdir=/usr/share/doc/zeitgeist install
  
  install -D -m644 data/zeitgeist-daemon.bash_completion "$pkgdir/usr/share/bash-completion/completions/zeitgeist-daemon"
}
