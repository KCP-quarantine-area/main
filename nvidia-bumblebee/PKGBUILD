

_extramodules=extramodules-3.13
_kver=$(cat /lib/modules/${_extramodules}/version)

pkgname=nvidia-bumblebee
pkgver=337.25
pkgrel=1
pkgdesc="NVIDIA drivers for linux. Packaged for Bumblebee"
arch=('x86_64')
url="http://www.nvidia.com/"
depends=('linux>=3.13' 'linux<3.14' "nvidia-utils-bumblebee")
makedepends=('linux-headers>3.13' 'linux-headers<3.14')
provides=("nvidia=${pkgver}")
conflicts=('nvidia')
license=('custom')
install=nvidia.install
options=('!strip')

_pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"
#_pkg="NVIDIA-Linux-x86_64-${pkgver}"
source=("ftp://download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/NVIDIA-Linux-x86_64-${pkgver}-no-compat32.run")
#source=("http://us.download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/NVIDIA-Linux-x86_64-${pkgver}.run")
md5sums=('66e19d6d621c9e70fbd5374dd8b44341')

build() {
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only

    cd "${_pkg}/kernel"
    
    make SYSSRC=/lib/modules/"${_kver}/build" module
}
 
package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/lib/modprobe.d/nouveau_blacklist.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}
