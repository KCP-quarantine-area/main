
pkgname=qtwebkit
pkgver=2.3.1
_pkgver=23
pkgrel=1
_qtver=4.8.4
arch=('x86_64')
url='http://trac.webkit.org/wiki/QtWebKit'
pkgdesc='An open source web browser engine (Qt port)'
license=('LGPL2.1' 'GPL3')
depends=('qt' 'gstreamer0.10' 'gstreamer0.10-base' 'gstreamer0.10-base-plugins'
         'gstreamer0.10-good-plugins' 'gstreamer0.10-bad-plugins' 'gstreamer0.10-ffmpeg'
         'gstreamer0.10-ugly-plugins' 'systemd')
makedepends=('gperf' 'sqlite3' 'fontconfig' 'perl' 'python2' 'mesa' 'ruby' 'git')
source=("${pkgname}-${pkgver}.tar.gz"::"https://gitorious.org/webkit/qtwebkit-23/archive-tarball/qtwebkit-${pkgver}"
        "http://releases.qt-project.org/qt4/source/qt-everywhere-opensource-src-${_qtver}.tar.gz"
        'qwebview.patch')
md5sums=('f54480ee6b6cc29d8a725f5933911a33'
         '89c5ecba180cae74c66260ac732dc5cb'
         '62c452d13c919bebc1d056a1a30f28cf')

build() {
  cd "${srcdir}"/webkit-qtwebkit-${_pkgver}

  export QTDIR=/usr
  Tools/Scripts/build-webkit --qt \
    --makeargs="${MAKEFLAGS}" --prefix=/usr --no-webkit2 

  cd ../

  # QWebView plugin 
  cd qt-everywhere-opensource-src-${_qtver}
  patch -p1 -i "${srcdir}"/qwebview.patch
  cd tools/designer/src/plugins/qwebview
  qmake
  make
}

package() {
  cd "${srcdir}"/webkit-qtwebkit-${_pkgver}
  make INSTALL_ROOT="${pkgdir}" -C WebKitBuild/Release install

  cd ../

  cd qt-everywhere-opensource-src-${_qtver}/tools/designer/src/plugins/qwebview
  make INSTALL_ROOT="${pkgdir}" install
}
