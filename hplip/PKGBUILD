
pkgname=hplip
pkgver=3.16.3
# incase a or b is used in hplip rls:
_pkgver=3.16.3
pkgrel=1
pkgdesc="Drivers for HP DeskJet, OfficeJet, Photosmart, Business Inkjet and some LaserJet."
arch=('x86_64')
url="http://hplipopensource.com"
license=('GPL')
depends=('python2' 'ghostscript' 'foomatic-db' 'foomatic-db-engine' 'cups'
         'net-snmp' 'libusb' 'wget' 'python2-gobject')
makedepends=('pkgconfig' 'sane' 'rpcbind' 'dbus-python2' 'pyqt-python2')
optdepends=('dbus-python2: for dbus support'
            'sane: for scanner support'
            'rpcbind: for network support'
            'pyqt-python2: for the GUI')
options=('!libtool')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/hplip/${pkgname}-${_pkgver}.tar.gz")
md5sums=('43379bb4790fa7252254094c294f8930')

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"

  # from Gentoo
  # Use system foomatic-rip for hpijs driver instead of foomatic-rip-hplip
  # move to cups-filters version asap http://hplipopensource.com/hplip-web/install/manual/distros/lfs.html
  local i
  for i in ppd/hpijs/*.ppd.gz ; do
    rm -f ${i}.temp
    gunzip -c ${i} | sed 's/foomatic-rip-hplip/foomatic-rip/g' | \
    gzip > ${i}.temp || return 1
    mv ${i}.temp ${i}
  done

  export AUTOMAKE='automake --foreign'
  autoreconf --force --install

	./configure --prefix=/usr \
             --enable-qt4 \
             --disable-foomatic-rip-hplip-install \
             --enable-foomatic-ppd-install \
             --enable-hpcups-install \
             --enable-new-hpcups \
             --enable-cups-ppd-install \
             --enable-cups-drv-install \
             --enable-hpijs-install \
             --enable-foomatic-drv-install \
             --enable-pp-build 
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${_pkgver}"

  make rulesdir=/usr/lib/udev/rules.d DESTDIR="${pkgdir}/" install

  # remove sane conflicts and obsolete files
  rm -rf "${pkgdir}"/etc/{sane.d,xdg}
  rm -vrf "${pkgdir}"/usr/share/hal
  rm -vrf "$pkgdir"/etc/init.d
}
