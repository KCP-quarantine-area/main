
pkgname=gtk2
pkgver=2.24.30
_pkgver=2.24
pkgrel=1
arch=('x86_64')
url="http://www.gtk.org/"
pkgdesc="GTK+ is a multi-platform toolkit (v2)"
license=('LGPL')
depends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage'
         'shared-mime-info' 'cairo' 'libcups' 'gtk-update-icon-cache')
makedepends=('gobject-introspection')
options=('!libtool' '!docs')
install=gtk2.install
backup=(etc/gtk-2.0/gtkrc)
source=("http://ftp.gnome.org/pub/gnome/sources/gtk+/${_pkgver}/gtk+-$pkgver.tar.xz"
        'xid-collision-debug.patch')
md5sums=('04568ba5c58b75e3c7543e45628ad789'
         '22355df723f6ca358e30e6b8f0baace5')

build() {
    cd "$srcdir/gtk+-$pkgver"
    patch -p1 -i "$srcdir/xid-collision-debug.patch"

    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-xinput=yes

    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "$srcdir/gtk+-$pkgver"

    make DESTDIR="$pkgdir" install
    sed -i "s#env python#env python2#" $pkgdir/usr/bin/gtk-builder-convert
    echo 'gtk-fallback-icon-theme = "gnome"' > "$pkgdir/etc/gtk-2.0/gtkrc"
    
    rm $pkgdir/usr/bin/gtk-update-icon-cache
}

