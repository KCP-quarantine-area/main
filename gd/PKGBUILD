
pkgname=gd
pkgver=2.1.1
_pkgver=2.1.1
pkgrel=4
pkgdesc="Library for the dynamic creation of images by programmers"
arch=('x86_64')
url="http://www.libgd.org/"
license=('custom')
depends=('libpng' 'fontconfig' 'libjpeg-turbo' 'libtiff')
#makedepends=('cmake')
options=('!libtool')
source=("https://github.com/libgd/libgd/archive/gd-${_pkgver}.tar.gz"
        'gdxpm.patch'
        "https://github.com/libgd/libgd/commit/2bb97f407c1145c850416a3bfbcc8cf124e68a19.diff")
md5sums=('e91a1a99903e460e7ba00a794e72cc1e'
         '51f5ff073eabbd7809e0ce438d5bb1c2'
         'a46b31d83c2653ca2dc7da29cf008832')

build() {
  cd libgd-gd-${_pkgver}
  # https://cve.mitre.org/cgi-bin/cvename.cgi?name=CVE-2014-2497
  patch -p1 -i ${srcdir}/gdxpm.patch
  # CVE-2016-3074 http://seclists.org/oss-sec/2016/q2/128 
  patch -p1 -i ${srcdir}/2bb97f407c1145c850416a3bfbcc8cf124e68a19.diff

  ./bootstrap.sh
  ./configure --prefix=/usr --without-xpm
  make
}

check() {
  cd libgd-gd-${_pkgver}

  make check
 }

package() {
  cd libgd-gd-${_pkgver}
  make DESTDIR=${pkgdir} install
  
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
