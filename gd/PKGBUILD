
pkgname=gd
pkgver=2.1.1
_pkgver=2.1.1
pkgrel=3
pkgdesc="Library for the dynamic creation of images by programmers"
arch=('x86_64')
url="http://www.libgd.org/"
license=('custom')
depends=('libpng' 'fontconfig' 'libjpeg-turbo' 'libtiff')
#makedepends=('cmake')
options=('!libtool')
source=("https://github.com/libgd/libgd/archive/gd-$_pkgver.tar.gz"
        'gdxpm.patch')
md5sums=('e91a1a99903e460e7ba00a794e72cc1e'
         '51f5ff073eabbd7809e0ce438d5bb1c2')

build() {
  cd "${srcdir}/libgd-gd-${_pkgver}"
  # https://cve.mitre.org/cgi-bin/cvename.cgi?name=CVE-2014-2497
  patch -p1 -i ${srcdir}/gdxpm.patch

  ./bootstrap.sh
  ./configure --prefix=/usr --without-xpm
  make
}

check() {
  cd "${srcdir}/libgd-gd-${_pkgver}"

  make check
 }

package() {
  cd "${srcdir}/libgd-gd-${_pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}