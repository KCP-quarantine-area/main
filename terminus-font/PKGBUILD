
pkgname=terminus-font
pkgver=4.38
pkgrel=1
arch=('x86_64')
url="http://sourceforge.net/projects/terminus-font/"
pkgdesc="A superb, monospace bitmap font (for X11 and console)"
license=('GPL2' 'custom:OFL')
depends=('xorg-fonts-encodings' 'xorg-fonts-alias' 'xorg-font-utils' 'fontconfig')
install=terminus-font.install
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('a8e792fe6e84c86ed2b6ed3e2a12ba66')

build() { 
  cd "${srcdir}/$pkgname-$pkgver"
  
  sh ./configure --prefix=/usr \
        --x11dir=/usr/share/fonts/local/ \
        --psfdir=/usr/share/kbd/consolefonts/ 
  make 
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install

  install -Dm644 "$srcdir/$pkgname-$pkgver/75-yes-terminus.conf" \
                 "$pkgdir/etc/fonts/conf.avail/75-yes-terminus.conf"
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/OFL.TXT" \
                 "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
