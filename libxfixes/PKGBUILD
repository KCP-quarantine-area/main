
pkgname=libxfixes
pkgver=5.0
pkgrel=2
pkgdesc="X11 miscellaneous 'fixes' extension library"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'fixesproto>=5.0')
makedepends=('xorg-util-macros')
options=('!libtool')
source=("${url}/releases/individual/lib/libXfixes-${pkgver}.tar.bz2")
md5sums=('678071bd7f9f7467e2fc712d81022318')

build() {
  cd "${srcdir}/libXfixes-${pkgver}"
  
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/libXfixes-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
