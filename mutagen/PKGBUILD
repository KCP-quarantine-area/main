
pkgname=mutagen
pkgver=1.29
pkgrel=1
pkgdesc="An audio metadata tag reader and writer implemented in pure python"
arch=('x86_64')
url="https://mutagen.readthedocs.org/en/latest/"
license=('GPL2')
depends=('python2')
# wget https://bitbucket.org/lazka/mutagen/downloads/mutagen-1.27.tar.gz
source=("https://bitbucket.org/lazka/mutagen/downloads/${pkgname}-${pkgver}.tar.gz")
md5sums=('579120529926a97ce6020ef5d20bd6fb')

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  python2 setup.py test
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}"
  
  install -d "${pkgdir}/usr/bin"
  install -m755 tools/* "${pkgdir}/usr/bin"
  sed -i 's|^#!.*/usr/bin/env.*python|#!/usr/bin/env python2|' "${pkgdir}"/usr/bin/* \
    "${pkgdir}/usr/lib/python2.7/site-packages/mutagen/__init__.py"
}