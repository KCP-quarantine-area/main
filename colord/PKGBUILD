
pkgname=colord
pkgver=0.1.32
pkgrel=1
pkgdesc="System service easily manage, install and generate color profiles to manage input and output devices"
arch=('x86_64')
url="http://www.freedesktop.org/software/colord"
license=('GPL2')
depends=('lcms2' 'polkit' 'shared-color-profiles' 'sqlite3' 'systemd' 'libgusb')
makedepends=('intltool' 'gobject-introspection' 'vala' 'docbook-xsl' 'sane')
opdepends=('sane')
source=($url/releases/$pkgname-$pkgver.tar.xz)
options=('!libtool')
md5sums=('fecd86425982d48b1545fe30e38a381d')

build() {
  cd "$pkgname-$pkgver"
  
  # put udev files in /usr/lib
  sed -i "/slashlibdir=/s#/lib#/usr/lib#" configure
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc --libexecdir=/usr/lib/colord \
    --localstatedir=/var --disable-static \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --enable-vala --enable-sane
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install \
    bashcompletiondir=/usr/share/bash-completion/completions
}

