
pkgname=libetonyek
pkgver=0.1.5
pkgrel=1
pkgdesc="Library and a set of tools for reading and converting Apple Keynote presentations."
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/libetonyek/"
license=('MPL2')
options=('!libtool')
depends=('libwpd' 'libxml2' 'gperf' 'zlib' 'librevenge')
makedepends=('boost' 'glm' 'cppunit' 'mdds')
source=("http://dev-www.libreoffice.org/src/libetonyek/${pkgname}-${pkgver}.tar.xz")
md5sums=('b37e09b5e3f0b5b05af2b549f7fde8a1')

build() {
  cd "${pkgname}-${pkgver}"

  ./autogen.sh
  ./configure \
      --prefix=/usr \
      --disable-werror \
      --without-docs \
      --enable-static=no 
  make
}

check() {
  cd "$pkgname-$pkgver"
  
  make check
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="$pkgdir" install
}