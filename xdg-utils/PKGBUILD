
pkgname=xdg-utils
pkgver=1.1.0.1
_gitver=050333e34e2fe7430ad2ead37e5a0420ac0c2a3e 
pkgrel=2
pkgdesc='Command line tools that assist applications with a variety of desktop integration tasks.'
arch=('x86_64')
url='http://portland.freedesktop.org/'
depends=('sh')
makedepends=('docbook-xsl' 'links' 'xmlto')
license=('MIT')
source=("http://cgit.freedesktop.org/xdg/xdg-utils/snapshot/xdg-utils-${_gitver}.tar.gz")
md5sums=('546dd99213959ca0ce6c75a2dab7c53b')

build() {
	cd ${srcdir}/${pkgname}-${_gitver}
	
	./configure --prefix=/usr --mandir=/usr/share/man
	make scripts-clean -C scripts 
	make man scripts -C scripts 
}

package() {
	cd ${srcdir}/${pkgname}-${_gitver}
	
	make DESTDIR="${pkgdir}" install
	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

