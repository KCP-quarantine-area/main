
pkgbase=accountsqt
pkgname=('accountsqt' 'accountsqt-doc')
pkgver=1.13
pkgrel=1
arch=('x86_64')
url="http://code.google.com/p/accounts-sso/"
license=('LGPL')
makedepends=('qt' 'libaccounts-glib' 'doxygen')
# check source https://groups.google.com/forum/#!forum/accounts-sso-announce
# latest tars https://drive.google.com/#folders/0B8fX9XOwH_g4alFsYV8tZTI4VjQ
source=("http://sourceforge.net/projects/kaosx/files/sources/accountsqt/accounts-qt-$pkgver.tar.bz2")
md5sums=('fac0f97d8eb2501857b77cad7325e6dd')

build() {
  cd accounts-qt-$pkgver
  sed -i 's|SUBDIRS  += Accounts tests|SUBDIRS += Accounts|' accounts-qt.pro
  
  qmake PREFIX=/usr LIBDIR=/usr/lib
  make
}

package_accountsqt() {
  pkgdesc="Qt-based client library for accessing the online accounts database"
  depends=('qt' 'libaccounts-glib')

  cd accounts-qt-$pkgver
  make INSTALL_ROOT="$pkgdir" install_subtargets
}

package_accountsqt-doc() {
  pkgdesc="Developer documention of accounts-qt"
  
  cd accounts-qt-$pkgver
  
  make INSTALL_ROOT="$pkgdir" install_documentation
}
