
pkgname=glib-networking
pkgver=2.48.2
_pkgver=2.48
pkgrel=1
pkgdesc="Network-related giomodules for glib"
arch=('x86_64')
url="http://www.gtk.org/"
license=('GPL2')
depends=('glib2' 'libproxy' 'gnutls' 'ca-certificates')
makedepends=('intltool')
source=("http://download.gnome.org/sources/${pkgname}/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('d7cf81d52c856b0c66f7821021f40e08')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${pkgname} \
        --disable-static \
        --disable-installed-tests
  make
}

check() {
  cd ${pkgname}-${pkgver}
  
  make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install testfiles_DATA=
}

