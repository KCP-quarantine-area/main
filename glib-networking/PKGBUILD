
pkgname=glib-networking
pkgver=2.46.1
_pkgver=2.46
pkgrel=1
pkgdesc="Network-related giomodules for glib"
arch=('x86_64')
url="http://www.gtk.org/"
license=('GPL2')
depends=('glib2' 'libproxy' 'gnutls' 'ca-certificates')
makedepends=('intltool')
install=glib-networking.install
source=("http://download.gnome.org/sources/$pkgname/${_pkgver}/$pkgname-$pkgver.tar.xz")
md5sums=('c000e0b579f5d8fd48efebc7ac4d95dc')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/$pkgname \
        --disable-static \
        --disable-installed-tests
  make
}

check() {
  cd $pkgname-$pkgver
  
  make -k check
}

package() {
  cd $pkgname-$pkgver
  
  make DESTDIR="$pkgdir" install testfiles_DATA=
}

