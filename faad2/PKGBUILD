
pkgname=faad2
pkgver=2.7
pkgrel=4
pkgdesc="ISO AAC audio decoder"
arch=('x86_64')
url="http://www.audiocoding.com/"
license=('custom:GPL')
depends=('glibc')
options=('!libtool' '!makeflags')
source=("http://downloads.sourceforge.net/sourceforge/faac/${pkgname}-${pkgver}.tar.bz2"
        'LICENSE')
md5sums=('4c332fa23febc0e4648064685a3d4332'
         'ec95f38142e14fb46c849503897a37a0')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  ./configure --prefix=/usr
  make CFLAGS="${CFLAGS} -fPIC"
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} manmdir=/usr/share/man/man1 install
  
  install -m644 common/mp4ff/mp4ff_int_types.h ${pkgdir}/usr/include/mp4ff_int_types.h
  install -D -m644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  mv ${pkgdir}/usr/share/man/man1/faad.man ${pkgdir}/usr/share/man/man1/faad.1
}
