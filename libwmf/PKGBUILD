
pkgname=libwmf
pkgver=0.2.8.4
pkgrel=15
pkgdesc="A library for reading vector images in Microsoft's native Windows Metafile Format (WMF)."
arch=('x86_64')
url="http://wvware.sourceforge.net/libwmf.html"
license=("LGPL")
depends=('libpng' 'libx11' 'freetype2' 'libjpeg-turbo' 'gsfonts' 'expat>=2.0')
makedepends=('pkgconfig')
options=('!libtool')
source=("http://downloads.sourceforge.net/sourceforge/wvware/${pkgname}-${pkgver}.tar.gz"
	'libwmf-0.2.8.4-libpng-1.5.patch')
md5sums=('d1177739bf1ceb07f57421f0cee191e0'
         '21bab2d951aef8e72805eacee950cb09')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}/libwmf-0.2.8.4-libpng-1.5.patch" 
  
  sed -i -e 's/src include fonts doc/src include fonts/g' Makefile.in
  ./configure --prefix=/usr \
              --with-gsfontdir=/usr/share/fonts/Type1 \
	      --with-fontdir=/usr/share/fonts/Type1 \
	      --with-gsfontmap=/usr/share/ghostscript/9.14/Resources/Init/Fontmap.GS \
              --disable-gtk2
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make DESTDIR=${pkgdir} install
  #Part of gsfonts
  rm -rf ${pkgdir}/usr/share/fonts

  rm -f ${pkgdir}/usr/lib/gtk-2.0/*/loaders/*.a
}
