
pkgbase=gtk3
pkgname=('gtk3' 'gtk-update-icon-cache')
pkgver=3.18.5
_pkgver=3.18
pkgrel=1
arch=('x86_64')
url="http://www.gtk.org/"
makedepends=('gobject-introspection' 'cairo' 'libcups' 'libxcursor' 'libxinerama' 'libxrandr' 
             'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info' 'gdk-pixbuf2' 
             'colord' 'at-spi2-atk' 'libepoxy' 'libxkbcommon' 'librsvg' 'wayland')
options=('!libtool' '!docs')
license=('LGPL')
source=("http://ftp.gnome.org/pub/gnome/sources/gtk+/${_pkgver}/gtk+-${pkgver}.tar.xz")
md5sums=('6cf1478025accbee2cd31a0b093d7efa')

build() {
  cd "${srcdir}/gtk+-${pkgver}"

 ./configure --prefix=/usr \
             --sysconfdir=/etc \
             --localstatedir=/var \
             --disable-schemas-compile \
             --enable-x11-backend \
             --enable-wayland-backend

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package_gtk3() {
  pkgdesc="The GTK+ Toolkit (v3)"
  depends=('cairo' 'libcups' 'libxcursor' 'libxinerama' 'libxrandr' 
          'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info' 
          'colord' 'at-spi2-atk' 'libepoxy' 'libxkbcommon' 'wayland')
  install="gtk3.install"
  
  cd "${srcdir}/gtk+-${pkgver}"

  make DESTDIR="${pkgdir}" install
  
  # avoid gtk2 conflict, used for both gtk2 & 3 this way
  rm "$pkgdir/usr/bin/gtk-update-icon-cache"
}

package_gtk-update-icon-cache() {
    pkgdesc="The GTK+ update icon cache tool"
    depends=('gdk-pixbuf2')
    install="gtk-update-icon-cache.install"

    cd "$srcdir/gtk+-$pkgver/gtk"

    install -D -m755 gtk-update-icon-cache $pkgdir/usr/bin/gtk-update-icon-cache
}