
pkgname=p7zip
pkgver=9.38.1
pkgrel=1
pkgdesc="A command-line port of the 7zip compression utility"
arch=('x86_64')
license=('GPL')
url="http://p7zip.sourceforge.net"
depends=('gcc-libs' 'bash')
makedepends=('yasm' 'nasm')
options=('!makeflags')
source=("http://downloads.sourceforge.net/project/p7zip/p7zip/${pkgver}/${pkgname}_${pkgver}_src_all.tar.bz2")
md5sums=('6cba8402ccab2370d3b70c5e28b3d651')

build() {
	cd "${srcdir}/${pkgname}_${pkgver}"
        cp makefile.linux_amd64_asm makefile.machine

	make all3 OPTFLAGS="${CXXFLAGS}"
}

package() {
	cd "${srcdir}/${pkgname}_${pkgver}"

	make install \
		DEST_DIR="${pkgdir}" \
		DEST_HOME="/usr" \
		DEST_MAN="/usr/share/man"

	install -d "${pkgdir}"/usr/share/licenses/p7zip
	ln -s -t "${pkgdir}"/usr/share/licenses/p7zip \
		/usr/share/doc/p7zip/DOCS/License.txt \
		/usr/share/doc/p7zip/DOCS/unRarLicense.txt
		
        install -Dm644 GUI/p7zip_32.png "${pkgdir}"/usr/share/icons/hicolor/32x32/apps/p7zip.png
	install -d "${pkgdir}"/usr/share/kservices5/ServiceMenus
	cp GUI/kde4/* "${pkgdir}"/usr/share/kservices5/ServiceMenus/
	ln -s 7zCon.sfx "${pkgdir}"/usr/lib/p7zip/7z.sfx
	
	chmod -R u+w,o+r "${pkgdir}/usr"
}
