
pkgname=libdmx
pkgver=1.1.3
pkgrel=3
pkgdesc="X11 Distributed Multihead extension library"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('dmxproto' 'libxext')
makedepends=('xorg-util-macros')
options=('!libtool')
source=("http://xorg.freedesktop.org/releases/individual/lib/${pkgname}-${pkgver}.tar.bz2")
md5sums=('ba983eba5a9f05d152a0725b8e863151')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
