
pkgbase=signon
pkgname=('signonqt' 'signonqt-doc')
pkgver=8.56
pkgrel=1
arch=('x86_64')
url="http://code.google.com/p/accounts-sso/"
license=('LGPL')
makedepends=('qt' 'libproxy' 'doxygen' 'ttf-dejavu')
options=('!emptydirs')
source=("http://accounts-sso.googlecode.com/files/signon-$pkgver.tar.bz2")
md5sums=('85ac10ab581d84ec2344a42349bc693b')

build() {
  cd $pkgbase-$pkgver
  
  qmake PREFIX=/usr LIBDIR=/usr/lib
  make
}

package_signonqt() {
  pkgdesc="Framework to provide credential storage and authentication service"
  depends=('qt' 'libproxy')
  backup=('etc/signond.conf')

  cd $pkgbase-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  mv "$pkgdir/usr/share/doc" "$srcdir"
}

package_signonqt-doc() {
  pkgdesc="Developer documention of signon"

  mkdir -p "$pkgdir/usr/share"
  mv "$srcdir/doc" "$pkgdir/usr/share"
}
