
pkgbase=signon
pkgname=('signonqt' 'signonqt-doc')
pkgver=8.57
pkgrel=1
arch=('x86_64')
url="http://code.google.com/p/accounts-sso/"
license=('LGPL')
makedepends=('qt' 'libproxy' 'doxygen' 'ttf-dejavu')
options=('!emptydirs')
# check source https://groups.google.com/forum/#!forum/accounts-sso-announce
# latest tars https://drive.google.com/#folders/0B8fX9XOwH_g4alFsYV8tZTI4VjQ
source=("http://sourceforge.net/projects/kaosx/files/sources/signon/signon-$pkgver.tar.bz2")
md5sums=('9afb31332df8afffd8923f0079967d31')

build() {
  cd $pkgbase-$pkgver
  
  qmake PREFIX=/usr LIBDIR=/usr/lib
  make
}

package_signonqt() {
  pkgdesc="Framework to provide credential storage and authentication service"
  depends=('qt' 'libproxy')
  backup=('etc/signond.conf')

  cd $pkgbase-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  mv "$pkgdir/usr/share/doc" "$srcdir"
}

package_signonqt-doc() {
  pkgdesc="Developer documention of signon"

  mkdir -p "$pkgdir/usr/share"
  mv "$srcdir/doc" "$pkgdir/usr/share"
}
