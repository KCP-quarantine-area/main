
pkgname=php
pkgver=5.6.10
pkgrel=1
arch=('x86_64')
pkgdesc='An HTML-embedded scripting language'
license=('PHP')
url='http://www.php.net'
depends=('postgresql-libs' 'libldap' 'sqlite' 'unixodbc' 'net-snmp' 'enchant' 
         'freetds' 'libmcrypt' 'tidyhtml' 'aspell' 'libtool' 'libpng' 'icu' 'curl' 'libxslt' 'bzip2'
         'freetype2' 'systemd' 'pcre' 'libxml2' 'libxpm' 'libzip') #'libvpx'
makedepends=('imap' 'mariadb' 'postfix' 'file' 'libjpeg-turbo' 'openssl' 'db' 'gmp')
backup=('etc/php/php.ini'
        'etc/php/php-fpm.conf'
        'etc/php/pear.conf')
install='php.install'
conflicts=('php-cgi'
         'php-fpm'
         'php-embed'
         'php-pear'
         'php-enchant'
         'php-gd'
         'php-intl'
         'php-ldap'
         'php-mcrypt'
         'php-mssql'
         'php-odbc'
         'php-pgsql'
         'php-pspell'
         'php-snmp'
         'php-sqlite'
         'php-tidy'
         'php-xsl')
replaces=('php-cgi'
         'php-fpm'
         'php-embed'
         'php-pear'
         'php-enchant'
         'php-gd'
         'php-intl'
         'php-ldap'
         'php-mcrypt'
         'php-mssql'
         'php-odbc'
         'php-pgsql'
         'php-pspell'
         'php-snmp'
         'php-sqlite'
         'php-tidy'
         'php-xsl')
source=("http://www.php.net/distributions/${pkgname}-${pkgver}.tar.bz2"
        'php.ini.patch'
        'apache.conf'
        'php-fpm.conf.in.patch'
        'logrotate.d.php-fpm'
        'php-fpm.service'
        'php-fpm.tmpfiles')
md5sums=('83b17e0cdc72a3a486f5e6342e76c45e'
         '942f9d8af9bcab2487ca710ef42ef1de'
         'dec2cbaad64e3abf4f0ec70e1de4e8e9'
         '16b5e2e4da59f15bea4c2db78a7bc8dc'
         '25bc67ad828e8147a817410b68d8016c'
         '8fb5a9bc4b44617ba1b29b60c43f62a0'
         'c60343df74f8e1afb13b084d5c0e47ed')

prepare() {
	cd ${srcdir}/${pkgname}-${pkgver}

	patch -p0 -i ${srcdir}/php.ini.patch
	patch -p0 -i ${srcdir}/php-fpm.conf.in.patch
}

build() {
        EXTENSION_DIR=/usr/lib/php/modules
	export EXTENSION_DIR
	PEAR_INSTALLDIR=/usr/share/pear
	export PEAR_INSTALLDIR

	cd ${srcdir}/${pkgname}-${pkgver}
	
	./configure --config-cache \
		--prefix=/usr \
		--sysconfdir=/etc/php \
		--localstatedir=/var \
		--with-layout=GNU \
		--with-config-file-path=/etc/php \
		--with-config-file-scan-dir=/etc/php/conf.d \
		--disable-rpath \
		--mandir=/usr/share/man \
		--with-pear \
		--enable-bcmath=shared \
		--enable-calendar=shared \
		--enable-dba=shared \
		--enable-exif=shared \
		--enable-ftp=shared \
		--enable-gd-native-ttf \
		--enable-intl=shared \
		--enable-mbstring \
		--enable-phar=shared \
		--enable-posix=shared \
		--enable-shmop=shared \
		--enable-soap=shared \
		--enable-sockets=shared \
		--enable-sysvmsg=shared \
		--enable-sysvsem=shared \
		--enable-sysvshm=shared \
		--enable-zip=shared \
		--with-bz2=shared \
		--with-curl=shared \
		--with-db4=/usr \
		--with-enchant=shared,/usr \
		--with-fpm-systemd \
		--with-freetype-dir=/usr \
		--with-xpm-dir=/usr \
		--with-gd=shared \
		--with-gdbm \
		--with-gettext=shared \
		--with-gmp=shared \
		--with-iconv=shared \
		--with-icu-dir=/usr \
		--with-imap-ssl \
		--with-imap=shared \
		--with-jpeg-dir=/usr \
		--with-ldap=shared \
		--with-ldap-sasl \
		--with-libzip \
		--with-mcrypt=shared \
		--with-mhash \
		--with-mssql=shared \
		--with-mysql-sock=/run/mysqld/mysqld.sock \
		--with-mysql=shared,mysqlnd \
		--with-mysqli=shared,mysqlnd \
		--with-openssl=shared \
		--with-pcre-regex=/usr \
		--with-pdo-mysql=shared,mysqlnd \
		--with-pdo-odbc=shared,unixODBC,/usr \
		--with-pdo-pgsql=shared \
		--with-pdo-sqlite=shared,/usr \
		--with-pgsql=shared \
		--with-png-dir=/usr \
		--with-pspell=shared \
		--with-snmp=shared \
		--with-sqlite3=shared,/usr \
		--with-tidy=shared \
		--with-unixODBC=shared,/usr \
		--with-xmlrpc=shared \
		--with-xsl=shared \
		--with-zlib \
		--enable-cli \
		--enable-cgi \
		--enable-fpm \
		--with-readline \
		--enable-pcntl \
		--with-fpm-user=http \
		--with-fpm-group=http \
		--enable-embed=shared \
		--with-pear

	make
}
	
package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make -j1 INSTALL_ROOT=${pkgdir} install
	install -d -m755 ${pkgdir}/usr/share/pear
	# install php.ini
	install -D -m644 ${srcdir}/${pkgbase}-${pkgver}/php.ini-production ${pkgdir}/etc/php/php.ini
	install -d -m755 ${pkgdir}/etc/php/conf.d/
	
	#install -D -m644 ${srcdir}/apache.conf ${pkgdir}/etc/httpd/conf/extra/php5_module.conf
	
	install -D -m644 ${srcdir}/logrotate.d.php-fpm ${pkgdir}/etc/logrotate.d/php-fpm
	install -d -m755 ${pkgdir}/etc/php/fpm.d
	install -D -m644 ${srcdir}/php-fpm.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/php-fpm.conf
	install -D -m644 ${srcdir}/php-fpm.service ${pkgdir}/usr/lib/systemd/system/php-fpm.service

	rm -f ${pkgdir}/usr/lib/php/modules/*.a
	rm -r ${pkgdir}/var/run
}
