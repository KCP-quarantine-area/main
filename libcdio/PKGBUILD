
pkgname=libcdio
pkgver=0.92
pkgrel=1
pkgdesc="GNU Compact Disc Input and Control Library"
arch=('x86_64')
license=('GPL3')
url="http://www.gnu.org/software/libcdio/"
depends=('gcc-libs' 'libcddb' 'ncurses')
options=('!libtool')
install=libcdio.install
source=("http://ftp.gnu.org/gnu/libcdio/${pkgname}-${pkgver}.tar.gz"
        'libcdio-0.83-linking.patch')
md5sums=('86f3f2869c1d34e4c6e52db77992b918'
         '2826f89c78f09b0c0cbaeb47d507a55e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  patch -p1 -i "${srcdir}/libcdio-0.83-linking.patch"
  
  ./configure --prefix=/usr \
              --disable-vcd-info \
              --enable-cpp-progs \
              --disable-static
              
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
  
  sed -i "/LIBCDIO_SOURCE_PATH/s|.*|/* #undef LIBCDIO_SOURCE_PATH */|" \
    "${pkgdir}/usr/include/cdio/cdio_config.h"
}
