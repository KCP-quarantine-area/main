
pkgname=libcdio
pkgver=0.93
pkgrel=3
pkgdesc="GNU Compact Disc Input and Control Library"
arch=('x86_64')
license=('GPL3')
url="http://www.gnu.org/software/libcdio/"
depends=('gcc-libs' 'libcddb' 'ncurses')
options=('!libtool')
source=("http://ftp.gnu.org/gnu/libcdio/${pkgname}-${pkgver}.tar.gz"
        'libcdio-0.83-linking.patch')
md5sums=('d154476feaac5a7b5f180e83eaf3d689'
         '2826f89c78f09b0c0cbaeb47d507a55e')

build() {
  cd ${pkgname}-${pkgver}
  
  patch -p1 -i ${srcdir}/libcdio-0.83-linking.patch
  
  ./configure --prefix=/usr \
              --disable-vcd-info \
              --enable-cpp-progs \
              --disable-static
              
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make -j1 DESTDIR=${pkgdir} install
  
  sed -i "/LIBCDIO_SOURCE_PATH/s|.*|/* #undef LIBCDIO_SOURCE_PATH */|" \
    ${pkgdir}/usr/include/cdio/cdio_config.h
}
