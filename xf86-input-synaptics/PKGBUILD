
pkgname=xf86-input-synaptics
pkgver=1.8.0
pkgrel=3
pkgdesc="Synaptics driver for notebook touchpads"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxtst' 'mtdev' 'libevdev')
makedepends=('xorg-server>=1.16.0' 'libxi' 'libx11' 'resourceproto' 'scrnsaverproto')
groups=('xorg-drivers' 'xorg')
options=('!libtool')
backup=('etc/X11/xorg.conf.d/50-synaptics.conf')
source=("http://xorg.freedesktop.org/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2"
        'tapbutton.patch')
md5sums=('27a3f2b31606a13dd6b58d419978d64f'
         '37e93467e7d9b6f67af301ccfd26f961')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}"/tapbutton.patch
  
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/etc/X11/xorg.conf.d"
  install -m644 conf/50-synaptics.conf "${pkgdir}/etc/X11/xorg.conf.d/"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

  rm -rf "${pkgdir}/usr/share/X11"
}