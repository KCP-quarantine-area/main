
pkgname=vim
pkgver=7.4.516
_pkgver=v7-4-516
pkgrel=1
arch=('x86_64')
pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor.'
license=('custom:vim')
url="http://www.vim.org"
depends=('gpm' 'perl')
makedepends=('python2>=2.7.1' 'ruby' 'libxt' 'libxext' 'libxpm' 'libxmu' 'desktop-file-utils'
             'gettext' 'pkgconfig')
provides=('vim-runtime')
conflicts=('vim-runtime')
# check version https://code.google.com/p/vim/source/list select correct tag
source=("https://vim.googlecode.com/archive/${_pkgver}.tar.gz")
md5sums=('aadf07fbdb38aa0a37571ff73e4ed568')

build() {
  cd "${srcdir}"

  # define the place for the global vimrc file (set to /etc/vimrc)
  sed -e 's|^.*\(#define SYS_.*VIMRC_FILE.*"\) .*$|\1|' \
      -i vim-${_pkgver}/src/feature.h
  sed -e 's|^.*\(#define VIMRC_FILE.*"\) .*$|\1|' \
      -i vim-${_pkgver}/src/feature.h
  
  (cd vim-${_pkgver}/src && autoconf)

  cd "${srcdir}/vim-${_pkgver}"

  ./configure --prefix=/usr --localstatedir=/var/lib/vim \
    --mandir=/usr/share/man --with-compiledby=KaOS \
    --with-features=huge --enable-gpm --enable-acl --with-x=no \
    --disable-gui --enable-multibyte --enable-cscope \
    --disable-netbeans --enable-perlinterp --disable-pythoninterp \
    --disable-rubyinterp
  make
}

package() {
  cd "${srcdir}/vim-${_pkgver}"
  make -j1 VIMRCLOC=/etc DESTDIR="${pkgdir}" install

  # provided by (n)vi in core
  rm "${pkgdir}/usr/bin"/{ex,view}

  # delete some manpages
  find "${pkgdir}/usr/share/man" -type d -name 'man1' 2>/dev/null | \
    while read _mandir; do
    cd "${_mandir}"
    rm -f ex.1 view.1 # provided by (n)vi
    rm -f evim.1    # this does not make sense if we have no GUI
  done

  # license
  install -d -m755 "${pkgdir}/usr/share/licenses/vim"
  ln -s "/usr/share/vim/${_versiondir}/doc/uganda.txt" \
        "${pkgdir}/usr/share/licenses/vim/license.txt"
}
