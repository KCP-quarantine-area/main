
pkgname=pyqt-python3
pkgver=4.11
pkgrel=1
pkgdesc="A set of Python3 bindings for the Qt toolkit"
arch=('x86_64')
url="http://riverbankcomputing.co.uk/software/pyqt/intro"
license=('GPL')
depends=('sip3' 'qtwebkit' 'dbus-python3' 'pyqt-python2')
makedepends=('phonon' 'python2-opengl' 'qt-assistant-compat' 'mesa')
optdepends=('phonon: enable audio and video in PyQt applications'
            'qscintilla: QScintilla API'
            'qt-assistant-compat: add PyQt online help in Qt Assistant')
source=("http://sourceforge.net/projects/pyqt/files/PyQt4/PyQt-${pkgver}/PyQt-x11-gpl-${pkgver}.tar.gz")
md5sums=('8b991f95fec9582cdcc1bf1f4ac02406')

build() {
  cd ${srcdir}/PyQt-x11-gpl-${pkgver}

  python3 configure-ng.py \
    --confirm-license \
    --qsci-api

  find -name 'Makefile' | xargs sed -i 's|-Wl,-rpath,/usr/lib||g;s|-Wl,-rpath,.* ||g'

  make
}

package(){
  cd ${srcdir}/PyQt-x11-gpl-${pkgver}
  # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
  make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install
  
  mv "${pkgdir}"/usr/bin/{,python3-}pyuic4
  
  rm ${pkgdir}/usr/bin/pylupdate4
  rm ${pkgdir}/usr/bin/pyrcc4
  rm ${pkgdir}/usr/lib/qt/plugins/designer/libpyqt4.so
  rm ${pkgdir}/usr/share/qt/qsci/api/python/PyQt4.api
}