
pkgname=crypto++
pkgver=5.6.3
_srcver=563
pkgrel=2
pkgdesc="A free C++ class library of cryptographic schemes."
arch=('x86_64')
license=('custom')
makedepends=('unzip')
url="http://www.cryptopp.com/"
source=("http://www.cryptopp.com/cryptopp${_srcver}.zip"
        'libcrypto++.pc')
md5sums=('3c5b70e2ec98b7a24988734446242d07'
         '46de3847c59ad16456fa863eb33e73b2')

build() {
  cd ${srcdir}
  sed -i -e 's/^CXXFLAGS/#CXXFLAGS/' GNUmakefile
  # https://web.nvd.nist.gov/view/vuln/detail?vulnId=CVE-2015-2141
  #patch -p1 -i ${srcdir}/9425e16437439e68c7d96abef922167d68fafaff.diff
  
  export CXXFLAGS="${CXXFLAGS} -DNDEBUG -fPIC"
  
  make -f GNUmakefile  
  make libcryptopp.so
}

package() {
  cd ${srcdir}

  install -d ${pkgdir}/usr/{lib/pkgconfig,include/cryptopp}
  install -m644 *.h ${pkgdir}/usr/include/cryptopp/
  install -m644 libcryptopp.a "${pkgdir}/usr/lib/libcryptopp.a"
  install -m644 libcryptopp.so "${pkgdir}/usr/lib/libcryptopp.so"
  install -m644 ${srcdir}/libcrypto++.pc "${pkgdir}/usr/lib/pkgconfig/libcrypto++.pc"
  install -D -m644 License.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
