
pkgname=xorg-xinit
pkgver=1.3.2
pkgrel=1
pkgdesc="X.Org initialisation program "
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libx11' 'xorg-xauth' 'inetutils')
makedepends=('pkgconfig')
optdepends=('xorg-twm' 'xterm')
backup=('etc/skel/.xinitrc'
        'etc/skel/.xsession'
        'etc/X11/xinit/xserverrc'
        'etc/X11/xinit/xinitrc')
groups=('xorg')
source=("${url}/releases/individual/app/xinit-${pkgver}.tar.bz2"
        '06_move_serverauthfile_into_tmp.diff'
        'xinitrc'
        'xsession'
        'xserverrc')
options=('!emptydirs')
md5sums=('9c0943cbd83e489ad1b05221b97efd44'
         'abd072ec435ce084cf4e89c58f83d45c'
         'cee2d70748a089fbc7db64a4a6e8fdb1'
         '7873d97db30d6c279ed37e3559e7c59d'
         '4deba1b7f2cc7f93b304b956023e65d6')

build() {
  cd "${srcdir}/xinit-${pkgver}"
  patch -p1 -i "${srcdir}/06_move_serverauthfile_into_tmp.diff"
  
  ./configure --prefix=/usr --with-xinitdir=/etc/X11/xinit
  make
}

package() {
  cd "${srcdir}/xinit-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/etc/skel"
  install -m644 "${srcdir}/xinitrc" "${pkgdir}/etc/skel/.xinitrc"
  install -m644 "${srcdir}/xsession" "${pkgdir}/etc/skel/.xsession"
  install -m644 "${srcdir}/xserverrc" "${pkgdir}/etc/X11/xinit/xserverrc"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
