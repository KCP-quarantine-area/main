
pkgname=plotutils
pkgver=2.6
pkgrel=4
arch=('x86_64')
pkgdesc="Set of utilities and libraries for plotting."
url="http://directory.fsf.org/graphics/plotutils.html"
license=("GPL")
depends=('libpng' 'gcc-libs' 'libxaw')
options=('!libtool')
install=plotutils.install
source=("http://ftp.gnu.org/pub/gnu/plotutils/$pkgname-$pkgver.tar.gz"
        'plotutils-2.6-libpng-1.5.patch')
md5sums=('c08a424bd2438c80a786a7f4b5bb6a40'
         'a155e64a8b0c74e14b774e19edb4554b')


build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 -i ../plotutils-2.6-libpng-1.5.patch
  
  ./configure --prefix=/usr \
	--with-gnu-ld \
	--with-x \
	--enable-libplotter
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make DESTDIR="${pkgdir}" install
}
