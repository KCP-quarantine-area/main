
pkgbase=libofx
pkgname=('libofx' 'libofx-doc')
pkgver=0.9.9
pkgrel=1
pkgdesc="API for the OFX banking standard"
arch=('x86_64')
url="http://libofx.sourceforge.net"
license=('GPL')
depends=('opensp' 'curl' 'libxml++')
checkdepends=('gnupg')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('2ea3adcec51e6dc90555a52a7ab621c9' )

build() {
  cd "$srcdir/$pkgbase-$pkgver"

  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgbase-$pkgver"
  
  make check
}

package_libofx() {
  options=('!libtool' '!docs')

  cd "$srcdir/$pkgbase-$pkgver"
  make DESTDIR="$pkgdir" install
}

package_libofx-doc() {
  pkgdesc="Documention of the OFX banking standard API"
  depends=()

  cd "$srcdir/$pkgbase-$pkgver"
  make DESTDIR="$pkgdir" install-docDATA
  make -C doc DESTDIR="$pkgdir" install
}
