
pkgname=tdb
pkgver=1.3.6
pkgrel=1
pkgdesc="Trivial Database similar to GDBM but allows simultaneous commits"
arch=('x86_64')
license=('GPL3')
url="http://tdb.samba.org/"
makedepends=('python2' 'libxslt' 'docbook-xsl')
optdepends=('python2: for python bindings')
source=("http://samba.org/ftp/tdb/${pkgname}-${pkgver}.tar.gz")
md5sums=('d4df2bcebbcd18463408b5a1ccb5b71b')

build() {
   cd ${srcdir}/${pkgname}-${pkgver}
   
   ./configure --prefix=/usr \
               --localstatedir=/var \
               --sysconfdir=/etc/samba
   make
}

package() {
   cd ${srcdir}/${pkgname}-${pkgver}
   
   make DESTDIR=$pkgdir/ install
}


