
pkgname=udisks
pkgver=1.0.5
pkgrel=3
pkgdesc="Disk Management Service"
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/udisks"
license=('GPL')
depends=('systemd' 'libgudev' 'sg3_utils' 'glib2' 'dbus-glib' 'polkit' 'parted' 'device-mapper'
         'libatasmart' 'lsof' 'lvm2' 'util-linux')
makedepends=('intltool' 'docbook-xsl')
options=(!libtool)
#install=udisks.install
replaces=('devicekit-disks')
source=("http://hal.freedesktop.org/releases/${pkgname}-${pkgver}.tar.gz"
	'drop-pci-db.patch')
md5sums=('70d48dcfe523a74cd7c7fbbc2847fcdd'
         '2746c65d0d12fcc6a550b5a8d9085ab2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i '1i #include <sys/stat.h>' src/helpers/job-drive-detach.c
  
  # fix fail pci-db message, patch thanks to Tom Gundersen <teg@jklm.no>
  patch -p1 -i ${srcdir}/drop-pci-db.patch
  
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/udisks \
      --disable-static

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make profiledir=/usr/share/bash-completion/completions DESTDIR="${pkgdir}" install
  chmod 644 "${pkgdir}/usr/share/bash-completion/completions/udisks-bash-completion.sh"
  # move udev helpers and rules to /usr/lib
  mv "${pkgdir}"/lib/udev "${pkgdir}"/usr/lib/
  rm -r "${pkgdir}"/lib
  
  #fix ntfs mounts
  sed -i -e 's|<allow_active>auth_admin_keep</allow_active>|<allow_active>yes</allow_active>|' ${pkgdir}/usr/share/polkit-1/actions/org.freedesktop.udisks.policy
}
