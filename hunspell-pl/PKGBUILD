
pkgname=hunspell-pl
pkgver=20140731
pkgrel=1
pkgdesc='Polish dictionary for Hunspell/Myspell'
arch=('x86_64')
url='http://www.sjp.pl/slownik/ort/'
license=('GPL')
depends=('hunspell')
source=("http://sjp.pl/slownik/ort/sjp-myspell-pl-$pkgver.zip")
md5sums=('d1b04ef4ad0a3ccb93e7fd15e2672e5c')

prepare() {
  bsdtar -xf pl_PL.zip
}

package() {
  install -dm755 "$pkgdir"/usr/share/hunspell
  install -m644 pl_PL.aff "$pkgdir"/usr/share/hunspell
  install -m644 pl_PL.dic "$pkgdir"/usr/share/hunspell

  install -dm755 ${pkgdir}/usr/share/myspell/dicts
  pushd "$pkgdir"/usr/share/myspell/dicts
  for file in "$pkgdir"/usr/share/hunspell/*; do
    ln -sv /usr/share/hunspell/$(basename $file) .
  done
  popd

  install -dm755 "$pkgdir"/usr/share/doc/$pkgname
  install -m644 README_pl_PL.txt "$pkgdir"/usr/share/doc/$pkgname
}