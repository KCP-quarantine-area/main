
pkgname=lm_sensors
pkgver=3.3.4
pkgrel=1
pkgdesc="Collection of user space tools for general SMBus access and hardware monitoring"
arch=('x86_64')
url="http://www.lm-sensors.org/"
license=('GPL' 'LGPL')
depends=('perl' 'sysfsutils')
backup=('etc/sensors3.conf' 'etc/conf.d/healthd' 'etc/conf.d/sensord')
options=('!emptydirs')
source=("http://dl.lm-sensors.org/lm-sensors/releases/lm_sensors-${pkgver}.tar.bz2" \
        'healthd' 'healthd.conf' 'sensord.conf' \
        'fancontrol.patch' 'linux_3.0.patch' \
        'fancontrol.service' 'sensord.service' 'healthd.service')
md5sums=('60cfb98ab7a8915c3ac684af5459de2e'
         'b68bdb73bb4d28ebdb36e2d92278582d'
         'f8af587038b0e2a89c441f7eeaa5e640'
         '96a8dd468e81d455ec9b165bdf33e0b7'
         '020a7ca27b203cd4fccbe7655e1f59d2'
         '0583decd3f47fb2b98204aa623ea7375'
         '3e2e0ebe6e0772cac50bd2721229f12f'
         'b2dfc6fc1cdb38432b8317dbc63ce23f'
         'daf19e142d59f015040814172742502b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|/etc/sysconfig|/etc/conf.d|' prog/{detect/sensors-detect,init/lm_sensors.service}
  sed -i 's@\(/bin/systemctl\|/lib/systemd/system\)@/usr\1@g' prog/detect/sensors-detect
  #if the configuration file is not present don't show any errors (file generated with sensors-detect), Arch bug #30887
  sed -i 's/EnvironmentFile=/EnvironmentFile=-/' prog/init/lm_sensors.service
  
  patch -p0 -i ${srcdir}/fancontrol.patch
  patch -p0 -i ${srcdir}/linux_3.0.patch
  
  make PREFIX=/usr
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make BUILD_STATIC_LIB=0 \
    PREFIX=/usr MANDIR=/usr/share/man DESTDIR="${pkgdir}" install
    
  install -D -m644 prog/init/lm_sensors.service "${pkgdir}/usr/lib/systemd/system/lm_sensors.service"
  install -D -m755 "${srcdir}/healthd" "${pkgdir}/usr/sbin/healthd"
  install -D -m644 "${srcdir}/healthd.conf" "${pkgdir}/etc/conf.d/healthd"
  install -D -m644 "${srcdir}/sensord.conf" "${pkgdir}/etc/conf.d/sensord"
  install -D -m644 "${srcdir}/fancontrol.service" "${pkgdir}/usr/lib/systemd/system/fancontrol.service"
  install -D -m644 "${srcdir}/sensord.service" "${pkgdir}/usr/lib/systemd/system/sensord.service"
  install -D -m644 "${srcdir}/healthd.service" "${pkgdir}/usr/lib/systemd/system/healthd.service"
}
