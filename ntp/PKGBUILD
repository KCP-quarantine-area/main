
pkgname=ntp
pkgver=4.2.8
_pkgver=4.2
pkgrel=1
pkgdesc="NTP (Network Time Protocol) tries to keep servers in sync"
arch=('x86_64')
license=('custom')
url="http://www.ntp.org/"
depends=('openssl' 'readline' 'libcap' 'libedit')
makedepends=('perl-html-parser')
backup=('etc/ntp.conf')
install=install
source=("http://archive.ntp.org/ntp4/ntp-${_pkgver}/ntp-${pkgver}.tar.gz"
        'ntp.conf'
	'ntpd.service'
	'ntpdate.service')
md5sums=('6972a626be6150db8cfbd0b63d8719e7'
         '086ad240f3ae42b15cc8dfa8682107e7'
         'bc19fc0b0e158581dce816af53b0f11b'
         '24f0e0aae19b2c1cd4a84cb9c0d0c934')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  ./configure --prefix=/usr \
      --mandir=/usr/share/man \
      --libexecdir=/usr/lib \
      --enable-linuxcaps \
      --enable-ntp-signd 
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}" 
  make DESTDIR="${pkgdir}" install
  
  install -d -o 87 "${pkgdir}/var/lib/ntp"
  echo > "${pkgdir}/var/lib/ntp/.placeholder"
  
  install -Dm644 ../ntp.conf "${pkgdir}"/etc/ntp.conf
  install -Dm644 ../ntpd.service "${pkgdir}"/usr/lib/systemd/system/ntpd.service
  install -Dm644 ../ntpdate.service "${pkgdir}"/usr/lib/systemd/system/ntpdate.service
  install -Dm644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
