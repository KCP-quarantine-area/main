
pkgname=tidyhtml
# obtain pkgver from http://tidy.cvs.sourceforge.net/tidy/tidy/src/version.h?view=markup
pkgver=1.46
pkgrel=3
pkgdesc="A tool to tidy down your HTML code to a clean style"
arch=('x86_64')
url="http://tidy.sourceforge.net/"
license=('custom')
depends=('glibc')
options=('!libtool')
source=("http://sourceforge.net/projects/kdeos/files/Sources/tidyhtml/${pkgname}-${pkgver}.tar.bz2")
md5sums=('3d970d54be83c5ec985687e9976a2832')

mksource() {
  mkdir tidyhtml-${pkgver}
  cd tidyhtml-${pkgver}
  cvs -d:pserver:anonymous@tidy.cvs.sourceforge.net:/cvsroot/tidy login   #hit enter for password
  cvs -z3 -d:pserver:anonymous@tidy.cvs.sourceforge.net:/cvsroot/tidy co -P tidy
  cd ..
  tar -cvjf tidyhtml-${pkgver}.tar.bz2 tidyhtml-${pkgver}/*
}

build(){
  cd $srcdir/${pkgname}-${pkgver}/tidy
  source build/gnuauto/setup.sh
  
  ./configure --prefix=/usr
  make 
}

package() {
  cd $srcdir/${pkgname}-${pkgver}/tidy
  make DESTDIR=$pkgdir install
  
  install -Dm644 htmldoc/license.html \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
