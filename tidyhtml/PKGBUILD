
pkgname=tidyhtml
_pkgname=tidy-html5
pkgver=5.2.0
pkgrel=1
pkgdesc="A tool to tidy down your HTML code to a clean style"
arch=('x86_64')
url="http://www.html-tidy.org/"
license=('custom')
depends=('glibc')
makedepends=('cmake' 'git')
options=('!libtool')
source=("https://github.com/htacg/${_pkgname}/archive/${pkgver}.tar.gz")
md5sums=('ffbe6e8471eff90877b4d74bb714e22f')

build() {
  mkdir -p build
  cd build
  
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr 
  make 
}

package() {
  cd build
  make DESTDIR=$pkgdir install
  
  install -Dm644 $srcdir/${_pkgname}-${pkgver}/README/LICENSE.md ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md
}
