
pkgname=openjpeg
pkgver=1.5.1
pkgrel=3
pkgdesc="An open source JPEG 2000 codec"
arch=('x86_64')
license=('BSD')
url="http://www.openjpeg.org"
depends=('zlib')
source=("http://openjpeg.googlecode.com/files/${pkgname}-${pkgver}.tar.gz")
md5sums=('b5f74cec2688fb918331bb014061be6f')

options=('!libtool')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # use system libs, informative output during build
  rm -rf thirdparty
  ./configure --prefix=/usr \
        --disable-static --disable-silent-rules
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/openjpeg"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/openjpeg/LICENSE"
  ln "${pkgdir}/usr/include/openjpeg-1.5/openjpeg.h" "${pkgdir}/usr/include/openjpeg.h"
}

