
pkgname=sni-qt
pkgver=0.2.6
pkgrel=1
pkgdesc='A Qt plugin which turns all QSystemTrayIcon into StatusNotifierItems (appindicators)'
arch=('x86_64')
url='https://launchpad.net/sni-qt'
license=('LGPL3')
depends=('libdbusmenu-qt')
makedepends=('cmake')
backup=('etc/sni-qt.conf')
source=("http://launchpad.net/sni-qt/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.bz2"
        'sni-qt.conf')
md5sums=('e84c66e776f7c22c961817387f618c81'
         '1b3ed5aba470a3539209b34ab637ff48')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/sni-qt.conf ${pkgdir}/etc/sni-qt.conf
}
