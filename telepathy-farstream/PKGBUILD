
pkgname=telepathy-farstream
pkgver=0.6.1
pkgrel=1
pkgdesc="A telepathy-backend to use stream engine."
arch=('x86_64')
url="http://telepathy.freedesktop.org"
license=('LGPL2.1')
depends=('telepathy-glib' 'farstream')
makedepends=('gobject-introspection' 'dbus-glib')
groups=('telepathy')
options=('!libtool')
source=("http://telepathy.freedesktop.org/releases/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('53e3a69bdee7b301e2fdd2f2d254e385')

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/telepathy \
    --disable-static
  make
}

package() {
  cd "$pkgname-$pkgver"
  
  make DESTDIR="$pkgdir" install
}

