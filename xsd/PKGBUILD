
pkgname=xsd
pkgver=3.3.0
_pkgver=3.3.0-2+dep
pkgrel=2
pkgdesc="An open-source, cross-platform W3C XML Schema to C++ data binding compiler"
arch=('x86_64')
url="http://www.codesynthesis.com/products/xsd"
license=('GPL2')
depends=('boost-libs' 'xerces-c')
makedepends=('boost')
source=("http://www.codesynthesis.com/download/${pkgname}/3.3/${pkgname}-${_pkgver}.tar.bz2"
        "xsdcxx.patch")
md5sums=('7ecee5029c119ef678bbe5ca4ef8a288'
         'ca9c49a202b6f4eed4e578840cab8674')

build() {
  cd "${pkgname}-${_pkgver}"
  patch -p0 -i "${srcdir}/xsdcxx.patch"

  make CXXFLAGS="${CXXFLAGS}"
}

package() {
  cd ${pkgname}-${_pkgver}
  make install_prefix="$pkgdir/usr" install

  # Fix conflicts with mono
  mv "${pkgdir}"/usr/bin/xsd{,cxx}
  mv "${pkgdir}"/usr/share/man/man1/xsd{,cxx}.1
}
