
pkgname=jack2
pkgver=1.9.10
pkgrel=1
_commit=ce825c6
pkgdesc="The next-generation JACK with SMP support"
arch=('x86_64')
url="http://jackaudio.org/"
backup=(etc/security/limits.d/99-audio.conf)
license=('GPL')
depends=('libsamplerate' 'celt' 'libffado' 'dbus' 'opus' 'dbus-python2' 'portaudio-svn')
makedepends=('python2' 'doxygen')
optdepends=('dbus-python2: jack_control')
provides=('jack' 'jack2-dbus')
# Build for doxygen fix, commit 95a1162d6aecc91882e4d8b01ba7fb12f6d29d1c Dec 11 2012, remove with 1.9.9.10 or above
#source=("http://sourceforge.net/projects/kdeos/files/Sources/jack/jack2.tar.xz"
source=("https://github.com/jackaudio/jack2/tarball/master/jack2-${pkgver}.tar.gz"
        '99-audio.conf'
        '40-hpet-permissions.rules')
md5sums=('SKIP'
         '4d928a76e3f3d77d037c42ab75ac0f0b'
         '23eba69d2dbbb800f68176e5d38b9af5')

build() {
  cd "${srcdir}/jackaudio-${pkgname}-${_commit}"
  # fix doxygen dir
  sed -i 's|build/default/html|html|' ${srcdir}/jackaudio-${pkgname}-${_commit}/wscript
  
  LDFLAGS="$LDFLAGS,--no-as-needed -lm" python2 ./waf configure \
    --prefix=/usr \
    --alsa \
    --classic \
    --dbus \
    --firewire \
    --doxygen \
    --portaudio

  python2 ./waf build


}

package() {
  cd "${srcdir}/jackaudio-${pkgname}-${_commit}"

  python2 waf install --destdir="${pkgdir}"

  # configure realtime access/scheduling
  install -Dm644 "${srcdir}/99-audio.conf" \
    "${pkgdir}/etc/security/limits.d/99-audio.conf"

  install -Dm644 "${srcdir}/40-hpet-permissions.rules" \
    "${pkgdir}/usr/lib/udev/rules.d/40-hpet-permissions.rules"
}

