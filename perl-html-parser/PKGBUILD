
pkgname=perl-html-parser
_realname=HTML-Parser
pkgver=3.71
pkgrel=4
pkgdesc="Perl HTML parser class"
arch=('x86_64')
license=('PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl-html-tagset' 'perl')
checkdepends=('perl-test-pod')
options=(!emptydirs)
source=("http://www.cpan.org/authors/id/G/GA/GAAS/${_realname}-${pkgver}.tar.gz")
md5sums=('9128a45893097dfa3bf03301b19c5efe')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  
  make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  
  make install DESTDIR="${pkgdir}"
}
