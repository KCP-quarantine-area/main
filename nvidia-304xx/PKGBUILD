
_extramodules=extramodules-3.19
_kver=$(cat /lib/modules/${_extramodules}/version)

pkgname=('nvidia-304xx' 'khd-nvidia-304xx')
pkgver=304.125
pkgrel=4
arch=('x86_64')
url="http://www.nvidia.com/"
makedepends=('linux-headers' 'linux>=3.19' 'linux<4.0' "nvidia-304xx-utils=${pkgver}")
license=('custom')
source=("https://raw.github.com/KaOSx/hardware-detection/master/hwdb/nvidia_supported"
        '20-nvidia.conf'
        'nv-drm.patch'
        'linux-3.19.patch')
md5sums=('62d0cdb856cfae64753c0f3acf729d47'
         '7642a7a57034c96a7952771c671c81a2'
         '79671a27131da619a33eb02ed0c2c031'
         'd14e651d20054d18e0360c157ae60ae4')

_pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"
source+=("ftp://download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/${_pkg}.run")
md5sums+=('291d41ccdbcc08f752a38f9e0a7eeea0')

build() {
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    
    cd "${_pkg}"
    patch -p0 -i ${srcdir}/nv-drm.patch
    patch -p1 -i ${srcdir}/linux-3.19.patch
    
    cd "${srcdir}/${_pkg}/kernel"  
    make SYSSRC=/lib/modules/"${_kver}/build" module
}

package_nvidia-304xx() {
    pkgdesc="NVIDIA drivers for linux."
    depends=('linux>=3.19' 'linux<4.0' "nvidia-304xx-utils=${pkgver}")
    conflicts=('nvidia')
    backup=('etc/X11/xorg.conf.d/20-nvidia.conf')
    install=nvidia.install
    
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/etc/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist.conf"
    
    install -D -m644 "${srcdir}/20-nvidia.conf" "${pkgdir}/etc/X11/xorg.conf.d/20-nvidia.conf"
    
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}

package_khd-nvidia-304xx() {
    pkgdesc="KHD ids for nvidia $pkgver"
    arch=('x86_64')

    cd "${srcdir}/${_pkg}"

    install -dm755 "${pkgdir}/opt/kdeos/hwdb"
    sh -e "${srcdir}/nvidia_supported" README.txt kernel/nv-kernel.o \
        > "${pkgdir}/opt/kdeos/hwdb/hw_nvidia304xx_hwdb"
}
