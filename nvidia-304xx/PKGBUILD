
_extramodules=extramodules-3.14
_kver=$(cat /lib/modules/${_extramodules}/version)

pkgname=('nvidia-304xx' 'khd-nvidia-304xx')
pkgver=304.123
pkgrel=1
arch=('x86_64')
url="http://www.nvidia.com/"
makedepends=('linux-headers' 'linux>=3.14' 'linux<3.15' "nvidia-304xx-utils=${pkgver}")
license=('custom')
source=("https://raw.github.com/KaOSx/hardware-detection/master/hwdb/nvidia_supported")
md5sums=('62d0cdb856cfae64753c0f3acf729d47')

_pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"
source+=("ftp://download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/${_pkg}.run")
md5sums+=('9d461e9e00e473f458aee96cd6d85101')

build() {
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    
    cd "${_pkg}/kernel"
    
    make SYSSRC=/lib/modules/"${_kver}/build" module
}

package_nvidia-304xx() {
    pkgdesc="NVIDIA drivers for linux."
    depends=('linux>=3.14' 'linux<3.15' "nvidia-304xx-utils=${pkgver}")
    conflicts=('nvidia')
    install=nvidia.install
    
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/etc/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}

package_khd-nvidia-304xx() {
    pkgdesc="KHD ids for nvidia $pkgver"
    arch=('x86_64')

    cd "${srcdir}/${_pkg}"

    install -dm755 "${pkgdir}/opt/kdeos/hwdb"
    sh -e "${srcdir}/nvidia_supported" README.txt kernel/nv-kernel.o \
        > "${pkgdir}/opt/kdeos/hwdb/hw_nvidia304xx_hwdb"
}
