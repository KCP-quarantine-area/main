
pkgname=bash-completion
pkgver=2.1
pkgrel=1
pkgdesc="Programmable completion for the bash shell"
arch=('x86_64')
url="http://bash-completion.alioth.debian.org/"
license=('GPL')
depends=('bash')
source=("http://bash-completion.alioth.debian.org/files/${pkgname}-${pkgver}.tar.bz2")
md5sums=('4e2a9f11a4042a38ee79ddcd048e8b9e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # this should be removed if /lib ever is a symlink to usr/lib
  sed -i '/lib\/modules/ s,/lib/modules,/usr&,' bash_completion

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # bash-completion is sourced in /etc/bash.bashrc so that non-bash shell don't source it
  rm -r "${pkgdir}/etc/profile.d"
  # remove Slackware's makepkg completion
  rm "${pkgdir}/usr/share/bash-completion/completions/makepkg"
  # part of networkmanager
  rm "${pkgdir}/usr/share/bash-completion/completions/nmcli"
  # part of util-linux
  rm "${pkgdir}/usr/share/bash-completion/completions/cal"
  rm "${pkgdir}/usr/share/bash-completion/completions/chsh"
  rm "${pkgdir}/usr/share/bash-completion/completions/dmesg"
  rm "${pkgdir}/usr/share/bash-completion/completions/eject"
  rm "${pkgdir}/usr/share/bash-completion/completions/hd"
  rm "${pkgdir}/usr/share/bash-completion/completions/hexdump"
  rm "${pkgdir}/usr/share/bash-completion/completions/hwclock"
  rm "${pkgdir}/usr/share/bash-completion/completions/ionice"
  rm "${pkgdir}/usr/share/bash-completion/completions/look"
  rm "${pkgdir}/usr/share/bash-completion/completions/ncal"
  rm "${pkgdir}/usr/share/bash-completion/completions/newgrp"
  rm "${pkgdir}/usr/share/bash-completion/completions/renice"
  rm "${pkgdir}/usr/share/bash-completion/completions/rtcwake"
  rm "${pkgdir}/usr/share/bash-completion/completions/su"
}

