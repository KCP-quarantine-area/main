
pkgbase=gstreamer0.10-ugly
pkgname=('gstreamer0.10-ugly' 'gstreamer0.10-ugly-plugins')
pkgver=0.10.19.1
pkgrel=2
arch=('x86_64')
license=('LGPL')
makedepends=('pkgconfig' 'gstreamer0.10-base' 'libdvdread' 'lame' 'libmpeg2' 'a52dec'
             'libid3tag' 'libmad' 'libsidplay' 'libcdio' 'x264' 'opencore-amr' 'git')
url="http://gstreamer.freedesktop.org/"
options=('!libtool')
source=("git://anongit.freedesktop.org/gstreamer-sdk/gst-plugins-ugly#commit=d637756a8e569753e9869c2c0728288f5dbc5089"
        'opencore-amr.patch'
        'libcdio.patch')
md5sums=('SKIP'
         'd8feb6c99bfaff8be6b2c48ea4e98e47'
         '6c1c665f864387f3a77d32231fedeaab')

build() {
  cd "${srcdir}/gst-plugins-ugly"
  patch -p0 -i "${srcdir}/opencore-amr.patch"
  patch -p1 -i "${srcdir}/libcdio.patch"
  
  sed -e 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' -i configure.ac
  NONCONFIGURE=1 ./autogen.sh --disable-gtk-doc

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
        --disable-static --enable-experimental \
        --with-package-name="GStreamer Ugly Plugins (KaOS)" \
        --with-package-origin="http://kaosx.us/" 
  make 
  sed -e 's/gst-libs gst ext/gst-libs gst/' -i Makefile 
}

package_gstreamer0.10-ugly() {
  pkgdesc="GStreamer Multimedia Framework Ugly plugin libraries"
  depends=('gstreamer0.10-base>=0.10.36')
  
  cd "${srcdir}/gst-plugins-ugly"
  make DESTDIR="${pkgdir}" install 
}

package_gstreamer0.10-ugly-plugins() {
  pkgdesc="GStreamer Multimedia Framework Ugly Plugins (gst-plugins-ugly)"
  depends=("gstreamer0.10-ugly=${pkgver}" 'libdvdread' 'lame' 'libmpeg2' 'a52dec' 'libid3tag'
           'libmad' 'libsidplay' 'libcdio' 'x264' 'opencore-amr')
  groups=('gstreamer0.10-plugins')

  cd "${srcdir}/gst-plugins-ugly"
  make -C ext DESTDIR="${pkgdir}" install 
}
