
pkgname=openmpi
pkgver=1.10.2
pkgrel=1
pkgdesc="High performance message passing library (MPI)"
arch=('x86_64')
url="http://www.open-mpi.org"
license=('custom')
depends=('gcc' 'openssh' 'valgrind' 'libltdl' 'hwloc')
makedepends=('inetutils')
options=('!libtool')
source=("http://www.open-mpi.org/software/ompi/v1.10/downloads/${pkgname}-${pkgver}.tar.bz2")
md5sums=('b2f43d9635d2d52826e5ef9feb97fd4c')

build() {
   cd "${srcdir}/${pkgname}-${pkgver}"

   ./configure --prefix=/usr \
               --sysconfdir=/etc/${pkgname} \
               --mandir=/usr/share/man \
               --enable-mpi-fortran=all \
               --libdir=/usr/lib/${pkgname} \
               --with-threads=posix \
               --enable-smp-locks \
               --with-valgrind \
               --enable-memchecker \
               --enable-debug \
               --enable-pretty-print-stacktrace \
               --without-slurm \
               --with-hwloc=/usr \
               --with-libltdl=/usr \
               FC=/usr/bin/gfortran \
               LDFLAGS="$LDFLAGS -Wl,-z,noexecstack"

   make
}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   make DESTDIR="${pkgdir}" install

   install -d -m 755 "${pkgdir}"/usr/lib/pkgconfig
   for i in ompi-c.pc ompi-cxx.pc ompi-f77.pc ompi-f90.pc ompi.pc; do
      ln -sf /usr/lib/openmpi/pkgconfig/$i "${pkgdir}"/usr/lib/pkgconfig/
   done

   # texlive conflicts
   mv "${pkgdir}"/usr/bin/otfinfo{,mpi}

   install -d -m 755 "${pkgdir}"/etc/ld.so.conf.d
   echo "/usr/lib/${pkgname}" > "${pkgdir}"/etc/ld.so.conf.d/${pkgname}.conf

   install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
