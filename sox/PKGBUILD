
pkgname=sox
pkgver=14.4.2
pkgrel=1
pkgdesc="The Swiss Army knife of sound processing tools"
arch=('x86_64')
url="http://sox.sourceforge.net/"
license=('GPL' 'LGPL')
depends=('libltdl' 'file' 'libsndfile' 'libpng' 'lame' 'opencore-amr' 'gsm' 'pulseaudio')
makedepends=('libao' 'libmad' 'libid3tag' 'wavpack')
optdepends=('libao: for ao plugin'
            'libmad: for mp3 plugin'
            'libid3tag: for mp3 plugin'
            'wavpack: for wavpack plugin')
options=('!libtool')
source=("http://downloads.sourceforge.net/sox/${pkgname}-${pkgver}.tar.bz2")
md5sums=('ba804bb1ce5c71dd484a102a5b27d0dd')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's/(sox_.*|lsx_(check_read_params|/(sox_.*|lsx_(error|flush|check_read_params|/' src/Makefile.am
  
  aclocal
  automake
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-dyn-default \
    --with-distro="KaOS" 
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make DESTDIR="${pkgdir}" install
}
