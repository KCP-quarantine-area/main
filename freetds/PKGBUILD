
pkgname=freetds
pkgver=0.95.21
pkgrel=1
pkgdesc='Library for accessing Sybase and MS SQL Server databases'
url='http://www.freetds.org'
arch=('x86_64')
license=('LGPL')
depends=('unixodbc' 'openssl')
options=('!libtool')
backup=('etc/freetds/freetds.conf'
        'etc/freetds/locales.conf'
        'etc/freetds/pool.conf')
source=("ftp://ftp.freetds.org/pub/freetds/stable/${pkgname}-${pkgver}.tar.bz2")
md5sums=('d76826675569e15a8e2b8f02c7c4ef98')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	
	./configure --prefix=/usr \
	      --sysconfdir=/etc/freetds \
	      --mandir=/usr/share/man \
	      --enable-msdblib \
	      --with-tdsver=7.0 \
	      --with-openssl
	make
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	
	make DESTDIR=${pkgdir} install
}
