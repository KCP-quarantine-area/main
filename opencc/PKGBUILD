
pkgname=opencc
_pkgname=OpenCC-ver
pkgver=1.0.2
pkgrel=1
url="https://github.com/BYVoid/OpenCC"
arch=('x86_64')
pkgdesc="Library for Open Chinese Convert"
license=('Apache')
depends=('glibc')
makedepends=('cmake' 'doxygen' 'python2')
source=("https://github.com/BYVoid/OpenCC/archive/ver.${pkgver}.tar.gz")
md5sums=('9909d78a3765eab3165573cf08b51ed9')

build() {
  cd ${_pkgname}.${pkgver}

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_DOCUMENTATION:BOOL=ON
  make
}

package() {
  cd ${_pkgname}.${pkgver}

  make DESTDIR="${pkgdir}" install
}