
pkgname=gc
pkgver=7.4.2
_pkgver=7.4.2
pkgrel=1
pkgdesc="A garbage collector for C and C++"
arch=('x86_64')
url="http://www.hboehm.info/gc/"
license=('GPL')
depends=('gcc-libs' 'libatomic_ops')
options=('!libtool')
source=("http://www.hboehm.info/gc/gc_source/${pkgname}-${_pkgver}.tar.gz")
md5sums=('12c05fd2811d989341d8c6d81f66af87')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's#pkgdata#doc#' doc/doc.am
  
  autoreconf -fi
  ./configure --prefix=/usr --enable-cplusplus --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  sed -i -e 's/GC_MALLOC 1L/gc 3/' doc/gc.man
  install -m755 -d "${pkgdir}/usr/share/man/man3"
  install -m644 doc/gc.man "${pkgdir}/usr/share/man/man3/gc.3"
}
