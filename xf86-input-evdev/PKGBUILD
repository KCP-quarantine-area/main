
pkgname=xf86-input-evdev
pkgver=2.9.0
pkgrel=2
pkgdesc="X.org evdev input driver"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc' 'systemd' 'mtdev' 'xf86driproto' 'libevdev')
makedepends=('xorg-server>=1.16.0' 'resourceproto' 'scrnsaverproto')
options=('!libtool' '!makeflags')
groups=('xorg-drivers' 'xorg')
source=("${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2")
md5sums=('9076ae2646f7aeb30963056e0bbfccf0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
