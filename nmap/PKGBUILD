
pkgname=nmap
pkgver=7.12
pkgrel=1
pkgdesc="Network exploration tool and security/port scanner"
arch=('x86_64')
url="http://nmap.org"
license=('GPL2')
depends=('pcre' 'openssl' 'libpcap>=1.0.0' 'lua')
makedepends=('python2')
source=("http://nmap.org/dist/${pkgname}-${pkgver}.tar.bz2")
md5sums=('0764f4dabe7cccda3c49fc3990b62a8a')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr --mandir=/usr/share/man --libexecdir=/usr/lib --without-gui
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  # remove zenmap uninstall script
  rm ${pkgdir}/usr/bin/uninstall_zenmap
  # zenmap needs gtk
  rm ${pkgdir}/usr/share/applications/zenmap-root.desktop
  rm ${pkgdir}/usr/share/applications/zenmap.desktop

  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

