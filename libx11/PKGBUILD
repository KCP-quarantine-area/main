
pkgname=libx11
pkgver=1.6.3
pkgrel=2
pkgdesc="X11 client-side library"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
depends=('libxcb' 'xproto' 'kbproto')
makedepends=('xorg-util-macros' 'xextproto' 'xtrans' 'inputproto')
options=('!libtool')
license=('custom')
source=("http://xorg.freedesktop.org/releases/individual/lib/libX11-${pkgver}.tar.bz2")
md5sums=('2e36b73f8a42143142dda8129f02e4e0')

build() {
  cd "${srcdir}/libX11-${pkgver}"
  
  ./configure --prefix=/usr --disable-static --disable-xf86bigfont
  make
}

check() {
  cd "${srcdir}/libX11-${pkgver}"
  
  make check
}

package() {
  cd "${srcdir}/libX11-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
