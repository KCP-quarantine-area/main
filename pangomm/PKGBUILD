
pkgbase=pangomm
pkgname=('pangomm' 'pangomm-docs')
pkgver=2.40.0
_pkgver=2.40
pkgrel=1
arch=('x86_64')
license=('LGPL')
makedepends=('pango' 'glibmm' 'glibmm-docs' 'cairomm' 'pkgconfig')
source=("http://ftp.gnome.org/pub/GNOME/sources/pangomm/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
options=('!libtool')
url="http://gtkmm.sourceforge.net/"
md5sums=('681fe43bac96e63321406e1b092093d2')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr
  make
}
package_pangomm() {
  pkgdesc="C++ bindings for pango"
  depends=('pango' 'glibmm' 'cairomm')
  
  cd ${pkgbase}-${pkgver}
  sed -i -e 's|^doc_subdirs|#doc_subdirs|' Makefile
  make DESTDIR=${pkgdir} install
}

package_pangomm-docs() {
  pkgdesc="Developer documentation for pangomm"
  
  cd ${pkgbase}-${pkgver}/docs
  make DESTDIR=${pkgdir} install
}
