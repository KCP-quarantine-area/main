
pkgname=vlc
pkgver=2.1.2
_pkgver=2.1.2
pkgrel=2
arch=('x86_64')
url="http://www.videolan.org/vlc/"
license=('GPL')
depends=('a52dec' 'fluidsynth' 'libmtp' 'libdvbpsi' 'libxpm' 'libcdio' 'libdca' 'qt' 'libproxy'
         'sdl_image' 'libdvdnav' 'mesa' 'v4l-utils' 'libcddb' 'samba' 'libmatroska' 'zvbi'
         'taglib' 'sysfsutils' 'libmpcdec' 'ffmpeg' 'faad2' 'libupnp' 'libshout' 'libmad' 'fribidi'
         'libmpeg2' 'libmodplug' 'opus' 'ttf-freefont' 'libxv' 'libass' 'xdg-utils'
         'desktop-file-utils' 'xcb-util-keysyms')
makedepends=('avahi' 'pkgconfig' 'libnotify' 'libbluray' 'flac' 'libtheora' 'alsa-lib'
             'jack' 'kdelibs' 'systemd' 'libraw1394' 'libdc1394' 'libavc1394' 'libva' 'pulseaudio'
             'lirc-utils' 'gnutls' 'libcaca' 'librsvg' 'live-media')
optdepends=('avahi: for service discovery using bonjour protocol'
            'libnotify: for notification plugin'
            'ncurses: for ncurses interface support'
            'libdvdcss: for decoding encrypted DVDs'
            'lirc-utils: for lirc plugin'
            'libavc1394: for devices using the 1394ta AV/C'
            'libdc1394: for IEEE 1394 plugin'
            'pulseaudio: PulseAudio support'
            'vdpau-video: vdpau back-end for nvidia'
            'libva-driver-intel: back-end for intel cards'
            'libbluray: for Blu-Ray disks')
backup=('usr/share/vlc/http/.hosts'
        'usr/share/vlc/http/dialogs/.hosts'
        'usr/share/vlc/lua/http/.hosts'
        'usr/share/vlc/lua/http/dialogs/.hosts')
groups=('multimedia')
options=('!libtool')
install="vlc.install"
source=("http://download.videolan.org/pub/videolan/$pkgname/${_pkgver/a}/$pkgname-$_pkgver.tar.xz")
md5sums=('fb6787ad749aadcfaeab3471939b3426')

build() {
  cd "${srcdir}/${pkgname}-${_pkgver/a}"

  sed -i -e 's:truetype/freefont:TTF:g' modules/text_renderer/freetype.c

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --disable-rpath \
              --enable-faad \
              --disable-rpath \
              --enable-faad \
              --enable-nls \
              --enable-lirc \
              --enable-upnp \
              --enable-realrtsp \
              --enable-bluray \
              --enable-opus \
              --enable-ncurses \
              --disable-lua
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_pkgver/a}"

  make DESTDIR="${pkgdir}" install

  for res in 16 32 48 128; do
    install -D -m644 "${srcdir}/vlc-${_pkgver/a}/share/icons/${res}x${res}/vlc.png" \
        "${pkgdir}/usr/share/icons/hicolor/${res}x${res}/apps/vlc.png"
  done
}

