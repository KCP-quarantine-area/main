
pkgbase=atkmm
pkgname=('atkmm' 'atkmm-docs')
pkgver=2.22.7
_pkgver=2.22
pkgrel=3
arch=('x86_64')
makedepends=('atk' 'glibmm' 'glibmm-docs' 'libsigc++')
license=('LGPL')
options=('!libtool' '!emptydirs')
url="http://gtkmm.sourceforge.net/"
source=("http://ftp.gnome.org/pub/GNOME/sources/atkmm/${_pkgver}/${pkgbase}-${pkgver}.tar.xz")
md5sums=('fec7db3fc47ba2e0c95d130ec865a236')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  
  ./configure --prefix=/usr
  make
}

package_atkmm() {
  pkgdesc="C++ bindings for atk"
  depends=('atk' 'glibmm' 'libsigc++')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="${pkgdir}" install
}

package_atkmm-docs() {
  pkgdesc="Developer documentation for atkmm"
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C doc DESTDIR="${pkgdir}" install
}
