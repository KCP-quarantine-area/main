
pkgname=phonon-qt5
_pkgname=phonon
pkgver=4.7.1
pkgrel=1
arch=('x86_64')
url="http://phonon.kde.org"
license=('LGPL')
pkgdesc="The multimedia framework for KDE4"
depends=('phonon-backend' 'pulseaudio' 'qt5-base')
makedepends=('cmake' 'automoc4' 'qt5-tools' 'qt5-quick1')
source=("http://download.kde.org/stable/${_pkgname}/${pkgver}/${_pkgname}-${pkgver}.tar.xz")
md5sums=('0935e1c00440a03e3032860030398665')

build() {
  cd "${srcdir}"
  mkdir build
  
  cd build
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DPHONON_BUILD_PHONON4QT5=ON \
    -DPHONON_INSTALL_QT_EXTENSIONS_INTO_SYSTEM_QT=ON 
  make
}

package(){
  cd "${srcdir}"/build
  
  make DESTDIR="${pkgdir}" install
}