
pkgname=libiodbc
pkgver=3.52.12
pkgrel=1
pkgdesc='Independent Open DataBase Connectivity for Linux'
arch=('x86_64')
url='http://www.iodbc.org'
license=('LGPL')
depends=('glibc' 'sh')
makedepends=('chrpath')
options=('!libtool')
source=("http://downloads.sourceforge.net/sourceforge/iodbc/${pkgname}-${pkgver}.tar.gz")
md5sums=('c76cab12a642a024bd1a262615a1a4a8')

build() {
	cd ${pkgname}-${pkgver}
	
	./configure --prefix=/usr \
		--disable-static \
		--includedir=/usr/include/libiodbc \
		--disable-gui \
		--disable-libodbc
	make 
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
	
	chrpath -d ${pkgdir}/usr/bin/iodbctest{,w}
	rm -rf ${pkgdir}/usr/share/libiodbc/
}
