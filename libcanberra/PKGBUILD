
pkgbase=libcanberra
pkgname=('libcanberra' 'libcanberra-pulse' 'libcanberra-gstreamer')
pkgver=0.29
pkgrel=1
pkgdesc="A small and lightweight implementation of the XDG Sound Theme Specification"
arch=('x86_64')
license=('LGPL')
depends=('libvorbis>=1.3.1' 'libtool' 'alsa-lib' 'tdb' 'sound-theme-freedesktop')
makedepends=('pulseaudio' 'gstreamer0.10')
options=('!emptydirs')
url="http://0pointer.de/lennart/projects/libcanberra"
source=("http://0pointer.de/lennart/projects/${pkgbase}/${pkgbase}-${pkgver}.tar.xz")
md5sums=('2594093a5d61047bd9cc87e955f86df8')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --sysconfdir=/etc --prefix=/usr --localstatedir=/var \
      --disable-static --with-builtin=dso --enable-null --disable-oss \
      --enable-alsa --enable-gstreamer --enable-pulse --disable-gtk \
      --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

package_libcanberra() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}/usr/lib/libcanberra-gtk.la"
  rm -f "${pkgdir}/usr/lib/gtk-2.0/modules/"*.la
  
  install -m755 -d "${pkgdir}/etc/X11/xinit/xinitrc.d"

  # Split libcanberra-pulse
  mkdir -p pulse-plugin
  mv "${pkgdir}"/usr/lib/${pkgbase}-${pkgver}/${pkgbase}-pulse.* pulse-plugin

  # Split libcanberra-gstreamer
  mkdir -p gstreamer-plugin
  mv "${pkgdir}"/usr/lib/${pkgbase}-${pkgver}/${pkgbase}-gstreamer.* gstreamer-plugin
}

package_libcanberra-pulse() {
  pkgdesc="PulseAudio plugin for libcanberra"
  depends=("$pkgbase=$pkgver-$pkgrel" 'libpulse')

  cd "${srcdir}/${pkgbase}-${pkgver}"

  mkdir -p "${pkgdir}/usr/lib/${pkgbase}-${pkgver}"
  mv pulse-plugin/* "${pkgdir}/usr/lib/${pkgbase}-${pkgver}" 
}

package_libcanberra-gstreamer() {
  pkgdesc="GStreamer plugin for libcanberra"
  depends=("$pkgbase=$pkgver-$pkgrel" 'gstreamer0.10')

  cd "${srcdir}/${pkgbase}-${pkgver}"

  mkdir -p "${pkgdir}/usr/lib/${pkgbase}-${pkgver}"
  mv gstreamer-plugin/* "${pkgdir}/usr/lib/${pkgbase}-${pkgver}"
}
