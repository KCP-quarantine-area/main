
pkgname=geoip
pkgver=1.5.0
pkgrel=1
pkgdesc="Non-DNS IP-to-country resolver C library & utils"
arch=('x86_64')
license=('GPL')
url="http://www.maxmind.com/app/c"
depends=('zlib' 'geoip-database')
backup=('etc/geoip/GeoIP.conf')
options=('!libtool' '!emptydirs')
source=("http://www.maxmind.com/download/geoip/api/c/GeoIP-${pkgver}.tar.gz")
md5sums=('57bc400b5c11057a4cab00e1c5cf3f00')

build() {
  cd "${srcdir}/GeoIP-${pkgver}"

  autoreconf -vi
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --sysconfdir=/etc/geoip

  make
}

check() {
  cd "${srcdir}/GeoIP-${pkgver}"

  make check
}

package() {
  cd "${startdir}/src/GeoIP-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm "${pkgdir}/usr/share/GeoIP/GeoIP.dat"
}

