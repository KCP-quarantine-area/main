
pkgname=networkmanager-openconnect
pkgver=1.0.8
_pkgver=1.0
pkgrel=1
pkgdesc="NetworkManager VPN integration for openconnect"
arch=('x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
depends=('networkmanager' 'openconnect' 'dbus-glib')
makedepends=('intltool') 
options=('!libtool')
install=$pkgname.install
source=("http://ftp.gnome.org/pub/GNOME/sources/NetworkManager-openconnect/${_pkgver}/NetworkManager-openconnect-${pkgver}.tar.xz")
md5sums=('c4d496016bad3f901fccbb571702df32')

build() {
  cd "${srcdir}/NetworkManager-openconnect-${pkgver}"
  
  ./configure --prefix=/usr \
  	--sysconfdir=/etc \
	--libexecdir=/usr/lib/networkmanager \
        --without-gnome \
	--disable-static 
  make 
}

package() {
  cd "${srcdir}/NetworkManager-openconnect-${pkgver}"
  
  make DESTDIR="${pkgdir}" install 
}
