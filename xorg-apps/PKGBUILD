
pkgname=xorg-apps
pkgver=7.8
pkgrel=1
pkgdesc="Various X.Org applications"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxcursor' 'libxkbfile' 'libpng' 'libxft' 'libfontenc' 'libxaw')
makedepends=('pkgconfig' 'xbitmaps' 'xorg-util-macros')
source=("${url}/releases/individual/app/oclock-1.0.3.tar.bz2"
        "${url}/releases/individual/app/luit-1.1.1.tar.bz2"
        "${url}/releases/individual/app/xclock-1.0.5.tar.bz2"
        "${url}/releases/individual/app/xpr-1.0.4.tar.bz2"
        "${url}/releases/individual/app/xwd-1.0.5.tar.bz2"
        "${url}/releases/individual/app/xwud-1.0.4.tar.bz2"
        "${url}/releases/individual/app/x11perf-1.5.4.tar.bz2"
        "${url}/releases/individual/app/xbiff-1.0.3.tar.bz2"
        "${url}/releases/individual/app/xclipboard-1.1.2.tar.bz2"
        "${url}/releases/individual/app/xconsole-1.0.6.tar.bz2"
        "${url}/releases/individual/app/xcursorgen-1.0.5.tar.bz2"
        "${url}/releases/individual/app/xeyes-1.1.1.tar.bz2"
        "${url}/releases/individual/app/xkill-1.0.3.tar.bz2"
        "${url}/releases/individual/app/xload-1.1.2.tar.bz2"
        "${url}/releases/individual/app/xlogo-1.0.4.tar.bz2"
        "${url}/releases/individual/app/xmag-1.0.5.tar.bz2"
        "${url}/releases/individual/app/xmessage-1.0.4.tar.bz2"
        "${url}/releases/individual/app/xcalc-1.0.5.tar.bz2"
        "${url}/releases/individual/app/xman-1.1.2.tar.bz2"
        "${url}/releases/individual/app/xedit-1.2.1.tar.bz2"
        'LICENSE'
        'luit.patch')
md5sums=('f73ad0fa3fda1efe1036e1f48f8a3953'
         'c4a3664e08e5a47c120ff9263ee2f20c'
         'eaf124bbc9d13e1a12a29faaa2ed3612'
         '6101f04731ffd40803df80eca274ec4b'
         '2113126f9ac9c02bb8547c112c5d037e'
         '3025b152b4f13fdffd0c46d0be587be6'
         '5c3c7431a38775caaea6051312a49bc9'
         'f38a0c63c2a7ed6fb9d6f8884785b5c9'
         'f53f9276c0d009b539d7f64c6db9f0ff'
         '5f2f9d97bd078715dffea2fd05b53544'
         '09f56978a62854534deacc8aa8ff3031'
         'a3035dcecdbdb89e864177c080924981'
         'e7f0d57b6ba49c384e9cf8c9ff3243c1'
         'b9e9808db18acecf4cdec134d86b157c'
         '4ce21861f7b9398805beba8b62d8ff8e'
         '280b81cb09d1903aa868a058d90128ad'
         '2dd5ae46fa18abc9331bc26250a25005'
         '8a5597cb97db0dfcfef9dc7a6238d204'
         '17d89b043083cba9e335379fc61981c0'
         '89b0800691feabe1c8a28554c3f73164'
         '1a3977bcff6dc0fc1086e0650fcea31a'
         '0d95a4e986c7229d9fe0653e7f13833b')

build() {
  cd "${srcdir}"
  patch -p0 -i $srcdir/luit.patch
  
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      # remove disable error on luit fix upstream
      ./configure --prefix=/usr --mandir=/usr/share/man --disable-selective-werror
      make
      popd
    fi
  done
}

package() {
  cd "${srcdir}"
  
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"    
      make DESTDIR="${pkgdir}" install
      popd
    fi
  done
  
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

