
pkgname=wxgtk2.9
pkgver=3.0.2
pkgrel=3
pkgdesc="GTK+ implementation of wxWidgets API for GUI"
arch=('x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'glu' 'webkitgtk2')
makedepends=('gconf')
options=('!emptydirs')
source=("http://downloads.sourceforge.net/wxwindows/wxWidgets-${pkgver}.tar.bz2")
md5sums=('ba4cd1f3853d0cd49134c5ae028ad080')

prepare() {
  cd wxWidgets-${pkgver}
  #patch -p1 -i ../wxGTK-2.9.5-collision.patch
}

build() {
  cd wxWidgets-${pkgver}
  
  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --enable-unicode \
    --enable-graphics_ctx --disable-mediactrl --enable-webview --with-regex=builtin \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys \
    --disable-precomp-headers --disable-gstreamer
  make
  make -C locale allmo
}

package() {
  cd wxWidgets-${pkgver}
  make DESTDIR="${pkgdir}" install
  
  install -D -m644 docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
