
pkgname=python2-gobject
_pkgname=pygobject
pkgver=2.28.6
pkgrel=6
pkgdesc="Python bindings for GObject"
arch=('x86_64')
url="http://www.pygtk.org/"
license=('LGPL')
depends=('python2' 'glib2' 'python2-cairo' 'gobject-introspection')
source=("http://ftp.gnome.org/pub/gnome/sources/pygobject/2.28/${_pkgname}-${pkgver}.tar.bz2"
        'python2-fix-type.patch')
options=('!libtool')
md5sums=('a43d783228dd32899e6908352b8308f3'
         'b33aa47c5361af04fb8a26b9e5a15f6f')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"  
  patch -p1 -i "${srcdir}/python2-fix-type.patch"
  
  export PYTHON=/usr/bin/python2
  ./configure --disable-introspection --prefix=/usr 
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  find "$pkgdir"/usr/share/pygobject -name '*.py' | \
    xargs sed -i "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|"
}
