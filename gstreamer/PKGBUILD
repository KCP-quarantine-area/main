
pkgname=gstreamer
pkgver=1.2.2
pkgrel=1
pkgdesc="GStreamer Multimedia Framework"
arch=('x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('libxml2' 'glib2')
optdepends=('sh: feedback script')
makedepends=('intltool' 'pkgconfig' 'gobject-introspection')
options=('!libtool')
source=("${url}/src/gstreamer/gstreamer-${pkgver}.tar.xz")
md5sums=('4293ca4d8333690d5acdffe3ad354924')

build() {
  cd "${srcdir}/gstreamer-${pkgver}"
  
    ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib \
      --with-package-name="GStreamer (KaOS)" \
      --with-package-origin="http://kaosx.us/" \
      --disable-static
  make
}

check() {
  cd "${srcdir}/gstreamer-${pkgver}"
  
  make check
}

package() {
  cd "${srcdir}/gstreamer-${pkgver}"
  
  make DESTDIR="${pkgdir}" install
}
