
pkgname=gstreamer
pkgver=1.6.2
pkgrel=1
pkgdesc="GStreamer Multimedia Framework"
arch=('x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('libxml2' 'glib2')
optdepends=('sh: feedback script')
makedepends=('intltool' 'pkgconfig' 'gobject-introspection')
options=('!libtool')
source=("http://gstreamer.freedesktop.org/src/gstreamer/gstreamer-${pkgver}.tar.xz")
md5sums=('5e610b5a94c209487310739b39b6c464')

build() {
  cd "${srcdir}/gstreamer-${pkgver}"
  
    ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib \
      --with-package-name="GStreamer (KaOS)" \
      --with-package-origin="http://kaosx.us/" \
      --disable-static
  make
}

check() {
  cd "${srcdir}/gstreamer-${pkgver}"
  
  make check
}

package() {
  cd "${srcdir}/gstreamer-${pkgver}"
  
  make DESTDIR="${pkgdir}" install
}
