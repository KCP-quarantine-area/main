
pkgname=gstreamer0.10-ffmpeg
pkgver=0.10.13
pkgrel=1
pkgdesc="Gstreamer FFMpeg Plugin"
arch=('x86_64')
license=('GPL')
depends=('gstreamer0.10-base>=0.10.34' 'bzip2')
makedepends=('pkgconfig' 'yasm' 'sdl' 'git')
url="http://www.gstreamer.net"
groups=('gstreamer0.10-plugins')
source=("git://anongit.freedesktop.org/gstreamer-sdk/gst-ffmpeg#commit=94e587b9182c6d2fde9a61c9def790c8047453c0")
md5sums=('SKIP')

build() {
  cd "${srcdir}/gst-ffmpeg"
  
  NONCONFIGURE=1 ./autogen.sh --disable-gtk-doc
  
  ./configure --prefix=/usr \
  --sysconfdir=/etc \
  --localstatedir=/var \
  --disable-static \
  --with-ffmpeg-extra-configure="--enable-runtime-cpudetect"
  make
}

check() {
  cd "${srcdir}/gst-ffmpeg"
  
  make check
}

package() {
  cd "${srcdir}/gst-ffmpeg"
  make DESTDIR="${pkgdir}" install 
  
  rm -f ${pkgdir}/usr/lib/gstreamer-0.10/*.{l,}a
}
