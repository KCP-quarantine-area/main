pkgname=os-prober
pkgver=1.57
pkgrel=1
pkgdesc="Utility to detect other operating systems on a set of drives."
url="http://joey.kitenet.net/code/os-prober/"
arch=('x86_64')
license=('GPL')
depends=('sh')
makedepends=('gcc' 'sed')
source=("http://ftp.free.org/pub/frugalware/frugalware-current/source/base/${pkgname}/${pkgname}_${pkgver}.tar.gz"
        'btrfs-detection.patch')
md5sums=('67548b17d55cc32c1168bb5a4061170d'
         'c12d27b045ee044eb57d971ff74aaefe')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # patch to make it recognize systems on Btrfs volumes
  patch -p1 -i "${srcdir}/btrfs-detection.patch"

  # adjust lib dir to allow detection of 64-bit distros
  sed -e "s:/lib/ld\*\.so\*:/lib*/ld*.so*:g" \
      -i os-probes/mounted/common/90linux-distro 

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -D -m755 linux-boot-prober "${pkgdir}/usr/bin/linux-boot-prober"
  install -D -m755 os-prober "${pkgdir}/usr/bin/os-prober"
  install -D -m755 newns "${pkgdir}/usr/lib/os-prober/newns"
  install -D -m755 common.sh "${pkgdir}/usr/share/os-prober/common.sh"
  
  for dir in os-probes os-probes/mounted os-probes/init linux-boot-probes linux-boot-probes/mounted; do
    install -dm755 "$pkgdir/usr/lib/$dir"
    install -m755 -t "$pkgdir/usr/lib/$dir" "$dir"/common/*
    [[ -d "$dir"/x86 ]] && install -m755 -t "$pkgdir/usr/lib/$dir" "$dir"/x86/*
  done

  install -Dm755 os-probes/mounted/powerpc/20macosx "$pkgdir"/usr/lib/os-probes/mounted/20macosx

  install -dm755 "$pkgdir"/var/lib/os-prober
}



