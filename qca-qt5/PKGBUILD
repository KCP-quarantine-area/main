
pkgname=qca-qt5
pkgver=2.1.0.3
pkgrel=1
pkgdesc="Qt Cryptographic Architecture"
arch=('x86_64')
url="http://delta.affinix.com/qca/"
license=('LGPL')
depends=('qt5-base' 'ca-certificates' 'nss')
makedepends=('cmake')
#source=("http://delta.affinix.com/download/qca/2.0/${_pkgname}-${pkgver}.tar.gz")
source=("http://download.kde.org/stable/qca-qt5/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"
        'qt5.5.diff')
md5sums=('e29cc1d8f0292eb28e20b216f52d60fc'
         'ac2dff0a97f6bc58544278fb4eee90cf')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/qt5.5.diff
}

build() {
  cd $srcdir
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DQCA_SUFFIX=qt5 \
    -DQCA_INSTALL_IN_QT_PREFIX=ON
  make 
}

package() {
  cd $srcdir/build
  
  make DESTDIR=${pkgdir} install 
}
