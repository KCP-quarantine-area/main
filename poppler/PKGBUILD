
pkgbase=poppler
pkgname=('poppler' 'poppler-glib' 'poppler-qt')
pkgver=0.43.0
pkgrel=1
arch=('x86_64')
license=('GPL')
makedepends=('libjpeg-turbo' 'gcc-libs' 'cairo' 'libxml2' 'openjpeg' 'qt'
             'pkgconfig' 'lcms2' 'fontconfig' 'icu' 'gobject-introspection')
options=('!libtool')
url="http://poppler.freedesktop.org/"
source=("http://poppler.freedesktop.org/${pkgname}-${pkgver}.tar.xz")
md5sums=('1d2b001663119855cdfbc0713dbfb9c6')


build() {
  cd ${pkgbase}-${pkgver}
  
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --enable-cairo-output \
      --enable-xpdf-headers \
      --enable-libjpeg --enable-zlib \
      --enable-poppler-qt4 \
      --disable-poppler-qt5 \
      --enable-poppler-glib 
      
  make
}

package_poppler() {
  pkgdesc="PDF rendering library based on xpdf 3.0"
  depends=('libjpeg' 'gcc-libs' 'cairo' 'libxml2' 'fontconfig' 'openjpeg' 'lcms2' 'poppler-data')

  cd ${pkgbase}-${pkgver}
  sed -e 's/^glib_subdir =.*/glib_subdir =/' \
      -e 's/^qt4_subdir =.*/qt4_subdir =/' \
      -e 's/^qt5_subdir =.*/qt5_subdir =/' -i Makefile
  make DESTDIR=${pkgdir} install

  rm -f ${pkgdir}/usr/lib/pkgconfig/poppler-{glib,qt4,qt5}.pc
}

package_poppler-glib() {
  pkgdesc="Poppler glib bindings"
  depends=("poppler=${pkgver}" 'glib2')

  cd ${pkgbase}-${pkgver}/poppler
  make DESTDIR=${pkgdir} install-libLTLIBRARIES
  
  cd ../../${pkgbase}-${pkgver}/glib
  make DESTDIR=${pkgdir} install
  
  install -m755 -d ${pkgdir}/usr/lib/pkgconfig
  install -m644 ../poppler-glib.pc ${pkgdir}/usr/lib/pkgconfig/
  rm -f ${pkgdir}/usr/lib/libpoppler.*
  rm -f ${pkgdir}/usr/bin/poppler-glib-demo
}

package_poppler-qt() {
  pkgdesc="Poppler Qt bindings"
  depends=("poppler=${pkgver}" 'qt')

  cd ${pkgbase}-${pkgver}/poppler
  make DESTDIR=${pkgdir} install-libLTLIBRARIES
  
  cd ../../${pkgbase}-${pkgver}/qt4
  make DESTDIR=${pkgdir} install
  
  install -m755 -d ${pkgdir}/usr/lib/pkgconfig
  install -m644 ../poppler-qt4.pc "${pkgdir}/usr/lib/pkgconfig/"
  rm -f ${pkgdir}/usr/lib/libpoppler.*
}


