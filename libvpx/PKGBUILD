
pkgname=libvpx
pkgver=1.2.0
pkgrel=1
pkgdesc="The VP8 Codec SDK"
arch=('x86_64')
url="http://www.webmproject.org/"
license=('BSD')
depends=('glibc')
makedepends=('yasm')
source=("http://sourceforge.net/projects/kdeos/files/Sources/libvpx/libvpx-${pkgver}.tar.xz")
md5sums=('2a5228c1d3807cf1313ec41914debb5d')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    ./configure --prefix=/usr \
        --enable-vp8 \
        --enable-runtime-cpu-detect \
        --enable-shared \
        --enable-postproc \
        --enable-pic \
        --disable-install-docs \
        --disable-install-srcs
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DIST_DIR="$pkgdir/usr" install
    
    chmod -R o+r "${pkgdir}/usr/include/vpx"
    install -D -m 0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
