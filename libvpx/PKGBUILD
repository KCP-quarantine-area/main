
pkgname=libvpx
pkgver=1.3.0
pkgrel=2
pkgdesc="The VP8 Codec SDK"
arch=('x86_64')
url="http://www.webmproject.org/"
license=('BSD')
depends=('glibc')
makedepends=('yasm')
source=("https://webm.googlecode.com/files/libvpx-v${pkgver}.tar.bz2")
#source=("$pkgname::git+http://code.google.com/p/webm.libvpx#tag=v$pkgver")
md5sums=('14783a148872f2d08629ff7c694eb31f')

build() {
    cd ${srcdir}/libvpx-v${pkgver}

    ./configure --prefix=/usr \
        --enable-vp8 \
        --enable-vp9 \
        --enable-runtime-cpu-detect \
        --enable-shared \
        --enable-postproc \
        --enable-pic \
        --disable-install-docs \
        --disable-install-srcs
    make
}

package() {
    cd ${srcdir}/libvpx-v${pkgver}
    make DIST_DIR="$pkgdir/usr" install
    
    chmod -R o+r "${pkgdir}/usr/include/vpx"
    install -D -m 0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
