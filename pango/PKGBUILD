
pkgname=pango
pkgver=1.36.0
pkgrel=1
pkgdesc="A library for layout and rendering of text"
arch=('x86_64')
license=('LGPL')
depends=('cairo' 'libxft' 'libthai' 'harfbuzz')
makedepends=('gobject-introspection' 'libxt' 'fontconfig')
options=('!libtool' '!emptydirs')
install=pango.install
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/1.36/${pkgname}-${pkgver}.tar.xz")
url="http://www.pango.org/"
md5sums=('5b50052df366e6d7bf08fdd80d58acce')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --with-included-modules=basic-fc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/etc/pango"
}
