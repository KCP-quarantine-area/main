
pkgname=lilv
pkgver=0.20.0
pkgrel=1
pkgdesc="A library to make the use of LV2 plugins as simple as possible for applications"
arch=('x86_64')
url="http://drobilla.net/software/lilv/"
license=('custom:ISC')
depends=('python2-numpy' 'sratom' 'jack')
makedepends=('swig')
source=("http://download.drobilla.net/$pkgname-$pkgver.tar.bz2")
md5sums=('f88419fa70cc96dfdc7e0bf3cd09b180')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export PYTHON="/usr/bin/python2"
  sed -i "/ldconfig/d" wscript

  python2 waf configure --prefix=/usr \
                        --configdir=/etc \
                        --dyn-manifest \
                        --bindings

  python2 waf build $MAKEFLAGS
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf install --destdir="$pkgdir"

  install -Dm644 COPYING  "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

