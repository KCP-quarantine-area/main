
pkgname=fontconfig
pkgver=2.10.92
pkgrel=1
pkgdesc="A library for configuring and customizing font access"
arch=('x86_64')
url="http://www.fontconfig.org/release/"
license=('custom')
depends=('expat' 'freetype2>=2.4.11')
options=('!libtool')
install='fontconfig.install'
source=("http://www.fontconfig.org/release/${pkgname}-${pkgver}.tar.gz"
        'git_fixes.diff'
        '29-replace-bitmap-fonts.conf')
md5sums=('5738838ef01f4c77fe33eeac2ceafe4d'
         'dc9b419cc1eaf3b8fb6ade9a9363105a'
         '526b95ebde1b07eb5dbc5d71fb613ed1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i "${srcdir}/git_fixes.diff"

  libtoolize -f
  autoreconf -fi

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-templatedir=/etc/fonts/conf.avail \
    --with-xmldir=/etc/fonts \
    --localstatedir=/var \
    --disable-static \
    --with-default-fonts=/usr/share/fonts \
    --with-add-fonts=/usr/share/fonts
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  
  install -m644 "$srcdir/29-replace-bitmap-fonts.conf" "$pkgdir/etc/fonts/conf.avail"
  ln -s ../conf.avail/29-replace-bitmap-fonts.conf \
    "$pkgdir/etc/fonts/conf.d/29-replace-bitmap-fonts.conf"

  #Install license
  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname"
}
