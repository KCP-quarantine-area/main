
pkgname=fontconfig
pkgver=2.11.0
pkgrel=1
pkgdesc="A library for configuring and customizing font access"
arch=('x86_64')
url="http://www.fontconfig.org/release/"
license=('custom')
depends=('expat' 'freetype2>=2.4.11')
options=('!libtool')
install='fontconfig.install'
source=("http://www.fontconfig.org/release/${pkgname}-${pkgver}.tar.gz")
md5sums=('84278204cd7f36adbea7ad8094e039ac')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  libtoolize -f
  autoreconf -fi

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-templatedir=/etc/fonts/conf.avail \
    --with-xmldir=/etc/fonts \
    --localstatedir=/var \
    --disable-static \
    --with-default-fonts=/usr/share/fonts \
    --with-add-fonts=/usr/share/fonts
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  
  #install -m644 "$srcdir/29-replace-bitmap-fonts.conf" "$pkgdir/etc/fonts/conf.avail"
  #ln -s ../conf.avail/29-replace-bitmap-fonts.conf \
  #  "$pkgdir/etc/fonts/conf.d/29-replace-bitmap-fonts.conf"

  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname"
}
