
pkgname=pth
pkgver=2.0.7
pkgrel=6
pkgdesc="The GNU Portable Threads."
arch=('x86_64')
url="http://www.gnu.org/software/pth/"
license=('LGPL')
depends=('glibc' 'awk')
options=('!libtool' '!makeflags')
source=("ftp://ftp.gnu.org/gnu/pth/${pkgname}-${pkgver}.tar.gz")
md5sums=('9cb4a25331a4c4db866a31cbe507c793')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
       --sysconfdir=/etc \
       --localstatedir=/var \
       --mandir=/usr/share/man \
       --disable-static \
       --enable-shared \
       --with-pic
       
  sed -i "s|awk=''|awk=/bin/awk|g" shtool 
  
  make 
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install 
}
