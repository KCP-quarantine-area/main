
pkgname=pth
pkgver=2.0.7
pkgrel=5
pkgdesc="The GNU Portable Threads."
arch=('x86_64')
url="http://www.gnu.org/software/pth/"
license=('LGPL')
depends=('glibc' 'awk')
options=('!libtool' '!makeflags')
source=("ftp://ftp.gnu.org/gnu/pth/${pkgname}-${pkgver}.tar.gz")
md5sums=('9cb4a25331a4c4db866a31cbe507c793')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
       --sysconfdir=/etc \
       --localstatedir=/var \
       --mandir=/usr/share/man \
       --disable-static \
       --enable-shared \
       --with-pic
       
  sed -i "s|awk=''|awk=/bin/awk|g" shtool # "Cannot find a reasonable Awk"
  
  make 
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install 
}
