
pkgname=libxcursor
pkgver=1.1.14
pkgrel=1
pkgdesc="X cursor management library"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxfixes' 'libxrender')
makedepends=('xorg-util-macros')
options=('!libtool')
source=("${url}/releases/individual/lib/libXcursor-${pkgver}.tar.bz2")
md5sums=('1e7c17afbbce83e2215917047c57d1b3')

build() {
  cd "${srcdir}/libXcursor-${pkgver}"
  
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/libXcursor-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
