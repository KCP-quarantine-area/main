
pkgname=xf86-input-wacom
pkgver=0.27.0
pkgrel=1
pkgdesc="X.Org Wacom tablet driver."
arch=('x86_64')
url="http://linuxwacom.sourceforge.net/"
license=('GPL')
backup=('etc/X11/xorg.conf.d/50-wacom.conf')
depends=('libxi' 'libxrandr' 'libxinerama')
makedepends=('xorg-server' 'libxext' 'resourceproto' 'scrnsaverproto')
options=('!libtool')
source=("http://downloads.sourceforge.net/project/linuxwacom/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        '70-wacom.rules')
md5sums=('f75b374c47969fb9b21d9c2134396400'
         'cc46dcb399d5ee62c9feb610c60d1270')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  ./configure --prefix=/usr --with-xorg-conf-dir=/etc/X11/xorg.conf.d
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 ../70-wacom.rules "$pkgdir/usr/lib/udev/rules.d/70-wacom.rules"
}

