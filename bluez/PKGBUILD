
pkgname=bluez
pkgver=5.13
pkgrel=2
pkgdesc="Libraries and tools for the Bluetooth protocol stack"
url="http://www.bluez.org/"
arch=('x86_64')
license=('GPL2')
depends=('libical' 'dbus' 'glib2' 'systemd')
makedepends=('gstreamer0.10-base' 'libsndfile' 'libusbx')
optdepends=("cups: CUPS backend")
conflicts=('obexd-client' 'bluez-hcidump')
provides=('obexd-client' 'bluez-hcidump')
replaces=('obexd-client' 'bluez-hcidump')
options=('!libtool')
backup=('etc/dbus-1/system.d/bluetooth.conf')
source=("http://www.kernel.org/pub/linux/bluetooth/${pkgname}-${pkgver}.tar.xz"
        'bluetooth.modprobe')
md5sums=('c68f8cb270110256a684403ee43b5f79'
         '671c15e99d7154c2df987b71c5851b3d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib \
    --enable-sixaxis \
    --enable-library
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver} 
  make DESTDIR=${pkgdir} install
  
  install -dm755 ${pkgdir}/etc/bluetooth
  install -Dm644 ${srcdir}/${pkgbase}-${pkgver}/src/main.conf ${pkgdir}/etc/bluetooth/main.conf

  install -dm755 ${pkgdir}/usr/share/doc/${pkgbase}/dbus-apis
  cp -a doc/*.txt ${pkgdir}/usr/share/doc/${pkgbase}/dbus-apis/

  install -dm755 ${pkgdir}/usr/lib/modprobe.d
  install -Dm644 ${srcdir}/bluetooth.modprobe ${pkgdir}/usr/lib/modprobe.d/bluetooth-usb.conf
}
