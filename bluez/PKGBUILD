
pkgname=bluez
pkgver=5.35
pkgrel=1
pkgdesc="Libraries and tools for the Bluetooth protocol stack"
url="http://www.bluez.org/"
arch=('x86_64')
license=('GPL2')
depends=('libical' 'dbus' 'glib2' 'systemd')
makedepends=('libusbx')
optdepends=("cups: CUPS backend")
conflicts=('obexd-client' 'bluez-hcidump')
provides=('obexd-client' 'bluez-hcidump')
replaces=('obexd-client' 'bluez-hcidump')
options=('!libtool')
backup=('etc/dbus-1/system.d/bluetooth.conf'
        'etc/bluetooth/main.conf')
install=bluez.install
source=("http://www.kernel.org/pub/linux/bluetooth/${pkgname}-${pkgver}.tar.xz"
        'bluetooth.modprobe'
        'obexd.patch')
md5sums=('9981441d7a61e2ad849b01106fa73445'
         '671c15e99d7154c2df987b71c5851b3d'
         '4a78cf07e98bc1961774a0971dd5d7c9')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # http://www.hadess.net/2013/11/bluetooth-file-sharing-obexpush-in.html
  patch -p1 -i ${srcdir}/obexd.patch
  
  autoreconf -vfi
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib \
    --enable-sixaxis \
    --enable-library
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver} 
  make DESTDIR=${pkgdir} install
  
  install -dm755 ${pkgdir}/etc/bluetooth
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/src/main.conf ${pkgdir}/etc/bluetooth/main.conf

  install -dm755 ${pkgdir}/usr/share/doc/${pkgname}/dbus-apis
  cp -a doc/*.txt ${pkgdir}/usr/share/doc/${pkgname}/dbus-apis/

  install -dm755 ${pkgdir}/usr/lib/modprobe.d
  install -Dm644 ${srcdir}/bluetooth.modprobe ${pkgdir}/usr/lib/modprobe.d/bluetooth-usb.conf
}
