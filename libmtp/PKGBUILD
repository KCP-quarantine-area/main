
pkgname=libmtp
pkgver=1.1.10
pkgrel=1
pkgdesc="Library implementation of the Media Transfer Protocol"
arch=('x86_64')
url="http://libmtp.sourceforge.net"
license=('LGPL')
depends=('libusb' 'libgcrypt')
options=('!libtool')
source=("http://downloads.sourceforge.net/libmtp/${pkgname}-${pkgver}.tar.gz")
md5sums=('a82e999363c2504025fb50d2e095ec23')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  ./configure --prefix=/usr --with-udev=/usr/lib/udev
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make DESTDIR="${pkgdir}" install
  # systemd errors, clean line in udev rules
  sed -i "/^Unable to open/d" ${pkgdir}/usr/lib/udev/rules.d/69-libmtp.rules
}

