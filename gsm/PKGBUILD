
pkgname=gsm
pkgver=1.0.14
_pkgver=1.0-pl14
pkgrel=1
pkgdesc="Shared libraries for GSM 06.10 lossy speech compression"
arch=('x86_64')
url="http://www.quut.com/gsm/"
license=('custom')
depends=('glibc')
source=("http://www.quut.com/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'gsm.patch')
md5sums=('4cbb4e7b73842de3d12d30b0d74200d4'
         '30f46c92d0a5e5654258e707a9e8b5a6')

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}/"
  patch -p0 -i "${srcdir}/${pkgname}.patch"

  # flags for shared lib
  CFLAGS="${CFLAGS} -fPIC"
  make CCFLAGS="-c ${CFLAGS}"
}

package() {
  cd "${srcdir}/${pkgname}-${_pkgver}/"

  install -m755 -d "${pkgdir}"/usr/{bin,lib,include/gsm,share/{licenses/${pkgname},man/man{1,3}}}

  make -j1 INSTALL_ROOT="${pkgdir}/usr" \
    GSM_INSTALL_INC="${pkgdir}/usr/include/gsm" \
    GSM_INSTALL_MAN="${pkgdir}/usr/share/man/man3" \
    TOAST_INSTALL_MAN="${pkgdir}/usr/share/man/man1" \
    install

  install -m644 COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}