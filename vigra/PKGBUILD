
pkgname=vigra
pkgver=1.9.0
pkgrel=4
pkgdesc="Computer vision library"
arch=('x86_64')
url="http://hci.iwr.uni-heidelberg.de/vigra/"
license=('custom:MIT')
depends=('libpng' 'libtiff' 'gcc-libs' 'sh' 'hdf5' 'fftw' 'openexr')
makedepends=('cmake' 'python2-nose' 'doxygen' 'python2-sphinx' 'boost' 'python2-numpy')
optdepends=('python2: for python bindings'
            'boost-libs: for python bindings')
options=('!libtool')
source=("http://kogs-www.informatik.uni-hamburg.de/~koethe/vigra/${pkgname}-${pkgver}-src.tar.gz")
md5sums=('b6155afe1ea967917d2be16d98a85404')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
      -DPYTHON_EXECUTABLE=/usr/bin/python2 \
      -DWITH_OPENEXR=1 \
      -DWITH_VIGRANUMPY=1 \
      -DDOCINSTALL=share/doc 
  make
}

check() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  
  make -k check || /bin/true 
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -D -m644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
